Функция СохраняемаяНастройкаФормы(выхНаименование, выхИменаСвойств) Экспорт 
	выхИменаСвойств = "Форма.ПроверятьКороткиеЛитералы, Форма.Автооткрытие, Форма.ИзмененныеПозже, Форма.ФильтрПодсистем";
	Возврат Неопределено;
КонецФункции

Процедура ЗагрузитьНастройкуВФорме(НастройкаФормы, ДопПараметры) Экспорт 
	
	ирКлиент.ЗагрузитьНастройкуФормыЛкс(ЭтаФорма, НастройкаФормы); 
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ирКлиент.Форма_ПриОткрытииЛкс(ЭтаФорма);
	ЭтаФорма.КоличествоНайдено = ОшибкиМодуля.Количество();
	//ЭлементыФормы.ИзмененныеПозже.Доступность = мПлатформа.мСвязанныйКонфигуратор <> Неопределено;
	ПриЛюбомОткрытии();

КонецПроцедуры

Процедура ПриЛюбомОткрытии() Экспорт
	ЭтаФорма.ДатаОбновленияКэша = ирОбщий.ДатаОбновленияКэшаМодулейЛкс();
	НовыйТекст = "";
	Если Не ВсеМодули Тогда
		НовыйТекст = ПараметрМодуль;
	КонецЕсли;
	ирОбщий.ОбновитьТекстПослеМаркераЛкс(ЭтаФорма.Заголовок,, НовыйТекст, ": ");
	//Обновить(); // Не помогает обновить отображаемый заголовок сразу
КонецПроцедуры

Процедура ПриЗакрытии()
	ирКлиент.Форма_ПриЗакрытииЛкс(ЭтаФорма);
КонецПроцедуры

Процедура ОшибкиМодуляПриАктивизацииСтроки(Элемент)
	ЭлементыФормы.ПолеТекстаВхождения.УстановитьТекст("");
	ТекущаяСтрока = Элемент.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ирКлиент.ТабличноеПолеПриАктивизацииСтрокиЛкс(ЭтаФорма, Элемент);
	ирКлиент.РазобратьПозициюМодуляВСтрокеТаблицыЛкс(ТекущаяСтрока, ИмяМодуляИзСтрокиОшибки(ТекущаяСтрока), ЭлементыФормы.ПолеТекстаВхождения);
КонецПроцедуры

//.
// Параметры:
//   СтрокаОшибки - ОбработкаТабличнаяЧастьСтрока.ирКлсПолеТекстаПрограммы.ОшибкиМодуля - 
// Возвращаемое значение:
//   Строка - 
Функция ИмяМодуляИзСтрокиОшибки(Знач СтрокаОшибки = Неопределено) Экспорт
	Если СтрокаОшибки = Неопределено Тогда
		СтрокаОшибки = ЭлементыФормы.ОшибкиМодуля.ТекущаяСтрока;
	КонецЕсли;
	Модуль = СтрокаОшибки.Модуль;
	Если Не ЗначениеЗаполнено(Модуль) Тогда
		Модуль = ПараметрМодуль;
	КонецЕсли;
	Возврат Модуль;
КонецФункции

Процедура ОшибкиМодуляПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки) Экспорт
	ирКлиент.ТабличноеПолеПриВыводеСтрокиЛкс(ЭтаФорма, Элемент, ОформлениеСтроки, ДанныеСтроки);
КонецПроцедуры

Процедура ОшибкиМодуляВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	Если Колонка = Элемент.Колонки.Ссылка Тогда
		ирКлиент.ПоказатьСсылкуНаСтрокуМодуляЛкс(ВыбраннаяСтрока.Ссылка);
	Иначе 
		Если Не ирКлиент.ОткрытьСсылкуСтрокиМодуляЛкс(ВыбраннаяСтрока.Ссылка,, Автооткрытие) Тогда 
			ИмяМодуля = ИмяМодуляИзСтрокиОшибки(ВыбраннаяСтрока);
			Если ИмяМодуля = мПлатформа.ИмяДинамическогоМодуля() Тогда 
				МодульМетаданных = мМодульМетаданных;
				ФормаВладелец.Активизировать();           
				ФормаВладелец.ТекущийЭлемент = ПолеТекста.ЭлементФормы;  
				ПолеТекстаЛ = ПолеТекста;
			Иначе
				МодульМетаданных = мПлатформа.МодульМетаданныхИзКэша(ИмяМодуля);
				ПолеТекстаЛ = ирКлиент.ОткрытьПолеТекстаМодуляКонфигурацииЛкс(ИмяМодуля).ПолеТекста;
			КонецЕсли;
			ВыделитьСловоОшибки(ВыбраннаяСтрока, МодульМетаданных, ПолеТекстаЛ);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ВыделитьСловоОшибки(ВыбраннаяСтрока, Знач МодульМетаданных, Знач ПолеТекстаЛ) Экспорт
	МетодМодуля = мПлатформа.СтрокаМетодаМодуляПоИмени(МодульМетаданных, ВыбраннаяСтрока.Метод);
	Если МетодМодуля = Неопределено Тогда
		НачальнаяПозицияВПоле = мМодульМетаданных.ПозицияПрограммы;
	Иначе 
		НачальнаяПозицияВПоле = МетодМодуля.ПозицияТела;
	КонецЕсли;
	НачальнаяПозицияВПоле = НачальнаяПозицияВПоле + ВыбраннаяСтрока.ПозицияВМетоде;
	ДлинаСлова = СтрДлина(ВыбраннаяСтрока.Слово) - ?(Прав(ВыбраннаяСтрока.Слово, 1) = "(", 1, 0);
	ПолеТекстаЛ.УстановитьГраницыВыделения(НачальнаяПозицияВПоле, НачальнаяПозицияВПоле + ДлинаСлова,,, Истина);
КонецПроцедуры

Процедура ДействияФормыОбновить(Кнопка)
	
	ОбновитьОшибки();
	
КонецПроцедуры

Процедура ПослеПрерыванияПроверки()
	Если Истина
		И ОшибкиМодуля.Количество() = 0
		И мПлатформа.мСвязанныйКонфигуратор <> Неопределено
	Тогда
		ирКлиент.ОткрытьСсылкуВКонфигуратореЛкс("<ВывестиСообщение> Ошибок не найдено");
	КонецЕсли;
КонецПроцедуры

//.
// Параметры:
//    ПолныйПересчет - Булево - 
Процедура ОбновитьОшибки(Знач ПолныйПересчет = Ложь) Экспорт
	
	Если Истина
		И ПараметрМодуль <> ""
		И (Ложь
			Или мМодульМетаданных = Неопределено
			Или ПараметрМодуль <> мМодульМетаданных.Имя)
	Тогда
		ЗагрузитьМодульМетаданных(мПлатформа.МодульМетаданныхИзКэша(ПараметрМодуль));
	КонецЕсли;
	Если мМодульМетаданных = Неопределено Тогда
		ЭтаФорма.ВсеМодули = Истина;
	КонецЕсли;
	ПриЛюбомОткрытии();
	Если мПлатформа.ИдентификаторыПроцессовОтладчиков().Количество() > 0 Тогда 
		ирОбщий.СообщитьЛкс("Возможно подключен отладчик и тогда он замедляет проверку.");
	КонецЕсли; 
	Если ВсеМодули Тогда
		ОшибкиМодуля.Очистить();
		ЭлементыФормы.ОшибкиМодуля.Колонки.Модуль.Видимость = Истина;
		Если ФильтрПодсистем.Количество() > 0 Тогда
			ОбъектыВыбранныхПодсистем = ирОбщий.ОбъектыПодсистемЛкс(ФильтрПодсистем);
		КонецЕсли;
		РасширениеФайлаМодуля = "txt";
		ФайлыМодулей = НайтиФайлы(мПлатформа.ПапкаКэшаМодулей.ПолноеИмя, "*." + РасширениеФайлаМодуля, Истина); 
		ОтобранныеФайлы = Новый СписокЗначений;
		СвежиеФайлы = Новый Соответствие;
		Индикатор = ирОбщий.ПолучитьИндикаторПроцессаЛкс(ФайлыМодулей.Количество(), "Отбор файлов");
		Для Каждого Файл Из ФайлыМодулей Цикл
			ирОбщий.ОбработатьИндикаторЛкс(Индикатор);
			ВремяИзменения = Файл.ПолучитьВремяИзменения();
			Если ВремяИзменения < ИзмененныеПозже Тогда
				Продолжить;
			КонецЕсли;
			ИмяМодуля = мПлатформа.ИмяМодуляИзИмениФайла(Файл.ПолноеИмя);
			Если ФильтрПодсистем.Количество() > 0 Тогда
				ПолноеИмяМД = ирОбщий.ПервыйФрагментЛкс(ИмяМодуля, ".Форма.Модуль");
				ПолноеИмяМД = ирОбщий.ПервыйФрагментЛкс(ПолноеИмяМД, ".Модуль");
				ПолноеИмяМД = ирОбщий.ПоследнийФрагментЛкс(ПолноеИмяМД, " ");
				ОбъектМД = ирКэш.ОбъектМДПоПолномуИмениЛкс(ирОбщий.ПервыеФрагментыЛкс(ПолноеИмяМД));
				Если ОбъектМД <> Неопределено Тогда
					Если ОбъектыВыбранныхПодсистем[ОбъектМД] = Неопределено тогда
						Продолжить;
					КонецЕсли;
				Иначе
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			Если ТекущаяДата() - ВремяИзменения < 600 Тогда
				СвежиеФайлы[Файл.Имя] = 1;
			КонецЕсли;
			ОтобранныеФайлы.Добавить(Файл, Файл.Имя);
		КонецЦикла;
		ирОбщий.ОсвободитьИндикаторПроцессаЛкс();
		ОтобранныеФайлы.СортироватьПоПредставлению();
		Индикатор = ирОбщий.ПолучитьИндикаторПроцессаЛкс(ОтобранныеФайлы.Количество(), "Проверка");
		Для Каждого ЭлементСписка Из ОтобранныеФайлы Цикл
			Файл = ЭлементСписка.Значение;
			ирОбщий.ОбработатьИндикаторЛкс(Индикатор);
			ИмяМодуля = мПлатформа.ИмяМодуляИзИмениФайла(Файл.ПолноеИмя);
			АнализаторМодуля = мПлатформа.ПолеТекстаМодуля(ИмяМодуля);
			Если СвежиеФайлы[Файл.Имя] <> Неопределено Тогда
				НовыйТекст = ирОбщий.ПрочитатьТекстИзФайлаЛкс(Файл.ПолноеИмя);
				Если НовыйТекст <> АнализаторМодуля.мОригинальныйТекст Тогда
					мПлатформа.ПодготовитьМодульМетаданных(АнализаторМодуля.мМодульМетаданных.СтруктураТипа,,, НовыйТекст);
				КонецЕсли;
			КонецЕсли;
			Если АнализаторМодуля.ПроверитьВыраженияТекста(,, ПолныйПересчет, ПроверятьКороткиеЛитералы, Ложь) Тогда 
				Если мПлатформа.ЛиСохранятьОшибкиМодуля(АнализаторМодуля.мМодульМетаданных) Тогда
					мПлатформа.ЗаписатьМодульВКэш(АнализаторМодуля.мМодульМетаданных);
				КонецЕсли;
			КонецЕсли;
			Если АнализаторМодуля.ОшибкиМодуля.Количество() > 0 Тогда
				ОбновитьОтображение = ОшибкиМодуля.Количество() < 50;
				НовыеЗначения = Новый Структура("Модуль", ИмяМодуля); 
				ирОбщий.ЗагрузитьВТаблицуЗначенийЛкс(АнализаторМодуля.ОшибкиМодуля, ОшибкиМодуля,, НовыеЗначения);
				Если ОбновитьОтображение Тогда
					ЭлементыФормы.ОшибкиМодуля.ОбновитьСтроки();
				КонецЕсли;
			КонецЕсли;
			ЭтаФорма.КоличествоНайдено = ОшибкиМодуля.Количество();
		КонецЦикла;
		ирОбщий.ОсвободитьИндикаторПроцессаЛкс();
	Иначе
		ЭлементыФормы.ОшибкиМодуля.Колонки.Модуль.Видимость = Ложь;
		ПодключитьОбработчикОжидания("ПослеПрерыванияПроверки", 0.1, Истина); 
		//ирКлиент.ПолеТекстаМодуляБезСтруктурыТипаЛкс(мИмяМодуля, мОригинальныйТекст);
		Если ЭлементыФормы.ОшибкиМодуля.ТекущаяСтрока <> Неопределено Тогда
			ЗагрузитьМетодМодуляПоПозиции(ЭлементыФормы.ОшибкиМодуля.ТекущаяСтрока.Позиция);
		КонецЕсли; 
		Если ПроверитьВыраженияТекста(ЭлементыФормы.ОшибкиМодуля, ЭтаФорма, ПолныйПересчет, ПроверятьКороткиеЛитералы) Тогда 
			Если мПлатформа.ЛиСохранятьОшибкиМодуля(мМодульМетаданных) Тогда
				мПлатформа.ЗаписатьМодульВКэш(мМодульМетаданных);
			КонецЕсли;
		КонецЕсли;
		Если ОшибкиМодуля.Количество() > 0 Тогда
			ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.ОшибкиМодуля;
			Если ПараметрИмяМетода <> Неопределено Тогда
				СтрокаМетода = ОшибкиМодуля.Найти(ПараметрИмяМетода, "Метод");
				Если СтрокаМетода <> Неопределено Тогда
					ЭлементыФормы.ОшибкиМодуля.ТекущаяСтрока = СтрокаМетода;
				КонецЕсли;
				ЭтаФорма.ПараметрИмяМетода = Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 
КонецПроцедуры

Процедура ВнешнееСобытие(Источник, Событие, Данные) Экспорт
	
	ирКлиент.Форма_ВнешнееСобытиеЛкс(ЭтаФорма, Источник, Событие, Данные);

КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник) Экспорт
	
	ирКлиент.Форма_ОбработкаОповещенияЛкс(ЭтаФорма, ИмяСобытия, Параметр, Источник); 

КонецПроцедуры
 
Процедура КлсКомандаНажатие(Кнопка) Экспорт 
	
	ирКлиент.УниверсальнаяКомандаФормыЛкс(ЭтаФорма, Кнопка);
	
КонецПроцедуры

Процедура НадписьКэшМодулейНажатие(Элемент)
	ПолучитьФорму("Настройки", ФормаВладелец).Открыть();
КонецПроцедуры

Процедура ПриПовторномОткрытии(СтандартнаяОбработка)
	ПриЛюбомОткрытии();
КонецПроцедуры

Процедура ДействияФормыОбновитьВсе(Кнопка)
	ОбновитьОшибки(Истина);
КонецПроцедуры

Процедура ДействияФормыПодавитьПроверки(Кнопка)
	ВыбраннаяСтрока = ЭлементыФормы.ОшибкиМодуля.ТекущаяСтрока;
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ИмяМодуляИзСтрокиОшибки(ВыбраннаяСтрока) = мПлатформа.ИмяДинамическогоМодуля() Тогда 
		ФормаВладелец.Активизировать();           
		ФормаВладелец.ТекущийЭлемент = ПолеТекста.ЭлементФормы;  
		ВыделитьСловоОшибки(ВыбраннаяСтрока, мМодульМетаданных, ПолеТекста);
		ВыделениеДвумерное = ПолеТекста.ВыделениеДвумерное();
		ДлинаСтроки = СтрДлина(ПолеТекста.ПолучитьСтроку(ВыделениеДвумерное.КонечнаяСтрока));
		ВыделениеДвумерное.НачальнаяКолонка = ДлинаСтроки + 1;
		ВыделениеДвумерное.КонечнаяКолонка = ВыделениеДвумерное.НачальнаяКолонка;
		ПолеТекста.УстановитьВыделениеДвумерное(ВыделениеДвумерное);
		ПолеТекста.ВыделенныйТекст(" " + ИнструкцияПодавленияПроверки(ВыбраннаяСтрока.ТипОшибки));
	Иначе 
		СсылкаПодавленияПроверок = СтрЗаменить(ВыбраннаяСтрока.Ссылка, ")}", "!ПодавитьПроверки)}");
		ирКлиент.ОткрытьСсылкуСтрокиМодуляЛкс(СсылкаПодавленияПроверок, Истина);
	КонецЕсли;
КонецПроцедуры

Процедура ФильтрПодсистем_НачалоВыбора(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ФормаВыбора = ирКэш.Получить().ПолучитьФорму("ВыборПодсистемы", Элемент);
	ФормаВыбора.МножественныйВыбор = Истина;
	ФормаВыбора.НачальноеЗначениеВыбора = ФильтрПодсистем;
	ФормаВыбора.РежимВыбора = Истина;
	ФормаВыбора.Открыть();
КонецПроцедуры

Процедура ФильтрПодсистем_ПриИзменении(Элемент)
	ирКлиент.ПолеВводаСИсториейВыбора_ПриИзмененииЛкс(Элемент, ЭтаФорма);
КонецПроцедуры

Процедура ФильтрПодсистемОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ирКлиент.ФильтрПодсистемОбработкаВыбораЛкс(ЭтаФорма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
КонецПроцедуры

Процедура ФильтрПодсистемНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	ирОбщий.ПолеВводаСИсториейВыбора_ОбновитьСписокЛкс(Элемент, ЭтаФорма);
КонецПроцедуры

ирКлиент.ИнициироватьФормуЛкс(ЭтаФорма, "Обработка.ирКлсПолеТекстаПрограммы.Форма.ОшибкиМодуля");
Автооткрытие = Истина; 

