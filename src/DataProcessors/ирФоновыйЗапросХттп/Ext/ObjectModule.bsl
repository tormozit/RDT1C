#Если Клиент Тогда 
//ирПортативный Перем ирПортативный Экспорт;
//ирПортативный Перем ирОбщий Экспорт;
//ирПортативный Перем ирСервер Экспорт;
//ирПортативный Перем ирКэш Экспорт;
//ирПортативный Перем ирКлиент Экспорт;
	
Перем мБраузер Экспорт; // Кэш
Перем мЛиПроверочный; // Булево
Перем мОписаниеЗадания;
Перем мЛиПотоковый;

Функция Браузер() Экспорт
	Возврат мБраузер;
КонецФункции

// Ответ нужно ловить в обработчике onclick браузера.
// Отменяет текущий активный запрос.
// Параметры:
//   Адрес - Строка - 
//   ТелоЗапросаСтрока - Строка - 
//   Заголовки - Соответствие - 
Процедура ОтправитьЗапрос(Знач Адрес, Знач ТелоЗапросаСтрока, Знач Заголовки, Знач ЛиПотоковый = Ложь, Знач ЛиПроверочный = Ложь) Экспорт
	мЛиПроверочный = ЛиПроверочный;
	ЗаголовкиМассив = Новый Массив;
	Для Каждого ЗаголовкиЭлемент Из Заголовки Цикл
		ЗаголовкиМассив.Добавить(Новый Структура("name,value", ЗаголовкиЭлемент.Ключ, ЗаголовкиЭлемент.Значение));
	КонецЦикла;
	ЗаголовкиТекст = ирОбщий.ОбъектВТекстЖСОНЛкс(ЗаголовкиМассив);
	мЛиПотоковый = ЛиПотоковый;
	Если мЛиПотоковый Тогда
		Браузер().sendStreamRequest(Адрес, ТелоЗапросаСтрока, ЗаголовкиТекст);
	Иначе
		Браузер().sendHttpRequest(Адрес, ТелоЗапросаСтрока, ЗаголовкиТекст);
	КонецЕсли;
КонецПроцедуры

Функция ЛиПотокЗавершен() Экспорт
	Возврат Браузер().isStreamCompleted();
КонецФункции

// Читает и удаляет полученную часть ответа
Функция ПрочитатьЧастьОтвета() Экспорт
	Возврат Браузер().getAndClearAllData();
КонецФункции

Функция ОтменитьЗапрос() Экспорт
	Возврат Браузер().cancelCurrentRequest();
КонецФункции

// Отправляет запрос к ИИ через OpenAPI. Ответ приходит событием onclick
// Параметры:
//   ПараметрыЗапроса - Структура - Содержимое сообщения
//    *Текст - Строка - текст запроса
//   Форма - Форма - 
//   ОбработчикОтвета -  - 
//   БлокируемыеЭлементы -  - 
//   Кнопка - Кнопка - 
//   ЛиПроверочный - Булево - 
//   РежимНапарника - Строка - docstring, explain, review, modify, custom, system, raw
//   КлючСервиса - Строка - Ключ API для аутентификации
//   Сервер - Строка - URL-адрес API, опционально, по умолчанию - OpenRouter
//   Модель - Строка - Название модели, опционально, по умолчанию - deepseek/deepseek-chat
// Возвращаемое значение:
//   Булево - успешность запуска
Функция ОтправитьЗапросИИ(Знач ПараметрыЗапроса, Знач Форма = Неопределено, Знач ОбработчикОтвета = "", Знач БлокируемыеЭлементы = Неопределено, Знач Кнопка = Неопределено,
		Знач ЛиПотоковый = Ложь, Знач ЛиПроверочный = Ложь, Знач РежимНапарника = "custom", Знач КлючСервиса = Неопределено, Знач Сервер = Неопределено, Знач Модель = Неопределено) Экспорт
	мПлатформа = ирКэш.Получить(); // Для сервера
	Если ПустаяСтрока(КлючСервиса) Тогда
		КлючСервиса = мПлатформа.КлючСервисаИИ();
	КонецЕсли;
	Сервер = мПлатформа.СерверИИ(, Сервер);
	Модель = мПлатформа.МодельИИ(, Модель, Сервер);
	Если ПустаяСтрока(КлючСервиса) Тогда
		Если Не ЛиПроверочный Тогда
			#Если Клиент Тогда
				Форма = ирКлиент.ОткрытьНастройкиПоляПрограммыНаЗаднемПланеЛкс("", Истина, "КлючСервисаИИ");
			#КонецЕсли
		КонецЕсли;
		ВызватьИсключение "Не указан ключ сервиса ИИ";
	КонецЕсли; 
	Если мОписаниеЗадания <> Неопределено Тогда
		ирКлиент.ОтменитьЗаданиеФормыЛкс(Форма, мОписаниеЗадания);
		ОтменитьЗапрос();
		мОписаниеЗадания = Неопределено;
		Возврат Ложь;
	КонецЕсли;
	мОписаниеЗадания = ирКлиент.ОписаниеФоновогоЗаданияФормыЛкс("ЗапросИИ",, ПараметрыЗапроса,, Кнопка, ОбработчикОтвета,,, БлокируемыеЭлементы,,,,, Ложь);
	ирКлиент.ЗапуститьИлиОтменитьФоновоеЗаданиеФормыЛкс(Форма, мОписаниеЗадания);
	Если Сервер = "https://code.1c.ai" Тогда
		Если РежимНапарника = "custom" Тогда
			ПараметрыЗапроса = Новый Структура("instruction", ПараметрыЗапроса.Текст);
		КонецЕсли;
		ПолеТекстаПрограммы = ирКэш.ПолеТекстаПрограммы();
		Соединение = ПолеТекстаПрограммы.СоединениеНапарника();
		Запрос = ирОбщий.ЗапросHTTPЛкс("/chat_api/v1/conversations/");
		СтруктураЗапроса = Новый Структура;
		//"tool_name": "review",
		//"ui_language": "russian",
		//"programming_language": "1с",
		//"script_language": "Russian"
		СтруктураЗапроса.Вставить("tool_name", РежимНапарника); 
		СтруктураЗапроса.Вставить("programming_language", "1с"); 
		СтруктураЗапроса.Вставить("ui_language", "russian"); 
		СтруктураЗапроса.Вставить("script_language", ПолеТекстаПрограммы.ИмяЯзыкаЧеловека()); 
		Заголовки = ПолеТекстаПрограммы.ЗаголовкиЗапросаНапарника();
		Запрос.УстановитьТелоИзСтроки(ирОбщий.ОбъектВТекстЖСОНЛкс(СтруктураЗапроса));
		Запрос.Заголовки = Заголовки;
		Ответ = Соединение.ОтправитьДляОбработки(Запрос);
		СтруктураОтвета = ирОбщий.ОбъектИзТекстаЖСОНЛкс(Ответ.ПолучитьТелоКакСтроку()); // см. ПрочитатьJSON(ОбразецОтветаСозданияЧатаНапарника())
		Адрес = "https://code.1c.ai/chat_api/v1/conversations/" + СтруктураОтвета.uuid + "/messages";
		СтруктураЗапроса = Новый Структура;
		//"parent_uuid": null,
		//"tool_content": ...
		СтруктураЗапроса.Вставить("tool_content", ПараметрыЗапроса);
	Иначе
		ЛиПотоковый = Ложь;
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type", "application/json"); 
		//Заголовки.Вставить("Origin", "https://chat.deepseek.com");
		//Заголовки.Вставить("Referer", "https://chat.deepseek.com/");
		Если ЗначениеЗаполнено(КлючСервиса) Тогда
			Заголовки.Вставить("Authorization", "Bearer " + КлючСервиса);
		КонецЕсли;
		СтруктураЗапроса = Новый Структура;
		СтруктураЗапроса.Вставить("model", Модель);
		СтруктураЗапроса.Вставить("messages", Новый Массив);
		СтруктураЗапроса.messages.Добавить(Новый Структура("role,content", "user", ПараметрыЗапроса.Текст));
		Адрес = Сервер + "/v1/chat/completions";
	КонецЕсли;
	ОтправитьЗапрос(Адрес, ирОбщий.ОбъектВТекстЖСОНЛкс(СтруктураЗапроса), Заголовки, ЛиПотоковый, ЛиПроверочный);
	Возврат Истина;
КонецФункции

Функция ОбразецОтветаСозданияЧатаНапарника() Экспорт
	Возврат "{""uuid"":""028c6d0f-864f-4b2d-846b-6c8b161c4171""}";
КонецФункции

Процедура ОбработатьОтвет(Форма) Экспорт
	Если Ложь
		Или мОписаниеЗадания = Неопределено 
		Или мОписаниеЗадания.Состояние = СостояниеФоновогоЗадания.Отменено
	Тогда
		Возврат;
	КонецЕсли;
	ТелоОтвета = СокрЛ(ПрочитатьЧастьОтвета());
	Если ирОбщий.СтрНачинаетсяСЛкс(ТелоОтвета, "Ошибка:") Тогда
		мОписаниеЗадания.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно;
		ирОбщий.СообщитьЛкс(ТелоОтвета);
		Возврат; 
	КонецЕсли;
	ЛиПотокЗавершен = Не мЛиПотоковый Или ЛиПотокЗавершен();
	ПрефиксЧастиОтвета = "data: ";
	Если ирОбщий.СтрНачинаетсяСЛкс(ТелоОтвета, "Отмена:") Тогда
		// Только для потокового режима
	ИначеЕсли ирОбщий.СтрНачинаетсяСЛкс(ТелоОтвета, "Конец:") Тогда
		// Только для потокового режима
		ТелоОтвета = "";
	ИначеЕсли ирОбщий.СтрНачинаетсяСЛкс(ТелоОтвета, ПрефиксЧастиОтвета) Тогда
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.УстановитьТекст(ТелоОтвета);
		Результат = Новый Массив;
		ПоследняяВерсияФрагмента = Неопределено;
		Для НомерСтроки = 1 По ТекстовыйДокумент.КоличествоСтрок() Цикл
			СтрокаТекста = ТекстовыйДокумент.ПолучитьСтроку(НомерСтроки);
			Если ирОбщий.СтрНачинаетсяСЛкс(СтрокаТекста, ПрефиксЧастиОтвета) Тогда
				ЭлементСообщения = ирОбщий.ОбъектИзТекстаЖСОНЛкс(ирОбщий.ТекстБезПервогоФрагментаЛкс(СтрокаТекста, ПрефиксЧастиОтвета));
				Если ЭлементСообщения.Свойство("content") Тогда
					ПоследняяВерсияФрагмента = ЭлементСообщения.content.text;
					Если Истина
						И ирОбщий.СвойствоСтруктурыЛкс(ЭлементСообщения, "finished") = Истина 
						И ирОбщий.СвойствоСтруктурыЛкс(ЭлементСообщения, "role") = "assistant"
					Тогда
						Результат.Добавить(ПоследняяВерсияФрагмента);
						ЛиПотокЗавершен = Истина; /// Нестандартный потоковый формат Напарника
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;  
		Если ПоследняяВерсияФрагмента <> Неопределено Тогда
			Результат.Добавить(ПоследняяВерсияФрагмента);
		КонецЕсли;
		ТелоОтвета = ирОбщий.СтрСоединитьЛкс(Результат, "");
	Иначе	
		Если ирОбщий.ЛиТекстJSONЛкс(ТелоОтвета) Тогда
			ОтветЖСОН = ирОбщий.ОбъектИзТекстаЖСОНЛкс(ТелоОтвета);
			Если Истина
				И ОтветЖСОН.Свойство("choices")
				И ОтветЖСОН.choices.Количество() > 0 
			Тогда
				ТелоОтвета = ОтветЖСОН.choices[0].message.content; // Строка
			Иначе
				ТелоОтвета = "";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	мОписаниеЗадания.Параметры.Вставить("Ответ", ТелоОтвета);
	Если ЛиПотокЗавершен Тогда
		мОписаниеЗадания.Состояние = СостояниеФоновогоЗадания.Завершено;
		Если ПустаяСтрока(ТелоОтвета) Тогда
			мОписаниеЗадания.Параметры = Неопределено;
		КонецЕсли;
		ирКлиент.ОбработатьЗавершениеЗаданияФормыЛкс(мОписаниеЗадания, Форма,,,, мОписаниеЗадания.Параметры); 
		Длительность = ТекущаяДата() - мОписаниеЗадания.НачалоВыполнения;
		Если Истина
			И Не мЛиПотоковый
			И (Ложь
				Или ирОбщий.СвойствоСтруктурыЛкс(мОписаниеЗадания.Параметры, "проверка") = Истина
				Или Длительность > 5 
				Или мЛиПроверочный И Длительность > 1)
		Тогда
			ирОбщий.СообщитьЛкс("ИИ ответил за " + Длительность + " секунд",,, Истина, Ложь);
		КонецЕсли;
		мОписаниеЗадания = Неопределено;
	Иначе
		Состояние = мОписаниеЗадания.Состояние;
		Параметры = мОписаниеЗадания.Параметры;
		Выполнить("Форма." + мОписаниеЗадания.ОбработчикЗавершения + "(Состояние, Параметры)"); 
	КонецЕсли;
КонецПроцедуры

//ирПортативный лФайл = Новый Файл(ИспользуемоеИмяФайла);
//ирПортативный ПолноеИмяФайлаБазовогоМодуля = Лев(лФайл.Путь, СтрДлина(лФайл.Путь) - СтрДлина("Модули\")) + "ирПортативный.epf";
//ирПортативный #Если Клиент Тогда
//ирПортативный 	Контейнер = Новый Структура();
//ирПортативный 	Оповестить("ирПолучитьБазовуюФорму", Контейнер);
//ирПортативный 	Если Не Контейнер.Свойство("ирПортативный", ирПортативный) Тогда
//ирПортативный 		ирПортативный = ВнешниеОбработки.ПолучитьФорму(ПолноеИмяФайлаБазовогоМодуля);
//ирПортативный 		ирПортативный.Открыть();
//ирПортативный 	КонецЕсли; 
//ирПортативный #Иначе
//ирПортативный 	ирПортативный = ВнешниеОбработки.Создать(ПолноеИмяФайлаБазовогоМодуля, Ложь); // Это будет второй экземпляр объекта
//ирПортативный #КонецЕсли
//ирПортативный ирОбщий = ирПортативный.ОбщийМодульЛкс("ирОбщий");
//ирПортативный ирКэш = ирПортативный.ОбщийМодульЛкс("ирКэш");
//ирПортативный ирСервер = ирПортативный.ОбщийМодульЛкс("ирСервер");
//ирПортативный ирКлиент = ирПортативный.ОбщийМодульЛкс("ирКлиент");

#КонецЕсли
