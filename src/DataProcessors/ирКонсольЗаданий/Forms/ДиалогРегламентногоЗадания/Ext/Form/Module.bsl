Процедура ПриОткрытии()
	
	Для Каждого Метаданное из Метаданные.РегламентныеЗадания Цикл
		ЭлементыФормы.МетаданныеВыбор.СписокВыбора.Добавить(Метаданное, Метаданное.Представление());
	КонецЦикла;
	
	Если МетаданныеВыбор <> Неопределено Тогда 
		МетаданныеВыборПриИзменении();
	КонецЕсли; 
	Если РегламентноеЗадание <> Неопределено Тогда
		ЭтаФорма.МетаданныеВыбор = РегламентноеЗадание.Метаданные;
		ЭтаФорма.МетаданныеИмя = РегламентноеЗадание.Метаданные.Имя;
		ЭлементыФормы.МетаданныеВыбор.ТолькоПросмотр = Истина;
		ЭтаФорма.Наименование = РегламентноеЗадание.Наименование;
		ЭтаФорма.Ключ = РегламентноеЗадание.Ключ;
		ЭтаФорма.Использование = РегламентноеЗадание.Использование;
		ЭтаФорма.ПользователиВыбор = РегламентноеЗадание.ИмяПользователя;
		ЭтаФорма.КоличествоПовторовПриАварийномЗавершении = РегламентноеЗадание.КоличествоПовторовПриАварийномЗавершении;
		ЭтаФорма.ИнтервалПовтораПриАварийномЗавершении = РегламентноеЗадание.ИнтервалПовтораПриАварийномЗавершении;
		ЭтаФорма.Предопределенное = РегламентноеЗадание.Предопределенное;
		ЭтаФорма.Расписание = РегламентноеЗадание.Расписание;
		ЭтаФорма.УникальныйИдентификатор = РегламентноеЗадание.УникальныйИдентификатор;
		Для Индекс = 0 По РегламентноеЗадание.Параметры.ВГраница() Цикл
			СтрокаПараметра = Параметры.Добавить();
			СтрокаПараметра.Значение = РегламентноеЗадание.Параметры[Индекс];
			ОбновитьПредставлениеИТипЗначенияВСтроке(СтрокаПараметра);
		КонецЦикла;
		МетаданныеВыборПриИзменении();
	Иначе
		ЭтаФорма.Расписание = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	Если СразуОткрытьРасписание Тогда 
		РасписаниеНажатие();
	КонецЕсли; 
КонецПроцедуры

Процедура РасписаниеНажатие(Элемент = Неопределено)
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	Если Диалог.ОткрытьМодально() Тогда
		ЭтаФорма.Расписание = Диалог.Расписание;
	КонецЕсли;
	
КонецПроцедуры

Процедура OK(Кнопка)
	Попытка
		Если МетаданныеВыбор = Неопределено Тогда
			ВызватьИсключение("Не выбраны метаданные регламентного задания.");
		КонецЕсли;
		Если РегламентноеЗадание = Неопределено Тогда
			РегламентноеЗадание = РегламентныеЗадания.СоздатьРегламентноеЗадание(МетаданныеВыбор);
		КонецЕсли;
		РегламентноеЗадание.Наименование = Наименование;
		РегламентноеЗадание.Ключ = Ключ;
		РегламентноеЗадание.Использование = Использование;
		РегламентноеЗадание.ИмяПользователя = ПользователиВыбор;
		РегламентноеЗадание.КоличествоПовторовПриАварийномЗавершении = КоличествоПовторовПриАварийномЗавершении;
		РегламентноеЗадание.ИнтервалПовтораПриАварийномЗавершении = ИнтервалПовтораПриАварийномЗавершении;
		РегламентноеЗадание.Расписание = Расписание;

		// Антибаг 8.2.15. Добавление в текущий массив не работает. http://partners.v8.1c.ru/forum/thread.jsp?id=1012620#1012620
		////РегламентноеЗадание.Параметры.Очистить();
		//Для Индекс = 0 По Параметры.Количество() - 1 Цикл
		//	РегламентноеЗадание.Параметры.Добавить(Параметры[Индекс].Значение);
		//КонецЦикла;
		РегламентноеЗадание.Параметры = Параметры.ВыгрузитьКолонку("Значение");

		РегламентноеЗадание.Записать();
		
		Закрыть(РегламентноеЗадание.УникальныйИдентификатор);
	Исключение	
		ПоказатьИнформациюОбОшибке(ИнформацияОбОшибке());	
	КонецПопытки;
КонецПроцедуры

Процедура ПараметрыЗначениеКонстантыПриИзменении(Элемент)
	
	ТабличноеПоле = ЭлементыФормы.Параметры;
	ТабличноеПоле.ТекущиеДанные.Значение = Элемент.Значение;
	ОбновитьПредставлениеИТипЗначенияВСтроке();
	
КонецПроцедуры

Процедура ПараметрыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки) Экспорт
	
	ОформлениеСтроки.Ячейки.Номер.УстановитьТекст(Параметры.Индекс(ДанныеСтроки) + 1);
	ирКлиент.ТабличноеПолеОтобразитьПиктограммыТиповЛкс(ОформлениеСтроки, "Значение");

КонецПроцедуры

Процедура КоманднаяПанельПараметрыИсследоватьПараметры(Кнопка)
	
	ирОбщий.ИсследоватьЛкс(Параметры,,Истина);
	
КонецПроцедуры

Процедура ПараметрыЗначениеОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	ирКлиент.ПолеВвода_ОкончаниеВводаТекстаЛкс(Элемент, Текст, Значение, СтандартнаяОбработка);

КонецПроцедуры

Процедура ПараметрыЗначениеНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ирКлиент.ПолеВводаКолонкиРасширенногоЗначения_НачалоВыбораЛкс(ЭтаФорма, ЭлементыФормы.Параметры, СтандартнаяОбработка, ЭлементыФормы.Параметры.ТекущаяСтрока.Значение);
	ОбновитьПредставлениеИТипЗначенияВСтроке();
	
КонецПроцедуры

Процедура МетаданныеВыборПриИзменении(Элемент = Неопределено)
	
	#Если Сервер И Не Сервер Тогда
		мПлатформа = Обработки.ирПлатформа.Создать();
	#КонецЕсли
	МетаЗадание = Метаданные.РегламентныеЗадания[МетаданныеВыбор.Имя];
	ЭтаФорма.ИмяМетода = МетаЗадание.ИмяМетода;
	ЭтаФорма.МетаданныеИмя = МетаЗадание.Имя;
	МетодМодуля = мПлатформа.ОписаниеМетодаМодуля(ИмяМетода);
	Если МетодМодуля <> Неопределено Тогда
		ПараметрыМетода = мПлатформа.ПараметрыМетодаМодуля(МетодМодуля);
		ЭтаФорма.ОписаниеМетода = МетодМодуля.Описание;
	КонецЕсли;
	Если Истина
		И МетодМодуля <> Неопределено
		И ЗначениеЗаполнено(ПараметрыМетода)
	Тогда
		НомерПараметра = 1;
		Для Каждого ОписаниеПараметра Из ПараметрыМетода Цикл
			Если Параметры.Количество() < НомерПараметра Тогда
				Продолжить;
			КонецЕсли; 
			СтрокаПараметра = Параметры[НомерПараметра - 1];
			СтрокаПараметра.Имя = ОписаниеПараметра.Имя;
			СтрокаПараметра.Комментарий = ОписаниеПараметра.Описание;
		КонецЦикла;
		ЭлементыФормы.Параметры.Колонки.Имя.Видимость = Истина;
	Иначе
		ЭлементыФормы.Параметры.Колонки.Имя.Видимость = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбновитьПредставлениеИТипЗначенияВСтроке(СтрокаТаблицы = Неопределено)
	
	Если СтрокаТаблицы = Неопределено Тогда
		СтрокаТаблицы = ЭлементыФормы.Параметры.ТекущиеДанные;
	КонецЕсли; 
	СтрокаТаблицы.ТипЗначения = ТипЗнч(СтрокаТаблицы.Значение);
	СтрокаТаблицы.ПредставлениеЗначения = СтрокаТаблицы.Значение;
	
КонецПроцедуры

Процедура МетодОткрытие(Элемент, СтандартнаяОбработка)
	
	ирКлиент.ОткрытьМетодМодуляПоИмениЛкс(ИмяМетода);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

Процедура ПользователиВыборНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ирКлиент.ПолеВводаПользователя_НачалоВыбораЛкс(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ВнешнееСобытие(Источник, Событие, Данные) Экспорт
	
	ирКлиент.Форма_ВнешнееСобытиеЛкс(ЭтаФорма, Источник, Событие, Данные);

КонецПроцедуры

Процедура ТабличноеПолеПриПолученииДанных(Элемент, ОформленияСтрок) Экспорт 
	
	ирКлиент.ТабличноеПолеПриПолученииДанныхЛкс(ЭтаФорма, Элемент, ОформленияСтрок);

КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	ирКлиент.ЗапроситьСохранениеДанныхФормыЛкс(ЭтаФорма, Отказ);
	
КонецПроцедуры

Процедура ОписаниеМетодаОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ирКлиент.ОткрытьТекстЛкс(Элемент.Значение, "Описание метода " + ИмяМетода,,, ИмяМетода);
КонецПроцедуры

ирКлиент.ИнициироватьФормуЛкс(ЭтаФорма, "Обработка.ирКонсольЗаданий.Форма.ДиалогРегламентногоЗадания");

ОписаниеТипов = Новый ОписаниеТипов();
Параметры.Колонки.Добавить("Значение", ОписаниеТипов);
