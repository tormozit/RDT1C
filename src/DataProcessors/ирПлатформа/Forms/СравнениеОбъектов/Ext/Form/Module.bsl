Перем Колонки;
Перем ПолноеИмяТаблицыБД;
Перем СравнениеТаблиц Экспорт;
Перем ИмяСвойстваНомерСтороны;

Процедура ПриОткрытии()
	
	Если КлючУникальности = "Автотест" Тогда
		Возврат;
	КонецЕсли;
	ирОбщий.Форма_ПриОткрытииЛкс(ЭтаФорма);
	ЭтаФорма.СравнениеТаблиц = ирОбщий.СоздатьОбъектПоПолномуИмениМетаданныхЛкс("Обработка.ирСравнениеТаблиц");
	ирОбщий.УстановитьПрикреплениеФормыВУправляемомПриложенииЛкс(Этаформа);
	
КонецПроцедуры

Функция ОбновитьСоставСвойств()
	
	ЭлементыФормы.ДействияФормы.Кнопки.ТолькоРазличия.Пометка = ТолькоРазличия;
	ЭлементыФормы.ТаблицаОбъектов.ИзменятьСоставСтрок = ТаблицаОбъектов.Количество() > 2;
	ЭлементыФормы.ДействияФормы.Кнопки.УдалитьОбъектИзСравнения.Доступность = ТаблицаОбъектов.Количество() > 2;
	СостоянияСтрок = ирОбщий.ТабличноеПолеСостояниеСтрокЛкс(ЭлементыФормы.СвойстваОбъектов, "Имя");
	СвойстваОбъектов.Очистить();
	СтрокаСвойства = СвойстваОбъектов.Добавить();
	СтрокаСвойства.Имя = ИмяСвойстваНомерСтороны;
	СтрокаСвойства.Представление = ИмяСвойстваНомерСтороны;
	ТипОбъекта = "";
	Счетчик = 1;
	Для Каждого СтрокаОбъекта Из ТаблицаОбъектов Цикл
		СтрокаОбъекта.НомерСтороны = Счетчик;
		ЗагрузитьСторонуСравнения(СтрокаОбъекта, ТипОбъекта);
		Счетчик = Счетчик + 1;
	КонецЦикла;
	Если ЭлементыФормы.ТаблицаОбъектов.ТекущаяСтрока <> Неопределено Тогда
		ЭлементыФормы.СвойстваОбъектов.ТекущаяКолонка = ЭлементыФормы.СвойстваОбъектов.Колонки["ПредставлениеЗначения" + ЭлементыФормы.ТаблицаОбъектов.ТекущаяСтрока.НомерСтороны];
	КонецЕсли;
	Если СвойстваОбъектов.Количество() = 1 Тогда
		Возврат Ложь;
	КонецЕсли; 
	Если ирОбщий.НеуникальныеЗначенияКолонкиТаблицыЛкс(ТаблицаОбъектов, "ПредставлениеОбъекта").Количество() = 0 Тогда
		Для Каждого СтрокаОбъектов Из ТаблицаОбъектов Цикл
			ЭлементыФормы.СвойстваОбъектов.Колонки["Объект" + СтрокаОбъектов.НомерСтороны].ТекстШапки = СтрокаОбъектов.ПредставлениеОбъекта;
		КонецЦикла;
	КонецЕсли;
	Для Счетчик = ТаблицаОбъектов[ТаблицаОбъектов.Количество() - 1].НомерСтороны + 1 По 100 Цикл
		Если НайтиУдалитьКолонкуПоРоли("Объект", Счетчик) Тогда
			НайтиУдалитьКолонкуПоРоли("Значение", Счетчик);
			НайтиУдалитьКолонкуПоРоли("ПредставлениеЗначения", Счетчик);
			НайтиУдалитьКолонкуПоРоли("ЗначениеУстановлено", Счетчик);
			НайтиУдалитьКолонкуПоРоли("Разность", Счетчик);
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Для Каждого СтрокаСвойства Из СвойстваОбъектов Цикл
		Если СтрокаСвойства.Имя = ИмяСвойстваНомерСтороны Тогда
			СтрокаСвойства.ОписаниеТипов = Новый ОписаниеТипов("Число");
			Продолжить;
		КонецЕсли;
		МассивТипов = Новый Массив;
		Если СтрокаСвойства.ЗначениеУстановлено1 И Не СтрокаСвойства.ЗначениеУстановлено2 Тогда 
			СтрокаСвойства.ТипРазличия = "Только 1";
			МассивТипов.Добавить(ТипЗнч(СтрокаСвойства["Значение1"]));
		ИначеЕсли СтрокаСвойства.ЗначениеУстановлено2 И Не СтрокаСвойства.ЗначениеУстановлено1 Тогда 
			СтрокаСвойства.ТипРазличия = "Только 2";
			МассивТипов.Добавить(ТипЗнч(СтрокаСвойства["Значение2"]));
		Иначе
			Значение1 = СтрокаСвойства["Значение1"];
			СтрокаЗначения1 = ирОбщий.ОбъектВСтрокуДляСравненияВнутрЛкс(Значение1);
			МассивТипов.Добавить(ТипЗнч(Значение1));
			Для Счетчик = 2 По ТаблицаОбъектов.Количество() Цикл
				ЗначениеХ = СтрокаСвойства["Значение" + Счетчик];
				СтрокаЗначенияХ = ирОбщий.ОбъектВСтрокуДляСравненияВнутрЛкс(ЗначениеХ);
				МассивТипов.Добавить(ТипЗнч(ЗначениеХ));
				Если Ложь
					Или СтрокаСвойства.ТипРазличия = "Изменено"
					Или (Истина
						//И Не ирОбщий.ЛиКоллекцияЛкс(Значение1)
						И Значение1 = ЗначениеХ)
					Или (Истина
						И СтрокаЗначения1 <> Неопределено
						И СтрокаЗначенияХ <> Неопределено
						И СтрокаЗначения1 = СтрокаЗначенияХ)
				Тогда
				Иначе
					СтрокаСвойства.ТипРазличия = "Изменено";
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли; 
		СтрокаСвойства.ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
	КонецЦикла;
	СвойстваОбъектов.Сортировать("Имя");
	Если ТолькоРазличия Тогда
		Для Каждого СтрокаРавныхЗначений Из СвойстваОбъектов.НайтиСтроки(Новый Структура("ТипРазличия", "")) Цикл
			Если СтрокаРавныхЗначений.Имя = ИмяСвойстваНомерСтороны Тогда
				Продолжить;
			КонецЕсли;
			СвойстваОбъектов.Удалить(СтрокаРавныхЗначений);
		КонецЦикла;
	КонецЕсли; 
	ирОбщий.ТабличноеПолеВосстановитьСостояниеСтрокЛкс(ЭлементыФормы.СвойстваОбъектов, СостоянияСтрок);
	Если ЗначениеЗаполнено(ПараметрТекущееСвойство) Тогда
		СтрокаТаблицы = СвойстваОбъектов.Найти(ПараметрТекущееСвойство, "Имя");
		Если СтрокаТаблицы <> Неопределено Тогда
			ЭлементыФормы.СвойстваОбъектов.ТекущаяСтрока = СтрокаТаблицы;
		КонецЕсли; 
		ЭтаФорма.ПараметрТекущееСвойство = "";
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрНомерЭталона) Тогда
		ТаблицаОбъектов[ПараметрНомерЭталона - 1].Эталон = Истина;
		ЭтаФорма.ПараметрНомерЭталона = 0;
	КонецЕсли;
	ирОбщий.ОбновитьТекстПослеМаркераВСтрокеЛкс(ЭтаФорма.Заголовок, , ТипОбъекта, ": ");
	КоличествоРазличий = СвойстваОбъектов.Количество() - СвойстваОбъектов.НайтиСтроки(Новый Структура("ТипРазличия", "")).Количество();
	ирОбщий.ОбновитьТекстПослеМаркераВСтрокеЛкс(ЭлементыФормы.ДействияФормы.Кнопки.ТолькоРазличия.Текст, , "" + КоличествоРазличий + " шт", ": ");
	ОбновитьРазностиЧисел();
	Возврат Истина;

КонецФункции

Функция НайтиУдалитьКолонкуПоРоли(Знач ИмяРолиКолонки, Знач НомерСтороны)
	
	Успех = Ложь;
	ИмяКолонки = ИмяРолиКолонки + НомерСтороны;
	КолонкаТП = ЭлементыФормы.СвойстваОбъектов.Колонки.Найти(ИмяКолонки);
	Если КолонкаТП <> Неопределено Тогда
		ЭлементыФормы.СвойстваОбъектов.Колонки.Удалить(КолонкаТП);
		Успех = Истина;
	КонецЕсли;
	КолонкаТЗ = СвойстваОбъектов.Колонки.Найти(ИмяКолонки);
	Если КолонкаТЗ <> Неопределено Тогда
		СвойстваОбъектов.Колонки.Удалить(КолонкаТЗ);
		Успех = Истина;
	КонецЕсли;
	Возврат Успех;

КонецФункции

Процедура ЗагрузитьСторонуСравнения(Знач СтрокаОбъекта, ТипОбъекта = "")
	
	НомерСтороны = СтрокаОбъекта.НомерСтороны;  
	Объект = СтрокаОбъекта.Объект;
	ПоляТаблицыБД = Неопределено;
	СтруктураОбъекта = ирОбщий.СтруктураИзОбъектаЛкс(Объект, ТипОбъекта, ПоляТаблицыБД);
	Для Каждого КлючИЗначение Из СтруктураОбъекта Цикл
		СтрокаСвойства = СвойстваОбъектов.Найти(КлючИЗначение.Ключ, "Имя");
		Если СтрокаСвойства = Неопределено Тогда
			СтрокаСвойства = СвойстваОбъектов.Добавить();
			СтрокаСвойства.Имя = КлючИЗначение.Ключ;
			Если ПоляТаблицыБД <> Неопределено Тогда
				СтрокаПоля = ПоляТаблицыБД.Найти(КлючИЗначение.Ключ, "Имя");
				Если СтрокаПоля <> Неопределено Тогда
					СтрокаСвойства.Представление = СтрокаПоля.Заголовок;
				КонецЕсли; 
			КонецЕсли;
			Если Не ЗначениеЗаполнено(СтрокаСвойства.Представление) Тогда
				СтрокаСвойства.Представление = СтрокаСвойства.Имя;
			КонецЕсли;
		КонецЕсли; 
		ЗначениеСвойства = КлючИЗначение.Значение;
		Если СвойстваОбъектов.Колонки.Найти("Значение" + НомерСтороны) = Неопределено Тогда
			СвойстваОбъектов.Колонки.Добавить("Значение" + НомерСтороны);
			КолонкаТП = ЭлементыФормы.СвойстваОбъектов.Колонки.Добавить("Объект" + НомерСтороны);
			ЗаполнитьЗначенияСвойств(КолонкаТП, ЭлементыФормы.СвойстваОбъектов.Колонки.Объект1,, "Имя");
			
			КолонкаТЗ = СвойстваОбъектов.Колонки.Добавить("ПредставлениеЗначения" + НомерСтороны, СвойстваОбъектов.Колонки.ПредставлениеЗначения1.ТипЗначения);
			КолонкаТП = ЭлементыФормы.СвойстваОбъектов.Колонки.Добавить(КолонкаТЗ.Имя);
			ЗаполнитьЗначенияСвойств(КолонкаТП, ЭлементыФормы.СвойстваОбъектов.Колонки.ПредставлениеЗначения1,, "Имя");
			КолонкаТП.Данные = КолонкаТЗ.Имя;
			
			КолонкаТЗ = СвойстваОбъектов.Колонки.Добавить("Разность" + НомерСтороны, СвойстваОбъектов.Колонки.Разность1.ТипЗначения);
			КолонкаТП = ЭлементыФормы.СвойстваОбъектов.Колонки.Добавить(КолонкаТЗ.Имя);
			ЗаполнитьЗначенияСвойств(КолонкаТП, ЭлементыФормы.СвойстваОбъектов.Колонки.Разность1,, "Имя");
			КолонкаТП.Данные = КолонкаТЗ.Имя;
			СвойстваОбъектов.Колонки.Добавить("ЗначениеУстановлено" + НомерСтороны, Новый ОписаниеТипов("Булево"));
		КонецЕсли;
		СтрокаСвойства["Значение" + НомерСтороны] = ЗначениеСвойства;
		СтрокаСвойства["ПредставлениеЗначения" + НомерСтороны] = ЗначениеСвойства;
		СтрокаСвойства["ЗначениеУстановлено" + НомерСтороны] = Истина;
		ирОбщий.ОбновитьТипЗначенияВСтрокеТаблицыЛкс(СтрокаСвойства, "Значение" + НомерСтороны,, "ТипЗначения" + НомерСтороны, "ИмяТипаЗначения" + НомерСтороны,, ЗначениеСвойства);
	КонецЦикла;
	СтрокаНомераСтороны = СвойстваОбъектов.Найти(ИмяСвойстваНомерСтороны, "Имя");
	СтрокаНомераСтороны["Значение" + НомерСтороны] = НомерСтороны;
	СтрокаНомераСтороны["ПредставлениеЗначения" + НомерСтороны] = НомерСтороны;
	СтрокаНомераСтороны["ЗначениеУстановлено" + НомерСтороны] = НомерСтороны;
	СтрокаНомераСтороны["Разность" + НомерСтороны] = НомерСтороны;

КонецПроцедуры

Процедура СвойстваОбъектовПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки) Экспорт

	Если Не ТолькоРазличия Тогда
		Если ДанныеСтроки.ТипРазличия = "Изменено" Тогда
			ОформлениеСтроки.ЦветФона = СравнениеТаблиц.ОтличаютсяЦветФона;
		ИначеЕсли ДанныеСтроки.ТипРазличия = "Только 1" Тогда
			ОформлениеСтроки.ЦветФона = СравнениеТаблиц.ТолькоВТаблице1ЦветФона;
		ИначеЕсли ДанныеСтроки.ТипРазличия = "Только 2" Тогда
			ОформлениеСтроки.ЦветФона = СравнениеТаблиц.ТолькоВТаблице2ЦветФона;
		КонецЕсли;  
	КонецЕсли;
	РасширенныеКолонки = Новый Структура;
	КолонкиСКартинками = Новый Структура;
	Для Каждого СтрокаОбъекта Из ТаблицаОбъектов Цикл
		НомерСтороны = СтрокаОбъекта.НомерСтороны;
		ИмяКолонки = "Объект" + НомерСтороны;
		ОформлениеСтроки.Ячейки[ИмяКолонки].Видимость = Ложь;
		РасширенныеКолонки.Вставить("ПредставлениеЗначения" + НомерСтороны, "Значение" + НомерСтороны);
		Если ДанныеСтроки.Имя <> ИмяСвойстваНомерСтороны Тогда
			ирОбщий.ОформитьЯчейкуРазностиЛкс(ОформлениеСтроки, "Разность" + НомерСтороны);
		КонецЕсли;    
	КонецЦикла;
	ТипыЗначений = ДанныеСтроки.ОписаниеТипов.Типы();
	Если ТипыЗначений.Количество() <> 1 Тогда
		КолонкиСКартинками = РасширенныеКолонки;
	КонецЕсли;
	ирОбщий.ТабличноеПолеПриВыводеСтрокиЛкс(ЭтаФорма, Элемент, ОформлениеСтроки, ДанныеСтроки, ЭлементыФормы.ДействияФормы.Кнопки.Идентификаторы, КолонкиСКартинками, РасширенныеКолонки);
	Если ТипыЗначений.Количество() = 1 Тогда
		ТипЗначенийСвойства = ТипыЗначений[0]; 
	Иначе
		ТипЗначенийСвойства = ТипЗнч(Неопределено);
	КонецЕсли;
	Если ЭлементыФормы.СвойстваОбъектов.Колонки.Имя.Видимость Тогда
		ИмяПервойВидимойКолонки = "Имя";
	Иначе
		ИмяПервойВидимойКолонки = "Представление";
	КонецЕсли;
	КартинкаТипа = ирОбщий.КартинкаТипаЛкс(ТипЗначенийСвойства);
	ОформлениеСтроки.Ячейки[ИмяПервойВидимойКолонки].УстановитьКартинку(КартинкаТипа);
	
КонецПроцедуры

Процедура ПриПовторномОткрытии(СтандартнаяОбработка = Неопределено)
	
	Если ПараметрОбъекты.Количество() > 0 Тогда
		ЗагрузитьДанные();
	КонецЕсли;

КонецПроцедуры

Функция ЗагрузитьДанные()
	
	ТаблицаОбъектов.Очистить();
	Для Каждого ЭлементСписка Из ПараметрОбъекты Цикл
		ДобавитьОбъектСравнения(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	Успех = Истина;
	ЭлементыФормы.ДействияФормы.Кнопки.СравнитьКакКоллекции.Доступность = ирОбщий.ЛиКоллекцияЛкс(ТаблицаОбъектов[0].Объект);
	Если Не ОбновитьСоставСвойств() Тогда 
		Успех = Ложь;
		Если ирОбщий.ЛиКоллекцияЛкс(ТаблицаОбъектов[0].Объект) Тогда 
			ДействияФормыСравнитьКакКоллекции();
		Иначе
			ирОбщий.СравнитьЗначенияВФормеЧерезXMLЛкс(ТаблицаОбъектов[0].Объект, ТаблицаОбъектов[1].Объект);
		КонецЕсли; 
	КонецЕсли;
	ПараметрОбъекты.Очистить();
	Возврат Успех;

КонецФункции

Функция ДобавитьОбъектСравнения(ОбъектСравнения, ПредставлениеОбъекта = "")
	
	СтрокаОбъекта = ТаблицаОбъектов.Добавить();
	Если ПараметрТекущийОбъект = ОбъектСравнения Тогда
		ЭлементыФормы.ТаблицаОбъектов.ТекущаяСтрока = СтрокаОбъекта;
		ЭтаФорма.ПараметрТекущийОбъект = Неопределено; 
	КонецЕсли;
	Если ирОбщий.ЛиСсылкаНаОбъектБДЛкс(ОбъектСравнения) Тогда
		ПредставлениеОбъекта = "" + ОбъектСравнения;
		ОбъектСравнения = ирОбщий.ОбъектБДПоКлючуЛкс(ОбъектСравнения.Метаданные().ПолноеИмя(), ОбъектСравнения).Методы;
	КонецЕсли;
	СтрокаОбъекта.Объект = ОбъектСравнения;
	Если ЗначениеЗаполнено(ПредставлениеОбъекта) Тогда
		СтрокаОбъекта.ПредставлениеОбъекта = ПредставлениеОбъекта;
	Иначе
		СтрокаОбъекта.ПредставлениеОбъекта = ирОбщий.РасширенноеПредставлениеЗначенияЛкс(ОбъектСравнения,, Истина);
	КонецЕсли;
	СтрокаОбъекта.ТипОбъекта = ТипЗнч(ОбъектСравнения); 
	Возврат СтрокаОбъекта;

КонецФункции

Процедура СвойстваОбъектовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтрокаНомераСтороны = СвойстваОбъектов.Найти(ИмяСвойстваНомерСтороны, "Имя");
	НомерСтороны = СтрокаНомераСтороны[Колонка.Имя];
	Если ТипЗнч(НомерСтороны) = Тип("Число") Тогда
		ирОбщий.ЯчейкаТабличногоПоляРасширенногоЗначения_ВыборЛкс(ЭтаФорма, Элемент, СтандартнаяОбработка, ВыбраннаяСтрока["Значение" + НомерСтороны]);
	ИначеЕсли Истина
		И ВыбраннаяСтрока.ТипРазличия = "Изменено" 
		//И Колонка = ЭлементыФормы.СвойстваОбъектов.Колонки.ТипРазличия 
	Тогда  
		Если ирОбщий.ЛиСсылкаНаОбъектБДЛкс(ВыбраннаяСтрока["Значение1"]) Тогда
			НовыеОбъекты = Новый СписокЗначений;
			НовыеОбъекты.ЗагрузитьЗначения(ТаблицаОбъектов.ВыгрузитьКолонку("Значение"));
			ирОбщий.СравнитьОбъектыВФормеЛкс(НовыеОбъекты,,,, Новый УникальныйИдентификатор);
		Иначе
			ПараОбъектов = ЭлементыФормы.ТаблицаОбъектов.ВыделенныеСтроки;
			Если ПараОбъектов.Количество() < 2 Тогда
				ПараОбъектов = ТаблицаОбъектов;
			КонецЕсли;
			ирОбщий.Сравнить2ЗначенияВФормеЛкс(ВыбраннаяСтрока["Значение" + ПараОбъектов[0].НомерСтороны], ВыбраннаяСтрока["Значение" + + ПараОбъектов[1].НомерСтороны],,,,,, Новый УникальныйИдентификатор);
		КонецЕсли;
	Иначе
		//ирОбщий.ЯчейкаТабличногоПоляРасширенногоЗначения_ВыборЛкс(ЭтаФорма, Элемент, СтандартнаяОбработка,, Ложь);
	КонецЕсли; 
	
КонецПроцедуры

Процедура СтруктураКоманднойПанелиНажатие(Кнопка)
	
	ирОбщий.ОткрытьСтруктуруКоманднойПанелиЛкс(ЭтаФорма, Кнопка);
	
КонецПроцедуры

Процедура КлсКомандаНажатие(Кнопка) Экспорт 
	
	ирОбщий.УниверсальнаяКомандаФормыЛкс(ЭтаФорма, Кнопка);
	
КонецПроцедуры

Процедура СвойстваОбъектовПриИзмененииФлажка(Элемент, Колонка)
	
	ирОбщий.ТабличноеПолеПриИзмененииФлажкаЛкс(ЭтаФорма, Элемент, Колонка);
	
КонецПроцедуры

Процедура ВнешнееСобытие(Источник, Событие, Данные) Экспорт
	
	ирОбщий.Форма_ВнешнееСобытиеЛкс(ЭтаФорма, Источник, Событие, Данные);

КонецПроцедуры

Процедура ТабличноеПолеПриПолученииДанных(Элемент, ОформленияСтрок) Экспорт 
	
	ирОбщий.ТабличноеПолеПриПолученииДанныхЛкс(ЭтаФорма, Элемент, ОформленияСтрок);

КонецПроцедуры

Процедура ПриЗакрытии()
	
	ирОбщий.Форма_ПриЗакрытииЛкс(ЭтаФорма);

КонецПроцедуры

Процедура ДействияФормыИсследовать(Кнопка)
	
	ТекущаяСтрока = ЭлементыФормы.СвойстваОбъектов.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	МетаданныеПоля = МетаданныеПоля(ТекущаяСтрока);
	Если МетаданныеПоля = Неопределено Тогда
		МетаданныеПоля = ирОбщий.ОбъектМДПоПолномуИмениТаблицыБДЛкс(ИмяТаблицыБД);
	КонецЕсли; 
	ирОбщий.ОткрытьОбъектМетаданныхЛкс(МетаданныеПоля);
	
КонецПроцедуры

Функция МетаданныеПоля(Знач ТекущаяСтрока = Неопределено)
	
	Если ТекущаяСтрока = Неопределено Тогда
		ТекущаяСтрока = ЭлементыФормы.СвойстваОбъектов.ТекущаяСтрока;
	КонецЕсли; 
	ИмяТаблицыБДВсеПоля = ИмяТаблицыБД;
	Если ирОбщий.ЛиКорневойТипРегистраБухгалтерииЛкс(ирОбщий.ТипТаблицыБДЛкс(ИмяТаблицыБД)) Тогда
		ИмяТаблицыБДВсеПоля = ИмяТаблицыБДВсеПоля + ".ДвиженияССубконто";
	КонецЕсли; 
	ПоляТаблицыБД = ирКэш.ПоляТаблицыБДЛкс(ИмяТаблицыБДВсеПоля);
	ПолеТаблицы = ПоляТаблицыБД.Найти(ТекущаяСтрока.Имя);
	МетаданныеПоля = ПолеТаблицы.Метаданные;
	Возврат МетаданныеПоля;

КонецФункции

Процедура СвойстваОбъектовПриАктивизацииСтроки(Элемент = Неопределено)
	Элемент = ЭлементыФормы.СвойстваОбъектов;
	ирОбщий.ТабличноеПолеПриАктивизацииСтрокиЛкс(ЭтаФорма, Элемент);
	НомерСтороны = СвойстваОбъектов.Найти(ИмяСвойстваНомерСтороны, "Имя")[Элемент.ТекущаяКолонка.Данные];
	Если ТипЗнч(НомерСтороны) = Тип("Число") Тогда
		СтрокаОбъекта = ТаблицаОбъектов.Найти(НомерСтороны, "НомерСтороны");
		ЭлементыФормы.ТаблицаОбъектов.ТекущаяСтрока = СтрокаОбъекта;
	КонецЕсли;
	ТекущаяСтрока = Элемент.ТекущаяСтрока;
	Если ТекущаяСтрока <> Неопределено Тогда
		Для Каждого СтрокаОбъекта Из ТаблицаОбъектов Цикл
			СтрокаОбъекта.Значение = ТекущаяСтрока["Значение" + СтрокаОбъекта.НомерСтороны];
			СтрокаОбъекта.ПредставлениеЗначения = ТекущаяСтрока["ПредставлениеЗначения" + СтрокаОбъекта.НомерСтороны]; 
			Если ТекущаяСтрока.Имя <> ИмяСвойстваНомерСтороны Тогда
				СтрокаОбъекта.Разность = ТекущаяСтрока["Разность" + СтрокаОбъекта.НомерСтороны];
			Иначе
				СтрокаОбъекта.Разность = Неопределено;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ДействияФормыСравнитьКакКоллекции(Кнопка = Неопределено)
	
	ПараОбъектов = ЭлементыФормы.ТаблицаОбъектов.ВыделенныеСтроки;
	Если ПараОбъектов.Количество() < 2 Тогда
		ПараОбъектов = ТаблицаОбъектов;
	КонецЕсли;
	ирОбщий.Сравнить2ЗначенияВФормеЛкс(ПараОбъектов[0].Объект, ПараОбъектов[1].Объект,,,,,,, Истина);
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если КлючУникальности = "Автотест" Тогда
		Возврат;
	КонецЕсли;
	Если Не ЗагрузитьДанные() Тогда 
		Отказ = Истина;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ДействияФормыТолькоРазличия(Кнопка)
	
	ЭтаФорма.ТолькоРазличия = Не Кнопка.Пометка;
	ОбновитьСоставСвойств();
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник) Экспорт
	
	ирОбщий.Форма_ОбработкаОповещенияЛкс(ЭтаФорма, ИмяСобытия, Параметр, Источник); 

КонецПроцедуры

Процедура ТаблицаОбъектовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ирОбщий.ИсследоватьЛкс(ВыбраннаяСтрока.Объект);
КонецПроцедуры

Процедура ТаблицаОбъектовПриАктивизацииСтроки(Элемент)
	ирОбщий.ТабличноеПолеПриАктивизацииСтрокиЛкс(ЭтаФорма, Элемент);
КонецПроцедуры

Процедура ТаблицаОбъектовПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	ирОбщий.ТабличноеПолеПриВыводеСтрокиЛкс(ЭтаФорма, Элемент, ОформлениеСтроки, ДанныеСтроки);
	ирОбщий.ОформитьЯчейкуРазностиЛкс(ОформлениеСтроки);
КонецПроцедуры

Процедура ДействияФормыОбновить(Кнопка)
	ОбновитьСоставСвойств();
КонецПроцедуры

Процедура ТаблицаОбъектовПередНачаломДобавления(Элемент, Отказ, Копирование)
	Отказ = Истина;
КонецПроцедуры

Процедура ТаблицаОбъектовПослеУдаления(Элемент = Неопределено)
	ОбновитьСоставСвойств();
КонецПроцедуры

Процедура ДействияФормыУдалитьОбъектИзСравнения(Кнопка)
	
	Если ЭлементыФормы.ТаблицаОбъектов.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТаблицаОбъектов.Удалить(ЭлементыФормы.ТаблицаОбъектов.ТекущаяСтрока);
	ТаблицаОбъектовПослеУдаления();
	
КонецПроцедуры

Процедура ДействияФормыВставитьОбъектИзБуфера(Кнопка)
	
	НовыйОбъект = ирОбщий.ЗначениеИзБуфераОбменаЛкс();
	Если ЛиОбъектСовместим(НовыйОбъект) Тогда
		ДобавитьОбъектИОбновитьСравнение(НовыйОбъект);
	КонецЕсли;
	
КонецПроцедуры

Функция ЛиОбъектСовместим(Знач НовыйОбъект)
	
	Возврат Ложь
		Или ТипЗнч(НовыйОбъект) = ТипЗнч(ТаблицаОбъектов[0].Объект)
		Или (Истина
			И ирОбщий.ЛиСсылкаНаОбъектБДЛкс(НовыйОбъект) 
			И НовыйОбъект.Метаданные() = ирОбщий.МетаданныеОбъектаБДЛкс(ТаблицаОбъектов[0].Объект));

КонецФункции

Процедура ТаблицаОбъектовПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка)
	МассивЗначений = ирОбщий.МассивЗначенийПеретаскиванияЛкс(ПараметрыПеретаскивания);
	Если ЛиОбъектСовместим(МассивЗначений[0]) Тогда
		СтандартнаяОбработка = Ложь;
		//ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.Копирование;
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Копирование;
	КонецЕсли;
КонецПроцедуры

Процедура ТаблицаОбъектовПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Колонка)
	МассивЗначений = ирОбщий.МассивЗначенийПеретаскиванияЛкс(ПараметрыПеретаскивания);
 	Если ЛиОбъектСовместим(МассивЗначений[0]) Тогда
		СтандартнаяОбработка = Ложь;
		ДобавитьОбъектИОбновитьСравнение(МассивЗначений[0]);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьОбъектИОбновитьСравнение(Знач НовыйОбъект)
	
	НоваяСтрока = ДобавитьОбъектСравнения(НовыйОбъект);
	ОбновитьСоставСвойств();
	ЭлементыФормы.ТаблицаОбъектов.ТекущаяСтрока = НоваяСтрока;
	ЭлементыФормы.СвойстваОбъектов.ТекущаяКолонка = ЭлементыФормы.СвойстваОбъектов.Колонки["ПредставлениеЗначения" + НоваяСтрока.НомерСтороны];

КонецПроцедуры

Процедура ТаблицаОбъектовПриИзмененииФлажка(Элемент, Колонка)
	
	ирОбщий.ТабличноеПолеПриИзмененииФлажкаЛкс(ЭтаФорма, Элемент, Колонка);
	
КонецПроцедуры

Процедура ТаблицаОбъектовЭталонПриИзменении(Элемент)
	
	Признак = Ложь;
	Для каждого СтрокаОбъекта Из ТаблицаОбъектов Цикл
		Если СтрокаОбъекта = ЭлементыФормы.ТаблицаОбъектов.ТекущаяСтрока Тогда
			Продолжить;
		КонецЕсли; 
		СтрокаОбъекта.Эталон = Признак;
	КонецЦикла;
	ОбновитьРазностиЧисел();
	
КонецПроцедуры

Процедура ОбновитьРазностиЧисел()
	
	НомерЭталона = ТаблицаОбъектов.Найти(Истина, "Эталон");
	Если НомерЭталона <> Неопределено Тогда
		НомерЭталона = НомерЭталона.НомерСтороны;
	КонецЕсли;
	Для каждого СтрокаОбъекта Из ТаблицаОбъектов Цикл
		ЭлементыФормы.СвойстваОбъектов.Колонки["Разность" + СтрокаОбъекта.НомерСтороны].Видимость = Истина
			И НомерЭталона <> Неопределено
			И СтрокаОбъекта.НомерСтороны <> НомерЭталона;
		КонецЦикла;
		ЭлементыФормы.ТаблицаОбъектов.Колонки.Разность.Видимость = НомерЭталона <> Неопределено;
	Для Каждого СтрокаСвойства Из СвойстваОбъектов Цикл
		Если СтрокаСвойства.Имя = ИмяСвойстваНомерСтороны Тогда
			Продолжить;
		КонецЕсли;
		ЗначениеЭталона = Неопределено;
		Если Истина  
			И СтрокаСвойства.ОписаниеТипов.СодержитТип(Тип("Число")) 
			И НомерЭталона <> Неопределено
		Тогда
			ЗначениеЭталона = СтрокаСвойства["Значение" + НомерЭталона];
		КонецЕсли;
		Для каждого СтрокаОбъекта Из ТаблицаОбъектов Цикл
			РазностьЗначения = Неопределено;
			Если ТипЗнч(ЗначениеЭталона) = Тип("Число") Тогда 
				ЗначениеСвойства = СтрокаСвойства["Значение" + СтрокаОбъекта.НомерСтороны];
				Если ТипЗнч(ЗначениеСвойства) = Тип("Число") Тогда
					РазностьЗначения = ЗначениеСвойства - ЗначениеЭталона;
				КонецЕсли;
			КонецЕсли; 
			СтрокаСвойства["Разность" + СтрокаОбъекта.НомерСтороны] = РазностьЗначения;
		КонецЦикла;
	КонецЦикла;
	СвойстваОбъектовПриАктивизацииСтроки();

КонецПроцедуры

ирОбщий.ИнициироватьФормуЛкс(ЭтаФорма, "Обработка.ирПлатформа.Форма.СравнениеОбъектов");
СвойстваОбъектов.Колонки.Добавить("Значение1");
СвойстваОбъектов.Колонки.Добавить("Значение2");
СвойстваОбъектов.Колонки.Добавить("ЗначениеУстановлено1", Новый ОписаниеТипов("Булево"));
СвойстваОбъектов.Колонки.Добавить("ЗначениеУстановлено2", Новый ОписаниеТипов("Булево"));
ТаблицаОбъектов.Колонки.Добавить("Объект"); 
ТаблицаОбъектов.Колонки.Добавить("Значение"); 
ИмяСвойстваНомерСтороны = "<Номер>";
ТолькоРазличия = Истина;