Процедура ВывестиСообщениеЛкс(Знач ТекстСообщения) Экспорт 
	мПлатформа = ирКэш.Получить();
	#Если Сервер И Не Сервер Тогда
		мПлатформа = Обработки.ирПлатформа.Создать();
	#КонецЕсли
	Если Истина
		И мПлатформа.МодальныеГруппы.Количество() >= КлючУникальности
		И Не мПлатформа.МодальныеГруппы[КлючУникальности - 1].Пометка
	Тогда 
		//Отложенное открытие нужно чтобы форма не открывалсь до фактического открытия модального пространства, если находимся в ПриОткрытии его первой формы https://www.hostedredmine.com/issues/948728
		ПодключитьОбработчикОжидания("ОткрытьОтложенно", 0.1, Истина); 
	Иначе
		Открыть();
	КонецЕсли;
	ТекстовыйДокумент = ЭлементыФормы.Текст;
	НовыйТекст = ТекстСообщения;
	Текст = ТекстовыйДокумент.ПолучитьТекст();
	Если ЗначениеЗаполнено(Текст) Тогда
		НовыйТекст = Текст + Символы.ПС + НовыйТекст;
	КонецЕсли; 
	ТекстовыйДокумент.УстановитьТекст(НовыйТекст);
	СделанаОбрезка = Ложь;
	Пока ТекстовыйДокумент.КоличествоСтрок() > 30 Цикл
		ТекстовыйДокумент.УдалитьСтроку(1);
		СделанаОбрезка = Истина;
	КонецЦикла; 
	Если СделанаОбрезка Тогда
		ТекстовыйДокумент.ВставитьСтроку(1, "...");
	КонецЕсли; 
	НомерСтроки = Макс(1, ТекстовыйДокумент.КоличествоСтрок()); // https://www.hostedredmine.com/issues/891268
	ЭлементыФормы.Текст.УстановитьГраницыВыделения(НомерСтроки, 1, НомерСтроки, 1);
КонецПроцедуры

Процедура ОткрытьОтложенно()
	
	Открыть();

КонецПроцедуры

Процедура ПриЗакрытии()
	ЭлементыФормы.Текст.УстановитьТекст("");
	ирКлиент.Форма_ПриЗакрытииЛкс(ЭтаФорма);
КонецПроцедуры

Процедура ВнешнееСобытие(Источник, Событие, Данные) Экспорт
	ирКлиент.Форма_ВнешнееСобытиеЛкс(ЭтаФорма, Источник, Событие, Данные);
КонецПроцедуры

Процедура ПриОткрытии()
	ирКлиент.Форма_ПриОткрытииЛкс(ЭтаФорма); 
	ирОбщий.ОбновитьТекстПослеМаркераВСтрокеЛкс(ЭтаФорма.Заголовок,, КлючУникальности, ": ");
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник) Экспорт 
	ирКлиент.Форма_ОбработкаОповещенияЛкс(ЭтаФорма, ИмяСобытия, Параметр, Источник); 
КонецПроцедуры

Процедура НадписьСправкаНажатие(Элемент)
	
	ОткрытьСправкуФормы();
	
КонецПроцедуры

ирКлиент.ИнициироватьФормуЛкс(ЭтаФорма, "Обработка.ирПлатформа.Форма.СообщенияМодальнойГруппы")

