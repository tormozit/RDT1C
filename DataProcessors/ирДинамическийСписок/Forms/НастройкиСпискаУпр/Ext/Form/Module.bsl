
&НаКлиенте
Процедура ФиксированныеНастройкиОтборВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ФиксированныеНастройкиОтборПравоеЗначение Тогда
		ПоказатьЗначение(, КомпоновщикНастроек.ФиксированныеНастройки.Отбор.ПолучитьОбъектПоИдентификатору(ВыбраннаяСтрока).ПравоеЗначение);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиОтборВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.НастройкиОтборПравоеЗначение Тогда
		ПоказатьЗначение(, КомпоновщикНастроек.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(ВыбраннаяСтрока).ПравоеЗначение);
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ИсполняемыеНастройки <> Неопределено Тогда
		ЭтаФорма.АдресСхемы = Параметры.ИсполняемаяСхема;
		КомпоновщикРезультата.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Параметры.ИсполняемаяСхема));
		КомпоновщикРезультата.ЗагрузитьНастройки(Параметры.ИсполняемыеНастройки);
		Представление = ПолучитьПредставлениеНастроек(Параметры.ИсполняемыеНастройки);
		Элементы.ИсполняемыеНастройки.Заголовок = Элементы.ИсполняемыеНастройки.Заголовок + Представление;
	КонецЕсли; 
	Представление = ПолучитьПредставлениеНастроек(Параметры.ФиксированныеНастройки);
	Элементы.ФиксированныеНастройки.Заголовок = Элементы.ФиксированныеНастройки.Заголовок + Представление;
	Представление = ПолучитьПредставлениеНастроек(Параметры.Настройки);
	Элементы.ОбычныеНастройки.Заголовок = Элементы.ОбычныеНастройки.Заголовок + Представление;
	Если Параметры.АктивироватьФиксированныйОтбор Тогда
		Элементы.ГруппаПанель.ТекущаяСтраница = Элементы.ГруппаПанель.ПодчиненныеЭлементы.ФиксированныеНастройки;
	КонецЕсли; 
	Элементы.ГруппаПанель.ПодчиненныеЭлементы.ИсполняемыеНастройки.Доступность = Параметры.ИсполняемыеНастройки <> Неопределено;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПредставлениеНастроек(Знач Настройки)
	
	Представление = "";
	Если ЗначениеЗаполнено("" + Настройки.Отбор) Тогда 
		Представление = Представление + ",Отбор";
	КонецЕсли;
	Если ЗначениеЗаполнено("" + Настройки.Порядок) Тогда 
		Представление = Представление + ",Порядок";
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Представление) Тогда
		Представление = ",0";
	КонецЕсли; 
	Представление = "(" + Сред(Представление, 2) + ")";
	Возврат Представление;

КонецФункции

&НаКлиенте
Процедура Применить(Команда = Неопределено)
	
	Если ВладелецФормы <> Неопределено Тогда
		ирОбщий.ДанныеЭлементаФормыЛкс(ВладелецФормы).КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(КомпоновщикНастроек.ПользовательскиеНастройки);
		Если ирКэш.НомерВерсииПлатформыЛкс() >= 803017 Тогда
			ЭтаФорма.НастройкиМодифицированы = Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьИЗакрыть(Команда)
	
	Применить();
	Закрыть(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикРезультатаНастройкиОтборВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.КомпоновщикРезультатаНастройкиОтборПравоеЗначение Тогда
		ПоказатьЗначение(, КомпоновщикРезультата.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(ВыбраннаяСтрока).ПравоеЗначение);
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ФормаПрименить.Доступность = ВладелецФормы <> Неопределено;
	Элементы.ФормаПрименитьИЗакрыть.Доступность = ВладелецФормы <> Неопределено;
	ПодключитьОбработчикОжидания("АктивироватьДоступныеПоляТекущейКолонки", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура АктивироватьДоступныеПоляТекущейКолонки()
	
	ИмяКолонки = ирОбщий.ПутьКДаннымКолонкиТабличногоПоляЛкс(ВладелецФормы);
	Если ЗначениеЗаполнено(ИмяКолонки) Тогда
		Индекс = 0;
		Для Каждого ГруппаНастроек Из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
			Если ТипЗнч(ГруппаНастроек) = Тип("ОтборКомпоновкиДанных") Тогда
				ДоступноеПоле = ГруппаНастроек.ДоступныеПоляОтбора.НайтиПоле(Новый ПолеКомпоновкиДанных(ИмяКолонки));
				ДоступноеПоле = ГруппаНастроек.ДоступныеПоляОтбора.ПолучитьИдентификаторПоОбъекту(ДоступноеПоле);
			ИначеЕсли ТипЗнч(ГруппаНастроек) = Тип("ПорядокКомпоновкиДанных") Тогда
				ДоступноеПоле = ГруппаНастроек.ДоступныеПоляПорядка.НайтиПоле(Новый ПолеКомпоновкиДанных(ИмяКолонки));
				ДоступноеПоле = ГруппаНастроек.ДоступныеПоляПорядка.ПолучитьИдентификаторПоОбъекту(ДоступноеПоле);
			ИначеЕсли ТипЗнч(ГруппаНастроек) = Тип("СтруктураНастроекКомпоновкиДанных") Тогда
				ДоступноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.НайтиПоле(Новый ПолеКомпоновкиДанных(ИмяКолонки));
				ДоступноеПоле = КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.ПолучитьИдентификаторПоОбъекту(ДоступноеПоле);
			Иначе
				ДоступноеПоле = Неопределено;
			КонецЕсли; 
			Если ДоступноеПоле <> Неопределено Тогда
				Элементы["КомпоновщикНастроекПользовательскиеНастройкиЭлемент" + Индекс + "ДоступныеПоляТаблица"].ТекущаяСтрока = ДоступноеПоле;
			КонецЕсли; 
			Индекс = Индекс + 1;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтладитьКомпоновку(Команда)
	
	Схема = ПолучитьИзВременногоХранилища(ЭтаФорма.АдресСхемы);
	ирОбщий.ОтладитьЛкс(Схема,, КомпоновщикРезультата.ПолучитьНастройки());
	
КонецПроцедуры

