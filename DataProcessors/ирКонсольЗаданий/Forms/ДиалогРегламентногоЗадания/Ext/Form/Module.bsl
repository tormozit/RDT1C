Процедура ПриОткрытии()
	
	Для Каждого Метаданное из Метаданные.РегламентныеЗадания Цикл
		ЭлементыФормы.МетаданныеВыбор.СписокВыбора.Добавить(Метаданное, Метаданное.Представление());
	КонецЦикла;
	
	ПользователиИнфобазы = ПользователиИнформационнойБазы.ПолучитьПользователей();
    Для Каждого Пользователь из ПользователиИнфобазы Цикл
		ЭлементыФормы.ПользователиВыбор.СписокВыбора.Добавить(Пользователь.Имя, Пользователь.ПолноеИмя);
	КонецЦикла;

	Если РегламентноеЗадание <> Неопределено Тогда
		МетаданныеВыбор = РегламентноеЗадание.Метаданные;
		ЭлементыФормы.МетаданныеВыбор.ТолькоПросмотр = Истина;
		Наименование = РегламентноеЗадание.Наименование;
		Ключ = РегламентноеЗадание.Ключ;
		Использование = РегламентноеЗадание.Использование;
		ПользователиВыбор = РегламентноеЗадание.ИмяПользователя;
		КоличествоПовторовПриАварийномЗавершении = РегламентноеЗадание.КоличествоПовторовПриАварийномЗавершении;
		ИнтервалПовтораПриАварийномЗавершении = РегламентноеЗадание.ИнтервалПовтораПриАварийномЗавершении;
		Предопределенное = РегламентноеЗадание.Предопределенное;
		Расписание = РегламентноеЗадание.Расписание;
		УникальныйИдентификатор = РегламентноеЗадание.УникальныйИдентификатор;
		Для Индекс = 0 По РегламентноеЗадание.Параметры.ВГраница() Цикл
			СтрокаПараметра = Параметры.Добавить();
			СтрокаПараметра.Значение = РегламентноеЗадание.Параметры[Индекс];
			СтрокаПараметра.ТипЗначения = ТипЗнч(СтрокаПараметра.Значение);
		КонецЦикла;
		МетаданныеВыборПриИзменении();
	Иначе
		Расписание = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	
КонецПроцедуры

Процедура РасписаниеНажатие(Элемент)
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
		
	Если Диалог.ОткрытьМодально() Тогда
		Расписание = Диалог.Расписание;
	КонецЕсли;
КонецПроцедуры

Процедура OK(Кнопка)
	Попытка
		Если МетаданныеВыбор = Неопределено Тогда
			ВызватьИсключение("Не выбраны метаданные регламентного задания.");
		КонецЕсли;
		
		Если РегламентноеЗадание = Неопределено Тогда
			РегламентноеЗадание = РегламентныеЗадания.СоздатьРегламентноеЗадание(МетаданныеВыбор);
		КонецЕсли;
		
		РегламентноеЗадание.Наименование = Наименование;
		РегламентноеЗадание.Ключ = Ключ;
		РегламентноеЗадание.Использование = Использование;
		РегламентноеЗадание.ИмяПользователя = ПользователиВыбор;
		РегламентноеЗадание.КоличествоПовторовПриАварийномЗавершении = КоличествоПовторовПриАварийномЗавершении;
		РегламентноеЗадание.ИнтервалПовтораПриАварийномЗавершении = ИнтервалПовтораПриАварийномЗавершении;
		РегламентноеЗадание.Расписание = Расписание;

		// Антибаг 8.2.15. Добавление в текущий массив не работает. http://partners.v8.1c.ru/forum/thread.jsp?id=1012620#1012620
		////РегламентноеЗадание.Параметры.Очистить();
		//Для Индекс = 0 По Параметры.Количество() - 1 Цикл
		//	РегламентноеЗадание.Параметры.Добавить(Параметры[Индекс].Значение);
		//КонецЦикла;
		РегламентноеЗадание.Параметры = Параметры.ВыгрузитьКолонку("Значение");

		РегламентноеЗадание.Записать();
		
		Закрыть(Истина);
	Исключение	
		ПоказатьИнформациюОбОшибке(ИнформацияОбОшибке());	
	КонецПопытки;
КонецПроцедуры

Процедура ПараметрыЗначениеКонстантыПриИзменении(Элемент)
	
	ЭлементыФормы.Параметры.ТекущаяСтрока.ТипЗначения = ТипЗнч(Элемент.Значение);
	
КонецПроцедуры

Процедура ПараметрыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.Номер.УстановитьТекст(Параметры.Индекс(ДанныеСтроки) + 1);
	ирОбщий.ТабличноеПоле_ОтобразитьПиктограммыТиповЛкс(ОформлениеСтроки, "Значение");

КонецПроцедуры

Процедура КоманднаяПанельПараметрыИсследоватьПараметры(Кнопка)
	
	ирОбщий.ИсследоватьЛкс(Параметры,,Истина);
	
КонецПроцедуры

Процедура ПараметрыЗначениеОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	ирОбщий.ПолеВвода_ОкончаниеВводаТекстаЛкс(Элемент, Текст, Значение, СтандартнаяОбработка);

КонецПроцедуры

Процедура ПараметрыЗначениеНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ирОбщий.ПолеВводаКолонкиРасширенногоЗначения_НачалоВыбораЛкс(ЭлементыФормы.Параметры, СтандартнаяОбработка,, Истина);
	ПараметрыЗначениеКонстантыПриИзменении(ЭлементыФормы.Параметры.Колонки.Значение.ЭлементУправления);
	
КонецПроцедуры

Процедура МетаданныеВыборПриИзменении(Элемент = Неопределено)
	
	ЭтаФорма.Метод = Метаданные.РегламентныеЗадания[МетаданныеВыбор.Имя].ИмяМетода;
	
КонецПроцедуры

ирОбщий.ИнициализироватьФормуЛкс(ЭтаФорма, "Обработка.ирКонсольЗаданий.Форма.ДиалогРегламентногоЗадания");

