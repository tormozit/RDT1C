Перем мСтруктураПоискаВДереве;
Перем мТекущийИндексНайденнойСтроки;
Перем мТекущаяСтрока;
Перем мУсловноеОформлениеФормы;

Процедура ПриОткрытии()
	
	ирОбщий.Форма_ПриОткрытииЛкс(ЭтаФорма);
	ЭтаФорма.РежимПодсистемы = КлючУникальности = "ВсеИнструменты";
	Если РежимПодсистемы Тогда
		ЭтаФорма.Заголовок = "Структура всех инструментов (ИР)";
	КонецЕсли; 
	ОбновитьДерево();
	Если ПараметрЭлементФормы = Неопределено Тогда
		Если Форма <> Неопределено Тогда
			ПараметрЭлементФормы = Форма.ТекущийЭлемент;
			Если ТипЗнч(ПараметрЭлементФормы) = Тип("ТабличноеПоле") Тогда
				Если ПараметрЭлементФормы.ТекущаяКолонка <> Неопределено Тогда
					Если Дерево.Строки.Найти(ПараметрЭлементФормы.ТекущаяКолонка, "ЭлементФормы", Истина) <> Неопределено Тогда 
						ПараметрЭлементФормы = ПараметрЭлементФормы.ТекущаяКолонка;
					Иначе
						// Отбор компоновки
					КонецЕсли; 
				КонецЕсли; 
			ИначеЕсли ТипЗнч(ПараметрЭлементФормы) = Тип("ТаблицаФормы") Тогда
				Если ПараметрЭлементФормы.ТекущийЭлемент <> Неопределено Тогда
					ПараметрЭлементФормы = ПараметрЭлементФормы.ТекущийЭлемент;
				КонецЕсли; 
			ИначеЕсли ТипЗнч(ПараметрЭлементФормы) = Тип("Панель") Тогда
				Если ПараметрЭлементФормы.ТекущаяСтраница <> Неопределено Тогда
					ПараметрЭлементФормы = ПараметрЭлементФормы.ТекущаяСтраница;
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	Если ПараметрЭлементФормы <> Неопределено Тогда
		СтрокаЭлементаФормы = Дерево.Строки.Найти(ПараметрЭлементФормы, "ЭлементФормы", Истина);
		Если СтрокаЭлементаФормы <> Неопределено Тогда
			ЭлементыФормы.Дерево.ТекущаяСтрока = СтрокаЭлементаФормы;
			ЭлементыФормы.Дерево.Развернуть(СтрокаЭлементаФормы);
		КонецЕсли; 
	КонецЕсли; 
	
	#Если Сервер И Не Сервер Тогда
		СвернутьПанели();
	#КонецЕсли
	ПодключитьОбработчикОжидания("СвернутьПанели", 0.1, Истина);
	ПоказатьСвязаннуюФорму();
	#Если Сервер И Не Сервер Тогда
		ИндикацияТекущегоЭлемента();
	#КонецЕсли
	ПодключитьОбработчикОжидания("ИндикацияТекущегоЭлемента", 0.2, Истина);
	
КонецПроцедуры

Функция ПолучитьПервогоРодителяПоТипу(Знач СтрокаДерева, Тип)
	
	Пока СтрокаДерева.Родитель <> Неопределено Цикл 
		СтрокаДерева = СтрокаДерева.Родитель;
		Если СтрокаДерева.Тип = Тип Тогда 
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	Возврат СтрокаДерева;
	
КонецФункции

Функция ПолучитьПоследнегоРодителяПоТипу(Знач СтрокаДерева, Тип)
	
	Пока Истина
		И СтрокаДерева.Родитель <> Неопределено 
		И СтрокаДерева.Родитель.Тип = Тип
	Цикл
		СтрокаДерева = СтрокаДерева.Родитель;
	КонецЦикла; 
	Возврат СтрокаДерева;
	
КонецФункции

Процедура ИндикацияТекущегоЭлемента()
	
	Если Не АктивнаяПодсветка Тогда
		ВосстановитьСвойстваЭлементовФормы();
	КонецЕсли; 
	ТекущаяСтрока = ЭлементыФормы.Дерево.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Перейти ~Конец;
	КонецЕсли; 
	Если РежимПодсистемы Тогда
		Форма = Неопределено;
		СтрокаФормы = ТекущаяСтрока;
		ОсновнойЛ = Ложь;
		Пока СтрокаФормы.Родитель <> Неопределено Цикл
			Если СтрокаФормы.Основной Тогда 
				Если СтрокаФормы.ЭлементФормы.Открыта() Тогда
					Форма = СтрокаФормы.ЭлементФормы;
				КонецЕсли; 
				Если ТекущаяСтрока = СтрокаФормы Тогда
					Перейти ~Конец;
				КонецЕсли; 
				Прервать;
			КонецЕсли; 
			СтрокаФормы = СтрокаФормы.Родитель;
		КонецЦикла;
	КонецЕсли; 
	Если Форма = Неопределено Тогда
		Перейти ~Конец;
	КонецЕсли; 
	//Попытка
	//	ТекущаяСтрока.Видимость = ЭлементФормыСтрокиДерева(ТекущаяСтрока).Видимость;
	//Исключение
	//КонецПопытки; 
	//Попытка
	//	ТекущаяСтрока.Доступность = ЭлементФормыСтрокиДерева(ТекущаяСтрока).Доступность;
	//Исключение
	//КонецПопытки; 
	ЭлементФормы = ЭлементФормыСтрокиДерева(ТекущаяСтрока);
	Если ЭлементФормы = Неопределено Тогда
		//
	ИначеЕсли ТекущаяСтрока.Тип = Тип("КнопкаКоманднойПанели") Тогда
		СтрокаДереваКоманднойПанели = ПолучитьПервогоРодителяПоТипу(ТекущаяСтрока, Тип("КоманднаяПанель"));
		ПоказатьВложенныйЭлементФормы(СтрокаДереваКоманднойПанели);
		СтрокаВерхнегоПодменю = ПолучитьПоследнегоРодителяПоТипу(ТекущаяСтрока, Тип("КнопкаКоманднойПанели"));
		ЭлементФормы = ЭлементФормыСтрокиДерева(СтрокаВерхнегоПодменю); 
		ИндикацияЭлементаФормыСЗаголовком(СтрокаВерхнегоПодменю, Новый Структура("Пометка, Отображение", Не ЭлементФормы.Пометка, ОтображениеКнопкиКоманднойПанели.НадписьКартинка), "Текст");
	ИначеЕсли ТекущаяСтрока.Тип = Тип("КолонкаТабличногоПоля") Тогда
		СтрокаДереваТабличногоПоля = ПолучитьПервогоРодителяПоТипу(ТекущаяСтрока, Тип("ТабличноеПоле"));
		ирОбщий.ПрисвоитьЕслиНеРавноЛкс(Форма.ТекущийЭлемент, ЭлементФормыСтрокиДерева(СтрокаДереваТабличногоПоля),, Истина);
		ЭлементФормы = ЭлементФормыСтрокиДерева(ТекущаяСтрока);
		ИндикацияЭлементаФормыСЗаголовком(ТекущаяСтрока, Новый Структура("ОтображатьВШапке, ЦветФонаПоля, ЦветФонаШапки, ЦветТекстаШапки", Истина), "ТекстШапки");
		ЭлементФормы.Видимость = Истина;
		ирОбщий.ПрисвоитьЕслиНеРавноЛкс(ЭлементФормыСтрокиДерева(СтрокаДереваТабличногоПоля).ТекущаяКолонка, ЭлементФормы,, Истина);
	ИначеЕсли ТекущаяСтрока.Тип = Тип("СтраницаПанели") Тогда
		СтрокаДереваПанели = ПолучитьПервогоРодителяПоТипу(ТекущаяСтрока, Тип("Панель"));
		ирОбщий.ПрисвоитьЕслиНеРавноЛкс(Форма.ТекущийЭлемент, ЭлементФормыСтрокиДерева(СтрокаДереваПанели));
		Если ТекущаяСтрока.Видимость Тогда
			ирОбщий.ПрисвоитьЕслиНеРавноЛкс(ЭлементФормыСтрокиДерева(СтрокаДереваПанели).ТекущаяСтраница, ЭлементФормыСтрокиДерева(ТекущаяСтрока),, Истина);
			ИндикацияЭлементаФормыСЗаголовком(ТекущаяСтрока);
		КонецЕсли; 
	ИначеЕсли Ложь
		Или ТекущаяСтрока.Тип = Тип("КнопкаФормы") 
		Или (Истина
			И ТекущаяСтрока.Тип = Тип("ГруппаФормы") 
			И (Ложь
				Или ЭлементФормы.Вид = ВидГруппыФормы.Подменю
				Или ЭлементФормы.Вид = ВидГруппыФормы.ГруппаКнопок))
	Тогда
		СтрокаВерхнегоПодменю = ТекущаяСтрока;
		ТекущийРодитель = ТекущаяСтрока;
		Пока Истина
			И ТекущийРодитель.Родитель.ЭлементФормы <> Форма 
			И ТекущийРодитель.Родитель.ЭлементФормы.Вид <> ВидГруппыФормы.КоманднаяПанель 
			И ТекущийРодитель.Родитель.ЭлементФормы.Вид <> ВидГруппыФормы.КонтекстноеМеню
		Цикл
			ТекущийРодитель = ТекущийРодитель.Родитель;
			Если ТекущийРодитель.ЭлементФормы.Вид = ВидГруппыФормы.Подменю Тогда
				СтрокаВерхнегоПодменю = ТекущийРодитель;
			КонецЕсли; 
		КонецЦикла; 
		ЭлементФормы = ЭлементФормыСтрокиДерева(СтрокаВерхнегоПодменю);
		Если ТипЗнч(ЭлементФормы) = Тип("КнопкаФормы") Тогда
			//СтруктураСвойств = Новый Структура("Отображение, ЦветФона, ЦветТекста", ОтображениеКнопки.Текст);
			СтруктураСвойств = Новый Структура("ЦветФона, ЦветТекста");
			СтруктураСвойств.Вставить("Пометка", Не ЭлементФормы.Пометка);
		Иначе
			СтруктураСвойств = Новый Структура("ЦветТекстаЗаголовка");
		КонецЕсли; 
		Если ирКэш.НомерВерсииПлатформыЛкс() >= 803008 Тогда
			СтруктураСвойств.Вставить("ОтображениеФигуры", Вычислить("ОтображениеФигурыКнопки.Всегда"));
		КонецЕсли; 
		ИндикацияЭлементаФормыСЗаголовком(СтрокаВерхнегоПодменю, СтруктураСвойств);
	ИначеЕсли ТекущаяСтрока.Тип = Тип("ПолеФормы") Тогда 
		//  Антибаг платформы 8.3.15 После присвоения Неопределено свойству ТекущийЭлемент таблицы формы можно присвоить колонку с отключенной пользовательской видимостью https://partners.v8.1c.ru/forum/topic/1912733
		ТаблицаФормы = ирОбщий.РодительЭлементаУправляемойФормыЛкс(ЭлементФормы, Тип("ТаблицаФормы"));
		Если ТаблицаФормы <> Неопределено Тогда
			СтараяТекущаяКолонка = ТаблицаФормы.ТекущийЭлемент;
		КонецЕсли; 
		Форма.ТекущийЭлемент = ЭлементФормы;
		Если ТекущаяСтрока.Доступность Тогда
			ТекущаяСтрока.Видимость = Ложь
				Или Форма.ТекущийЭлемент = ЭлементФормы
				Или ТипЗнч(Форма.ТекущийЭлемент) = Тип("ТаблицаФормы") И Форма.ТекущийЭлемент.ТекущийЭлемент = ЭлементФормы;
			//  Антибаг платформы 8.3.15 После присвоения Неопределено свойству ТекущийЭлемент таблицы формы можно присвоить колонку с отключенной пользовательской видимостью https://partners.v8.1c.ru/forum/topic/1912733
			Если ТаблицаФормы <> Неопределено И ТаблицаФормы.ТекущийЭлемент <> ЭлементФормы Тогда
				ТаблицаФормы.ТекущийЭлемент = СтараяТекущаяКолонка;
			КонецЕсли; 
		КонецЕсли; 
		Если ирОбщий.РодительЭлементаУправляемойФормыЛкс(ЭлементФормы, Тип("ТаблицаФормы")) <> Неопределено Тогда 
			ИндикацияЭлементаФормыСЗаголовком(ТекущаяСтрока, Новый Структура("ОтображатьВШапке, ЦветТекстаЗаголовка, ЦветФонаЗаголовка", Истина));
		Иначе
			ИндикацияЭлементаФормыСЗаголовком(ТекущаяСтрока, Новый Структура("ЦветТекстаЗаголовка, ЦветФона, ЦветФонаЗаголовка, ЦветРамки"));
		КонецЕсли; 
	ИначеЕсли ТекущаяСтрока.Тип = Тип("ГруппаФормы") Тогда 
		Форма.ТекущийЭлемент = ЭлементФормы;
		Если ЭлементФормы.Вид = ВидГруппыФормы.ОбычнаяГруппа Тогда 
			ИндикацияЭлементаФормыСЗаголовком(ТекущаяСтрока, Новый Структура("ЦветТекстаЗаголовка, ЦветФона"));
		ИначеЕсли ЭлементФормы.Вид = ВидГруппыФормы.ГруппаКолонок Тогда 
			ИндикацияЭлементаФормыСЗаголовком(ТекущаяСтрока, Новый Структура("ОтображатьЗаголовок, ОтображатьВШапке, ЦветТекстаЗаголовка, ЦветФонаЗаголовка", Истина, Истина));
		Иначе
			ИндикацияПроизвольногоЭлемента(ТекущаяСтрока);
		КонецЕсли; 
	ИначеЕсли ТекущаяСтрока.Тип = Тип("ДекорацияФормы") Тогда 
		Форма.ТекущийЭлемент = ЭлементФормы;
		ИндикацияЭлементаФормыСЗаголовком(ТекущаяСтрока, Новый Структура("ЦветТекста, ЦветРамки"));
	ИначеЕсли ТекущаяСтрока.Видимость Тогда
		Форма.ТекущийЭлемент = ЭлементФормы;
		Если Форма.ТекущийЭлемент <> ЭлементФормы Тогда
			ПоказатьВложенныйЭлементФормы(ТекущаяСтрока);
		КонецЕсли; 
		ИндикацияПроизвольногоЭлемента(ТекущаяСтрока);
	КонецЕсли;
~Конец: 
	ПодключитьОбработчикОжидания("ИндикацияТекущегоЭлемента", 0.5, Истина);
	
КонецПроцедуры

Процедура ИндикацияПроизвольногоЭлемента(Знач ТекущаяСтрока)
	
	Если Не АктивнаяПодсветка Тогда
		Возврат;
	КонецЕсли; 
	ЭлементФормы = ЭлементФормыСтрокиДерева(ТекущаяСтрока);
	Если ТипЗнч(Форма) = Тип("Форма") Тогда 
		ИмяСвойства = "ЦветРамки";
		Если Ложь
			Или ТекущаяСтрока.Тип = Тип("Надпись")
			Или ТекущаяСтрока.Тип = Тип("Флажок")
			Или ТекущаяСтрока.Тип = Тип("КоманднаяПанель")
			Или ТекущаяСтрока.Тип = Тип("ПолеВвода")
			Или ТекущаяСтрока.Тип = Тип("ПолеВыбора")
		Тогда
			Если Истина
				И (Ложь
					Или ТекущаяСтрока.Тип = Тип("ПолеВвода") 
					Или ТекущаяСтрока.Тип = Тип("ПолеВыбора"))
				И Не ЭлементФормы.ТолькоПросмотр
				И ЭлементФормы.Доступность
			Тогда 
				ИмяСвойства = "ЦветФонаПоля";
			Иначе
				ИмяСвойства = "ЦветФона";
			КонецЕсли; 
		КонецЕсли; 
		Если ТипЗнч(ЭлементФормы) = Тип("КоманднаяПанель") Тогда 
			ДопСвойства = Новый Структура("ПрозрачныйФон", Ложь); // https://www.hostedredmine.com/issues/923456
		КонецЕсли; 
	Иначе
		ИмяСвойства = "";
		// Только для колонок
		Если Истина
			И ИмяСвойства = "" 
			И ТипЗнч(ЭлементФормы) <> Тип("ТаблицаФормы") // Антибаг платформы 8.3.17 Аварийно завершается на некоторых формах (список Алгоритмы). Видимо из-за свойства ЦветТекстаЗаголовка https://partners.v8.1c.ru/forum/t/1911713/m/1911713 , http://www.hostedredmine.com/issues/872715
			И (Ложь
				Или ирОбщий.РодительЭлементаУправляемойФормыЛкс(ЭлементФормы, Тип("ТаблицаФормы")) <> Неопределено 
				Или (Истина
			И ТипЗнч(ЭлементФормы) = Тип("ГруппаФормы") 
			И ЭлементФормы.Вид = ВидГруппыФормы.КоманднаяПанель))
		Тогда
			ИмяСвойства = "ЦветТекстаЗаголовка";
			Попытка
				ТекущийЦвет = ЭлементФормы[ИмяСвойства];
			Исключение
				ИмяСвойства = "";
			КонецПопытки; 
		КонецЕсли; 
		Если ИмяСвойства = "" И ТекущаяСтрока.Тип <> Тип("ПолеФормы") Тогда
			ИмяСвойства = "ЦветРамки";
			Попытка
				ТекущийЦвет = ЭлементФормы[ИмяСвойства];
			Исключение
				ИмяСвойства = "";
			КонецПопытки; 
		КонецЕсли; 
		Если ИмяСвойства = "" Тогда
			ИмяСвойства = "ЦветФона";
			Попытка
				ТекущийЦвет = ЭлементФормы[ИмяСвойства];
			Исключение
				ИмяСвойства = "";
			КонецПопытки;
		КонецЕсли; 
	КонецЕсли; 
	Если ЗначениеЗаполнено(ИмяСвойства) Тогда
		Если Истина
			И ТекущаяСтрока.СтарыеСвойства <> Неопределено
			И ТекущаяСтрока.СтарыеСвойства[ИмяСвойства] <> ТекущийЦвет 
		Тогда
			ВосстановитьСтарыеСвойстваЭлементаФормы(ТекущаяСтрока); 
		Иначе
			СохранитьСтарыеСвойстваЭлементаФормы(ТекущаяСтрока, ИмяСвойства, ДопСвойства);
			Если ДопСвойства <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(ЭлементФормы, ДопСвойства); 
			КонецЕсли; 
			ЦветПодсветки = ПолучитьЦветИндикации(ИмяСвойства, ЭлементФормы);
			ЭлементФормы[ИмяСвойства] = ЦветПодсветки;
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры

Процедура ИндикацияЭлементаФормыСЗаголовком(Знач ТекущаяСтрока, СтруктураСвойств = Неопределено, ИмяСвойстваЗаголовок = "Заголовок")
	
	Если Не АктивнаяПодсветка Тогда
		Возврат;
	КонецЕсли; 
	Если СтруктураСвойств = Неопределено Тогда
		СтруктураСвойств = Новый Структура;
	КонецЕсли; 
	ЭлементФормы = ЭлементФормыСтрокиДерева(ТекущаяСтрока);
	Если ТекущаяСтрока.СтарыеСвойства <> Неопределено Тогда
		ВосстановитьСтарыеСвойстваЭлементаФормы(ТекущаяСтрока); 
	Иначе
		Если ТипЗнч(Форма) = Тип("Форма") Тогда
			Маркер = "<<<<!>>>>";
			СтруктураСвойств.Вставить(ИмяСвойстваЗаголовок, Маркер);
		КонецЕсли; 
		ИменаСвойств = Новый Массив;
		Для Каждого КлючИЗначение Из СтруктураСвойств Цикл
			Если КлючИЗначение.Значение = Неопределено Тогда
				Если Найти(КлючИЗначение.Ключ, "Цвет") > 0 Тогда
					Попытка
						ТекущийЦвет = ЭлементФормы[КлючИЗначение.Ключ];
					Исключение
						ТекущийЦвет = Неопределено;
					КонецПопытки;
					Если ТекущийЦвет <> Неопределено Тогда
						СтруктураСвойств.Вставить(КлючИЗначение.Ключ, ПолучитьЦветИндикации(КлючИЗначение.Ключ, ЭлементФормы));
					КонецЕсли; 
				КонецЕсли; 
			КонецЕсли; 
			ИменаСвойств.Добавить(КлючИЗначение.Ключ);
		КонецЦикла; 
		СохранитьСтарыеСвойстваЭлементаФормы(ТекущаяСтрока, ирОбщий.СтрСоединитьЛкс(ИменаСвойств));
		Если ТипЗнч(ЭлементФормы) = Тип("КнопкаФормы") Тогда
			ЭлементФормы = ЭлементФормы.Родитель.ПодчиненныеЭлементы[ЭлементФормы.Имя];
		КонецЕсли; 
		ЗаполнитьЗначенияСвойств(ЭлементФормы, СтруктураСвойств);
	КонецЕсли;

КонецПроцедуры

Процедура ПоказатьВложенныйЭлементФормы(Знач ТекущаяСтрока)
	
	Если Истина
		И ТекущаяСтрока.Родитель <> Неопределено 
		И ТекущаяСтрока.Родитель.Родитель <> Неопределено 
		И ТекущаяСтрока.Родитель.Родитель.Тип = Тип("Панель")
	Тогда
		ЭлементФормыСтроки = ЭлементФормыСтрокиДерева(ТекущаяСтрока.Родитель.Родитель);
		Если ТипЗнч(ЭлементФормыСтроки) <> Тип("Панель") Тогда 
			ирОбщий.ПрисвоитьЕслиНеРавноЛкс(Форма.ТекущийЭлемент, ЭлементФормыСтроки);
		Иначе
			// Панель не может являться текущим элементом, а попытка ее присвоения вызывает невозможность выделять текст в полях ввода https://www.hostedredmine.com/issues/923455 
		КонецЕсли; 
		ирОбщий.ПрисвоитьЕслиНеРавноЛкс(ЭлементФормыСтроки.ТекущаяСтраница, ЭлементФормыСтрокиДерева(ТекущаяСтрока.Родитель));
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьЦветИндикации(ИмяСвойства = "", ЭлементФормы = Неопределено)
	
	ирОбщий.ТребоватьТипЛкс(ИмяСвойства,, Тип("Строка"));
	Результат = Новый Цвет(255, 1, 1);
	Если ЗначениеЗаполнено(ИмяСвойства) Тогда
		Если Найти(ИмяСвойства, "Фон") > 0 Тогда
			ТекущийЦвет = ЭлементФормы[ИмяСвойства];
			Если ТекущийЦвет <> Неопределено Тогда
				Результат = ирОбщий.СмещенныйЦветЛкс(ТекущийЦвет, -50, -50);
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	Возврат Результат;

КонецФункции

Процедура СохранитьСтарыеСвойстваЭлементаФормы(Знач ТекущаяСтрока, СтрокаСвойств, ДопСвойства = Неопределено)
	
	Если ТекущаяСтрока.СтарыеСвойства = Неопределено Тогда
		СтарыеСвойства = Новый Структура(СтрокаСвойств);
		Если ДопСвойства <> Неопределено Тогда
			ирОбщий.СкопироватьУниверсальнуюКоллекциюЛкс(ДопСвойства, СтарыеСвойства);
		КонецЕсли; 
		ЗаполнитьЗначенияСвойств(СтарыеСвойства, ЭлементФормыСтрокиДерева(ТекущаяСтрока));
		ТекущаяСтрока.СтарыеСвойства = СтарыеСвойства;
	КонецЕсли; 
	Если ТекущаяСтрока.СтарыеСвойстваТекущейСтроки = Неопределено Тогда
		СтарыеСвойстваТекущейСтроки = Новый Структура("Видимость");
		ЗаполнитьЗначенияСвойств(СтарыеСвойстваТекущейСтроки, ЭлементФормыСтрокиДерева(ТекущаяСтрока));
		ТекущаяСтрока.СтарыеСвойстваТекущейСтроки = СтарыеСвойстваТекущейСтроки;
	КонецЕсли; 

КонецПроцедуры

Процедура ВосстановитьСтарыеСвойстваЭлементаФормы(Знач ТекущаяСтрока, ВосстанавливатьВидимостьПоТекущейСтроке = Ложь)
	
	Если ТекущаяСтрока.СтарыеСвойства <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭлементФормыСтрокиДерева(ТекущаяСтрока), ТекущаяСтрока.СтарыеСвойства);
		ТекущаяСтрока.СтарыеСвойства = Неопределено;
	КонецЕсли; 
	Если ВосстанавливатьВидимостьПоТекущейСтроке Тогда
		Если ТекущаяСтрока.СтарыеСвойстваТекущейСтроки <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ЭлементФормыСтрокиДерева(ТекущаяСтрока), ТекущаяСтрока.СтарыеСвойстваТекущейСтроки);
			ТекущаяСтрока.СтарыеСвойстваТекущейСтроки = Неопределено;
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры

Функция ЭлементФормыСтрокиДерева(Знач ТекущаяСтрока, РазрешитьФорму = Ложь)
	
	ЭлементФормы = ТекущаяСтрока.ЭлементФормы;
	Если Истина
		И ЭлементФормы = Неопределено 
		И Форма <> Неопределено
	Тогда
		ЭлементФормы = Форма.Элементы[ТекущаяСтрока.Имя];
	КонецЕсли;
	Если Истина
		И Не РазрешитьФорму
		И (Ложь
			Или ТипЗнч(ЭлементФормы) = Тип("Форма") 
			Или ТипЗнч(ЭлементФормы) = Тип("УправляемаяФорма"))
	Тогда
		ЭлементФормы = Неопределено;
	КонецЕсли; 
	Если ТипЗнч(ЭлементФормы) = Тип("Структура") Тогда
		ЭлементФормы = Неопределено;
	КонецЕсли; 
	Возврат ЭлементФормы;

КонецФункции

Процедура ОбновитьДерево()
	
	//СтарыеКоординаты = ПолучитьКоординаты();
	Если Не РежимПодсистемы Тогда
		Если мТекущаяСтрока <> Неопределено Тогда
			ВосстановитьСтарыеСвойстваЭлементаФормы(мТекущаяСтрока);
		КонецЕсли;
		мТекущаяСтрока = Неопределено;
		Дерево.Строки.Очистить();
		Если Форма <> Неопределено Тогда
			ДобавитьФорму(Форма);
		КонецЕсли; 
		СортироватьСтроки(Дерево);
	Иначе
		ЗаполнитьСписокИнструментов();
		Индикатор2 = ирОбщий.ПолучитьИндикаторПроцессаЛкс(СписокИнструментов.Количество());
		Для Каждого СтрокаИнструмента Из СписокИнструментов Цикл
			ирОбщий.ОбработатьИндикаторЛкс(Индикатор2);
			КорневойТип = ирОбщий.ПервыйФрагментЛкс(СтрокаИнструмента.ПолноеИмя);
			Если Истина
				И КорневойТип <> "Обработка"
				И КорневойТип <> "Отчет"
			Тогда
				Продолжить;
			КонецЕсли; 
			Объект = ирОбщий.СоздатьОбъектПоПолномуИмениМетаданныхЛкс(СтрокаИнструмента.ПолноеИмя);
			МетаОбъект = Объект.Метаданные();
			Если Дерево.Строки.Найти(МетаОбъект.ПолноеИмя(), "Имя") <> Неопределено Тогда
				Продолжить;
			КонецЕсли; 
			Попытка
				МетаФормы = МетаОбъект.Формы;
			Исключение
				Продолжить;
			КонецПопытки;
			СтрокаДереваОбъекта = Неопределено;
			//МенеджерОбъектаМетаданных = ирОбщий.ПолучитьМенеджерЛкс(МетаОбъект);
			МенеджерОбъектаМетаданных = ирОбщий.СоздатьОбъектПоПолномуИмениМетаданныхЛкс(МетаОбъект.ПолноеИмя());
			Индикатор3 = ирОбщий.ПолучитьИндикаторПроцессаЛкс(МетаФормы.Количество(), "Формы");
			Для Каждого МетаФорма Из МетаФормы Цикл
				ирОбщий.ОбработатьИндикаторЛкс(Индикатор3);
				ПолноеИмяФормы = МетаФорма.ПолноеИмя();
				Попытка
					ФормаЛ = МенеджерОбъектаМетаданных.ПолучитьФорму(МетаФорма.Имя,,Новый УникальныйИдентификатор());
				Исключение
					ирОбщий.СообщитьЛкс("Ошибка при получении формы " + ПолноеИмяФормы + ": " + ОписаниеОшибки(), СтатусСообщения.ОченьВажное);
					Продолжить;
				КонецПопытки;
				Если ТипЗнч(ФормаЛ) = Тип("Форма") Тогда
					Если СтрокаДереваОбъекта = Неопределено Тогда
						СтрокаДереваОбъекта = Дерево.Строки.Добавить();
						СтрокаДереваОбъекта.Текст = МетаОбъект.Представление();
						СтрокаДереваОбъекта.Имя = МетаОбъект.ПолноеИмя();
						СтрокаДереваОбъекта.Видимость = Истина;
						СтрокаДереваОбъекта.Доступность = Истина;
						СтрокаДереваОбъекта.ПредставлениеТипа = "Объект";
						Если ЗначениеЗаполнено(СтрокаИнструмента.ИмяКартинки) Тогда
							СтрокаДереваОбъекта.Картинка = ирКэш.КартинкаПоИмениЛкс(СтрокаИнструмента.ИмяКартинки);
						КонецЕсли; 
					КонецЕсли; 
					СтрокаДереваФормы = СтрокаДереваОбъекта.Строки.Добавить();
					СтрокаДереваФормы.Текст = МетаФорма.Представление();
					СтрокаДереваФормы.Имя = МетаФорма.ПолноеИмя();
					СтрокаДереваФормы.Видимость = Истина;
					СтрокаДереваФормы.Доступность = Истина;
					СтрокаДереваФормы.ПредставлениеТипа = "Форма";
					СтрокаДереваФормы.Картинка = ФормаЛ.КартинкаЗаголовка;
					СтрокаДереваФормы.ЭлементФормы = ФормаЛ;
					ДобавитьФорму(ФормаЛ, СтрокаДереваФормы);
					Если МетаОбъект.ОсновнаяФорма = МетаФорма Тогда 
						СтрокаДереваФормы.Основной = Истина;
						СтрокаДереваФормы.Текст = СтрокаДереваФормы.Текст + " (Основная)";
					КонецЕсли; 
				КонецЕсли; 
			КонецЦикла;
			ирОбщий.ОсвободитьИндикаторПроцессаЛкс();
		КонецЦикла;
		ирОбщий.ОсвободитьИндикаторПроцессаЛкс();
	КонецЕсли; 
	
КонецПроцедуры

Процедура ДобавитьФорму(Форма, КорневаяСтрокаФормы = Неопределено)
	
	Если Форма = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	СтрокаФормы = ЗначениеВСтрокуВнутр(Форма);
	XMLСтрокаФормы = ирОбщий.СтрокаВнутрВХМЛТелоЛкс(СтрокаФормы);
	//ирОбщий.ИсЛкс(XMLСтрокаФормы)
	ДокументDOM = ирОбщий.ПрочитатьТекстВДокументDOMЛкс(XMLСтрокаФормы);
	#Если Сервер И Не Сервер Тогда
		ДокументDOM = Новый ДокументDOM;
	#КонецЕсли
	РазыменовательПИ = Новый РазыменовательПространствИменDOM(ДокументDOM);
	Если ТипЗнч(Форма) = Тип("Форма") Тогда
		Если КорневаяСтрокаФормы = Неопределено Тогда
			КорневаяСтрокаФормы = Дерево.Строки.Добавить();
			КорневаяСтрокаФормы.Видимость = Истина;
			КорневаяСтрокаФормы.Доступность = Истина;
			КорневаяСтрокаФормы.Текст = "Обычная форма";
			КорневаяСтрокаФормы.ЭлементФормы = Форма;
		КонецЕсли; 
		КорневаяСтрокаФормы.Тип = ТипЗнч(Форма);
		СлужебныеДанныеФормы = ирОбщий.СлужебныеДанныеФормыЛкс(Форма);
		Если СлужебныеДанныеФормы.Свойство("ИмяФормы") Тогда
			КорневаяСтрокаФормы.Имя = СлужебныеДанныеФормы.ИмяФормы;
		КонецЕсли; 
		СтрокаXPath = "/elem/elem/elem[1]/elem[2]/elem[2]";
		РезультатXPath = ДокументDOM.ВычислитьВыражениеXPath(СтрокаXPath, ДокументDOM, РазыменовательПИ, ТипРезультатаDOMXPath.НеупорядоченныйИтераторУзлов);
		Узел = РезультатXPath.ПолучитьСледующий();
		КорневойЭлемент = Форма.Панель;
		Если КорневойЭлемент.Страницы.Количество() = 1 Тогда
			СтрокаДЗ = КорневаяСтрокаФормы;
			КорневойЭлемент = КорневойЭлемент.Страницы[0];
		Иначе
			СтрокаДЗ = КорневаяСтрокаФормы.Строки.Добавить();
			СтрокаДЗ.ЭлементФормы = Форма.Панель;
			ЗаполнитьСтрокуЭлементаОбычнойФормы(КорневаяСтрокаФормы);
		КонецЕсли; 
		Если Узел <> Неопределено Тогда
			ОбойтиУзелДереваЭлементовОбычнойФормы(КорневаяСтрокаФормы, Форма, Узел, СтрокаДЗ, КорневойЭлемент);
			//Если КорневойЭлемент <> Неопределено Тогда
			//	КорневаяСтрокаФормы.Строки.Удалить(КорневаяСтрокаФормы.Строки.Найти(Форма.Панель, "ЭлементФормы"));
			//КонецЕсли; 
		КонецЕсли;
	Иначе
		#Если Сервер И Не Сервер Тогда
			ирОбщий.УправляемаяФормаБСП_УсловноеОформлениеЛкс();
		#КонецЕсли
		мУсловноеОформлениеФормы = ирОбщий.УправляемаяФормаБСП_ВыполнитьНаСервереЛкс(Форма, "ирОбщий.УправляемаяФормаБСП_УсловноеОформлениеЛкс");
		Если КорневаяСтрокаФормы = Неопределено Тогда
			КорневаяСтрокаФормы = Дерево.Строки.Добавить();
			КорневаяСтрокаФормы.Видимость = Истина;
			КорневаяСтрокаФормы.Доступность = Истина;
			КорневаяСтрокаФормы.Текст = "Управляемая форма";
			КорневаяСтрокаФормы.ЭлементФормы = Форма;
		КонецЕсли; 
		КорневаяСтрокаФормы.Имя = Форма.ИмяФормы;
		КорневаяСтрокаФормы.Тип = ТипЗнч(Форма);
		СтруктураЗаголовков = Новый Структура;
		НаборыКолонок = Новый Структура;
		НаборыКнопок = Новый Структура;
		// XPATH
		// http://internetka.in.ua/xpath-start-part2/ - Оси
		// http://internetka.in.ua/xpath-start-part3/ - функции
		ТаблицаИндексов = Новый ТаблицаЗначений;
		ТаблицаИндексов.Колонки.Добавить("Индекс");
		ТаблицаИндексов.Колонки.Добавить("Имя");
		ИндексЗаголовокаПоля = Неопределено;
		НастройкиФормыDOM = Неопределено;
		КлючОбъекта = КлючОбъектаДляСохраненияНастроекФормы(Форма, "НастройкиФормы");
		НастройкиФормы = ХранилищеСистемныхНастроек.Загрузить(КлючОбъекта);
		Если НастройкиФормы <> Неопределено Тогда
			СтрокаНастроекФормы = ЗначениеВСтрокуВнутр(НастройкиФормы);
			XMLСтрокаНастроекФормы = ирОбщий.СтрокаВнутрВХМЛТелоЛкс(СтрокаНастроекФормы);
			//ирОбщий.ИсЛкс(XMLСтрокаНастроекФормы)
			НастройкиФормыDOM = ирОбщий.ПрочитатьТекстВДокументDOMЛкс(XMLСтрокаНастроекФормы);
			#Если Сервер И Не Сервер Тогда
				НастройкиФормыDOM = Новый ДокументDOM;
			#КонецЕсли
			НастройкиФормыРазыменовательПИ = Новый РазыменовательПространствИменDOM(ДокументDOM);
		КонецЕсли; 
		СтрокаXPath = "//data[not(text()='0' or text()='4294967295' or contains(text(),'""'))]/following-sibling::data[1][contains(text(),'""') and not(text()='""""' or contains(text(),' ') or contains(text(),'$') or contains(text(),'/'))]";
		РезультатXPath = ДокументDOM.ВычислитьВыражениеXPath(СтрокаXPath, ДокументDOM, РазыменовательПИ, ТипРезультатаDOMXPath.НеупорядоченныйИтераторУзлов);
		Пока Истина Цикл
			УзелЭлемента = РезультатXPath.ПолучитьСледующий();
			Если УзелЭлемента = Неопределено Тогда
				Прервать;
			КонецЕсли; 
			ИмяГрафическогоЭлемента = СобратьМногострочнуюСтроку(УзелЭлемента);
			Если Истина
				И Найти(ИмяГрафическогоЭлемента, "#") > 0
				И Не ирОбщий.СтрКончаетсяНаЛкс(ИмяГрафическогоЭлемента, "#title")
			Тогда
				Продолжить;
			КонецЕсли; 
			СтрокаЭлемента = ТаблицаИндексов.Добавить();
			Если ирКэш.НомерРежимаСовместимостиЛкс() > 803000 Тогда
				ИндексЭлемента = УзелЭлемента.ПредыдущийСоседний.ТекстовоеСодержимое;
				ИмяГрафическогоЭлемента = ирОбщий.ПервыйФрагментЛкс(ИмяГрафическогоЭлемента, "#");
			Иначе
				ИндексЭлемента = УзелЭлемента.РодительскийУзел.РодительскийУзел.ДочерниеУзлы[1].ТекстовоеСодержимое;
				ИмяГрафическогоЭлемента = ирОбщий.ПервыйФрагментЛкс(ИмяГрафическогоЭлемента, "_text");
			КонецЕсли; 
			СтрокаЭлемента.Индекс = ИндексЭлемента;
			СтрокаЭлемента.Имя = ИмяГрафическогоЭлемента;
		КонецЦикла; 
		ТаблицаИндексов.Индексы.Добавить("Индекс");
		ОписанияЭлементов = Новый Соответствие;
		СтрокаXPath = "//data[contains(text(),'-') and not(starts-with(text(),'-'))]/following-sibling::data[1][not(text()='0' or text()='4294967295' or contains(text(),'""'))]";
		РезультатXPath = ДокументDOM.ВычислитьВыражениеXPath(СтрокаXPath, ДокументDOM, РазыменовательПИ, ТипРезультатаDOMXPath.НеупорядоченныйИтераторУзлов);
		Пока Истина Цикл
			БазовыйУзел = РезультатXPath.ПолучитьСледующий();
			Если БазовыйУзел = Неопределено Тогда
				Прервать;
			КонецЕсли;
			ИндексЭлемента = БазовыйУзел.ТекстовоеСодержимое;
			// following-sibling::elem[1]
			Пока БазовыйУзел <> Неопределено И БазовыйУзел.ИмяУзла <> "elem" Цикл
				БазовыйУзел = БазовыйУзел.СледующийСоседний; 
			КонецЦикла; 
			Если БазовыйУзел = Неопределено Тогда
				Продолжить;
			КонецЕсли; 
			ПодходящиеСтрокиЭлементов = ТаблицаИндексов.НайтиСтроки(Новый Структура("Индекс", ИндексЭлемента));
			Для Каждого СтрокаЭлемента Из ПодходящиеСтрокиЭлементов Цикл
				ОписаниеЭлемента = Неопределено;
				Если Истина
					И ОписанияЭлементов[СтрокаЭлемента.Имя] <> Неопределено
					И ОписанияЭлементов[СтрокаЭлемента.Имя].ЗаголовкиЗаполнены
				Тогда
					Продолжить;
				КонецЕсли; 
				ОписаниеЭлемента = Новый Структура("Индекс, ЗаголовкиЗаполнены, Заголовок, Подсказка", ИндексЭлемента, Ложь, "", "");
				ОписанияЭлементов.Вставить(СтрокаЭлемента.Имя, ОписаниеЭлемента);
				ЗаголовокЭлемента = "";
				Если БазовыйУзел.ДочерниеУзлы.Количество() > 4 Тогда
					ЗаголовокЭлемента = СобратьМногострочнуюСтроку(БазовыйУзел.ДочерниеУзлы[4]);
				КонецЕсли; 
				Если Не ЗначениеЗаполнено(ЗаголовокЭлемента) Тогда
					СтрокаXPath = "elem[2]/elem[1]/data[2][contains(text(),'""')]/..";
					РезультатXPath2 = ДокументDOM.ВычислитьВыражениеXPath(СтрокаXPath, БазовыйУзел, РазыменовательПИ, ТипРезультатаDOMXPath.НеупорядоченныйИтераторУзлов);
					УзелЗаголовка = РезультатXPath2.ПолучитьСледующий();
					Если УзелЗаголовка <> Неопределено Тогда
						ЗаголовокЭлемента = СобратьМногострочнуюСтроку(УзелЗаголовка.ДочерниеУзлы[1]);
					КонецЕсли; 
				КонецЕсли;
				ОписаниеЭлемента.Заголовок = ЗаголовокЭлемента;
				СтрокаXPath = "elem[1]/elem[10]/elem[1]/data[2][contains(text(),'""')]/.."; // 10-й узел вероятно подойдет только для ПолеФормы
				ИтераторЗаголовка = ДокументDOM.ВычислитьВыражениеXPath(СтрокаXPath, БазовыйУзел, РазыменовательПИ, ТипРезультатаDOMXPath.НеупорядоченныйИтераторУзлов);
				УзелПодсказки = ИтераторЗаголовка.ПолучитьСледующий();
				Если УзелПодсказки <> Неопределено Тогда
					ОписаниеЭлемента.Подсказка = СобратьМногострочнуюСтроку(УзелПодсказки.ДочерниеУзлы[1]);
				КонецЕсли; 
				Если Ложь
					Или ОписаниеЭлемента.Заголовок <> ""
					Или ОписаниеЭлемента.Подсказка <> ""
				Тогда
					ОписаниеЭлемента.ЗаголовкиЗаполнены = Истина;
					//Для Каждого СтрокаЭлемента Из ПодходящиеСтрокиЭлементов Цикл
					//	ТаблицаИндексов.Удалить(СтрокаЭлемента);
					//КонецЦикла;
					Прервать;
				КонецЕсли; 
			КонецЦикла;
		КонецЦикла;
		//Для Каждого СтрокаЭлемента Из ТаблицаИндексов Цикл
		//	ОписаниеЭлемента = Новый Структура("Индекс, ЗаголовкиЗаполнены, Заголовок, Подсказка", СтрокаЭлемента.Индекс, Ложь "", "");
		//	ОписанияЭлементов.Вставить(СтрокаЭлемента.Имя, ОписаниеЭлемента);
		//КонецЦикла;
		ТаблицаИндексов = Неопределено;
		Для Каждого ЭлементФормы Из Форма.Элементы Цикл
			ИмяЭлемента = ЭлементФормы.Имя;
			Если ТипЗнч(ЭлементФормы) = Тип("ТаблицаФормы") Тогда
				Колонки = Новый Структура;
				ОписаниеЭлемента = ОписанияЭлементов[ИмяЭлемента];
				Если ОписаниеЭлемента <> Неопределено Тогда
					ИндексПоля = ОписаниеЭлемента.Индекс;
					СтрокаXPath = "//data[text()='6d38aadb-3b55-46c6-9b24-abddca02bec9']/following-sibling::data[1][text()=" + ИндексПоля + "]/..//data[contains(text(),'""Колонка')]";
					ИтераторКолонок = ДокументDOM.ВычислитьВыражениеXPath(СтрокаXPath, ДокументDOM, РазыменовательПИ, ТипРезультатаDOMXPath.НеупорядоченныйИтераторУзлов);
					Пока Истина Цикл
						УзелКолонки = ИтераторКолонок.ПолучитьСледующий();
						Если УзелКолонки = Неопределено Тогда
							Прервать;
						КонецЕсли;
						ВнутреннееИмяКолонки = Вычислить(УзелКолонки.ТекстовоеСодержимое);
						ЭффективныеСвойстваКолонки = Новый Структура;
						СтрокаXPath = "../elem[1]/elem[1]";
						ИтераторЗаголовка = ДокументDOM.ВычислитьВыражениеXPath(СтрокаXPath, УзелКолонки, РазыменовательПИ, ТипРезультатаDOMXPath.НеупорядоченныйИтераторУзлов);
						УзелЗаголовка = ИтераторЗаголовка.ПолучитьСледующий();
						Если УзелЗаголовка <> Неопределено Тогда
							ЗаголовокКолонки = СобратьМногострочнуюСтроку(УзелЗаголовка.ДочерниеУзлы[1]);
						Иначе
							ЗаголовокКолонки = "";
						КонецЕсли; 
						ЭффективныеСвойстваКолонки.Вставить("Заголовок", ЗаголовокКолонки);
						СтрокаXPath = "../elem[3]/elem[1]";
						ИтераторЗаголовка = ДокументDOM.ВычислитьВыражениеXPath(СтрокаXPath, УзелКолонки, РазыменовательПИ, ТипРезультатаDOMXPath.НеупорядоченныйИтераторУзлов);
						УзелПодсказки = ИтераторЗаголовка.ПолучитьСледующий();
						Если УзелПодсказки <> Неопределено Тогда
							ПодсказкаКолонки = СобратьМногострочнуюСтроку(УзелПодсказки.ДочерниеУзлы[1]);
						Иначе
							ПодсказкаКолонки = "";
						КонецЕсли; 
						ЭффективныеСвойстваКолонки.Вставить("Подсказка", ПодсказкаКолонки);
						Колонки.Вставить(ВнутреннееИмяКолонки, ЭффективныеСвойстваКолонки);
					КонецЦикла;
				КонецЕсли; 
				Если НастройкиФормыDOM <> Неопределено Тогда
					СтрокаXPath = "//elem/data[1][text()='""" + ИмяЭлемента + """']/../elem/elem[2]";
					РезультатXPath = НастройкиФормыDOM.ВычислитьВыражениеXPath(СтрокаXPath, НастройкиФормыDOM, НастройкиФормыРазыменовательПИ, ТипРезультатаDOMXPath.НеупорядоченныйИтераторУзлов);
					УзелНастроек = РезультатXPath.ПолучитьСледующий();
					Если УзелНастроек <> Неопределено Тогда
						ИзмененияСоставаКолонок = УзелНастроек.ПервыйДочерний;
						Для Счетчик = 2 По ИзмененияСоставаКолонок.ДочерниеУзлы.Количество() Цикл
							СвойстваКолонки = Новый Структура;
							ИмяКолонки = Вычислить(ИзмененияСоставаКолонок.ДочерниеУзлы[Счетчик - 1].ТекстовоеСодержимое);
							ПозицияКолонки = Вычислить(ИзмененияСоставаКолонок.ДочерниеУзлы[Счетчик].ТекстовоеСодержимое);
							Счетчик = Счетчик + 1;
							Если Форма.Элементы.Найти(ИмяКолонки) = Неопределено Тогда
								СтрокаXPath1 = "//elem/data[1][text()='""" + ИмяКолонки + """']/../elem/data[12]";
								РезультатXPath1 = НастройкиФормыDOM.ВычислитьВыражениеXPath(СтрокаXPath1, НастройкиФормыDOM, НастройкиФормыРазыменовательПИ, ТипРезультатаDOMXPath.НеупорядоченныйИтераторУзлов);
								УзелДобавленнойКолонки = РезультатXPath1.ПолучитьСледующий();
								Если УзелДобавленнойКолонки <> Неопределено Тогда
									ПутьКДаннымКолонки = Вычислить(УзелДобавленнойКолонки.ТекстовоеСодержимое);
								Иначе
									ПутьКДаннымКолонки = "";
								КонецЕсли; 
								СвойстваКолонки.Вставить("ПутьКДанным", ПутьКДаннымКолонки);
							КонецЕсли; 
							СвойстваКолонки.Вставить("Имя", ИмяКолонки);
							Колонки.Вставить("ИзменениеПозиции" + ПозицияКолонки, СвойстваКолонки);
						КонецЦикла;
					КонецЕсли; 
				КонецЕсли; 
				НаборыКолонок.Вставить(ИмяЭлемента, Колонки);
			ИначеЕсли Истина
				И ТипЗнч(ЭлементФормы) = Тип("ГруппаФормы")
				И (Ложь
					//Или ЭлементФормы.Вид = ВидГруппыФормы.КонтекстноеМеню // Контекстные меню здесь точно отсутствуют
					Или ЭлементФормы.Вид = ВидГруппыФормы.КоманднаяПанель) 
			Тогда
				ПолныеКнопки = Новый Массив;
				ОписаниеЭлемента = ОписанияЭлементов[ИмяЭлемента];
				Если ОписаниеЭлемента <> Неопределено Тогда
					ИндексПоля = ОписаниеЭлемента.Индекс;
					ГруппыКнопок = Новый ТаблицаЗначений;
					ГруппыКнопок.Колонки.Добавить("Индекс");
					ГруппыКнопок.Колонки.Добавить("Кнопки");
					СоответствиеКнопокКорня = Неопределено;
					ИндексПодменюЕще = 0;
					ИндексКорняПанели = 4; // отображаемый набор кнопок непосредственно в командной панели
					СтрокаXPath = "//data[text()='18a60f47-8dfd-4f2e-a693-026b98f58b2e']/following-sibling::data[1][text()=" + ИндексПоля + "]/following-sibling::elem[2]/elem[1]/elem";
					ИтераторНабора = ДокументDOM.ВычислитьВыражениеXPath(СтрокаXPath, ДокументDOM, РазыменовательПИ, ТипРезультатаDOMXPath.НеупорядоченныйИтераторУзлов);
					Пока Истина Цикл
						УзелНабора = ИтераторНабора.ПолучитьСледующий();
						Если УзелНабора = Неопределено Тогда
							Прервать;
						КонецЕсли;
						Кнопки = Новый Массив;
						СоответствиеКнопокНабора = Новый Соответствие;
						СтрокаXPath = "elem[*]";
						ИтераторКнопок = ДокументDOM.ВычислитьВыражениеXPath(СтрокаXPath, УзелНабора, РазыменовательПИ, ТипРезультатаDOMXPath.НеупорядоченныйИтераторУзлов);
						Пока Истина Цикл
							УзелКнопки = ИтераторКнопок.ПолучитьСледующий();
							Если УзелКнопки = Неопределено Тогда
								Прервать;
							КонецЕсли;
							УзелЗаголовка = УзелКнопки.ДочерниеУзлы[7];
							ЗаголовокКнопки = СобратьМногострочнуюСтроку(УзелЗаголовка, УзелЗаголовка);
							ПодсказкиКнопки = СобратьМногострочнуюСтроку(УзелЗаголовка, УзелЗаголовка);
							ИДКартинки = ДокументDOM.ВычислитьВыражениеXPath("elem[1]/elem[1]/elem[1]/data[2]/text()", УзелКнопки, РазыменовательПИ, ТипРезультатаDOMXPath.Строка).СтроковоеЗначение;
							Картинка = КартинкаИзБиблиотекиПоИдентификатору(ИДКартинки);
							ИндексПодменю = ДокументDOM.ВычислитьВыражениеXPath("elem[7]/elem[1]/data[3]/text()", УзелКнопки, РазыменовательПИ, ТипРезультатаDOMXPath.Строка).СтроковоеЗначение;
							Кнопки.Добавить(Новый Структура("Заголовок, Подсказка, ИндексПодменю, КоличествоПодчиненных, Видимость, Картинка", ЗаголовокКнопки, ПодсказкиКнопки, ИндексПодменю, 0, Истина, Картинка));
							Если ЗначениеЗаполнено(ИндексПодменю) И ЗаголовокКнопки = "Еще" Тогда
								ИндексПодменюЕще = Число(ИндексПодменю);
								СоответствиеКнопокКорня = СоответствиеКнопокНабора;
							КонецЕсли; 
							СоответствиеКнопокНабора.Вставить(ЗаголовокКнопки, 1);
						КонецЦикла;
						СтрокаНабора = ГруппыКнопок.Добавить();
						СтрокаНабора.Кнопки = Кнопки;
						СтрокаНабора.Индекс = Число(ДокументDOM.ВычислитьВыражениеXPath("preceding-sibling::data[1]/text()", УзелНабора, РазыменовательПИ, ТипРезультатаDOMXPath.Строка).СтроковоеЗначение);
					КонецЦикла;
					ТочкаОстанова = 0;
					ГруппаКнопок = ГруппыКнопок.Найти(ИндексПодменюЕще, "Индекс");
					Если ГруппаКнопок = Неопределено Тогда
						ГруппаКнопок = ГруппыКнопок.Найти(ИндексКорняПанели, "Индекс");
					КонецЕсли; 
					Для Каждого ОписаниеКнопки Из ГруппаКнопок.Кнопки Цикл
						Если ЗначениеЗаполнено(ОписаниеКнопки.ИндексПодменю) Тогда
							Если ОписаниеКнопки.Заголовок = "Управление поиском" Тогда
								ВиртуальнаяГруппа = Новый Структура;
								ВиртуальнаяГруппа.Вставить("Имя", "");
								ВиртуальнаяГруппа.Вставить("Картинка");
								ВиртуальнаяГруппа.Вставить("Заголовок", ОписаниеКнопки.Заголовок);
								ВиртуальнаяГруппа.Вставить("Видимость", Истина);
								ВиртуальнаяГруппа.Вставить("Вид", ВидГруппыФормы.Подменю);
								ВиртуальнаяГруппа.Вставить("ПодчиненныеЭлементы", Новый Массив);
								ДочерниеКнопки = ГруппыКнопок.Найти(Число(ОписаниеКнопки.ИндексПодменю), "Индекс").Кнопки;
								ДобавитьОписаниеКнопкиВПолныйСписокКнопок(ОписаниеКнопки, ПолныеКнопки, СоответствиеКнопокКорня, ВиртуальнаяГруппа);
								Для Каждого ДочерняяКнопка Из ДочерниеКнопки Цикл
									ДочерняяКнопка.Вставить("Имя", "");
									ДочерняяКнопка.Вставить("ПодчиненныеЭлементы", Новый Массив);
									ВиртуальнаяГруппа.ПодчиненныеЭлементы.Добавить(ДочерняяКнопка);
									ДобавитьОписаниеКнопкиВПолныйСписокКнопок(ДочерняяКнопка, ПолныеКнопки, СоответствиеКнопокКорня);
								КонецЦикла; 
							Иначе
								ДобавитьОписаниеКнопкиВПолныйСписокКнопок(ОписаниеКнопки, ПолныеКнопки, СоответствиеКнопокКорня);
								ДочерниеКнопки = ГруппыКнопок.Найти(Число(ОписаниеКнопки.ИндексПодменю), "Индекс").Кнопки;
								ОписаниеКнопки.КоличествоПодчиненных = ДочерниеКнопки.Количество();
								Для Каждого ДочерняяКнопка Из ДочерниеКнопки Цикл
									ДобавитьОписаниеКнопкиВПолныйСписокКнопок(ДочерняяКнопка, ПолныеКнопки, СоответствиеКнопокКорня,, СоответствиеКнопокКорня[ОписаниеКнопки.Заголовок] = Неопределено);
								КонецЦикла; 
							КонецЕсли; 
						Иначе
							ДобавитьОписаниеКнопкиВПолныйСписокКнопок(ОписаниеКнопки, ПолныеКнопки, СоответствиеКнопокКорня);
						КонецЕсли; 
					КонецЦикла; 
				КонецЕсли;
				НаборыКнопок.Вставить(ИмяЭлемента, ПолныеКнопки);
			КонецЕсли;
		КонецЦикла;
		СтарыйТекущийЭлемент = Форма.ТекущийЭлемент;
		ДобавитьЗависимуюКоманднуюПанель(КорневаяСтрокаФормы, Форма, НаборыКнопок);
		ДобавитьГруппуФормы(КорневаяСтрокаФормы, ДокументDOM, НаборыКолонок, НаборыКнопок, РазыменовательПИ, ОписанияЭлементов);
		ЗаполнитьСтрокуЭлементаУправляемойФормы(КорневаяСтрокаФормы);
		Форма.ТекущийЭлемент = СтарыйТекущийЭлемент;
		//Если мУсловноеОформлениеФормы <> Неопределено Тогда
		//	ИмитаторДоступныхПолей = Новый ТаблицаЗначений;
		//	ИмитаторДоступныхПолей.Колонки.Добавить();
		//	СхемаКомпоновки = ирОбщий.СоздатьСхемуПоТаблицеЗначенийЛкс(ИмитаторДоступныхПолей);
		//КонецЕсли; 
	КонецЕсли;
	ЭлементыФормы.ПанельЭлемента.Страницы.УсловноеОформление.Доступность = мУсловноеОформлениеФормы <> Неопределено;
	ЭлементыФормы.Дерево.Колонки.ЕстьУсловноеОформление.Видимость = мУсловноеОформлениеФормы <> Неопределено;
	ЭлементыФормы.Дерево.Колонки.ЕстьУсловноеОформление.ИзменятьВидимость = мУсловноеОформлениеФормы <> Неопределено;

КонецПроцедуры

Функция КлючОбъектаДляСохраненияНастроекФормы(Знач Форма, Знач ТипНастройки)
	
	КлючОбъекта = Форма.ИмяФормы;
	Если Форма.КлючНазначенияИспользования <> "" Тогда
		КлючОбъекта = КлючОбъекта + "/" + Форма.КлючНазначенияИспользования;
	КонецЕсли; 
	КлючОбъекта = КлючОбъекта + "/" + ТипНастройки;
	Возврат КлючОбъекта;

КонецФункции

Функция КартинкаИзБиблиотекиПоИдентификатору(ИДКартинки)
	
	Перем Картинка, Пустышка;
	
	Картинка = Неопределено;
	Если ЗначениеЗаполнено(ИДКартинки) И Найти(ИДКартинки, "-") > 1 Тогда
		Попытка
			Картинка = ЗначениеИзСтрокиВнутр("{""#"",e6f51714-91cb-4dce-94fe-90ae3e3e1ad1,{4,1,{0," + ИДКартинки + "},"""",-1,-1,0,0,""""}}");
		Исключение
			Пустышка = 0;
		КонецПопытки; 
	КонецЕсли;
	Возврат Картинка;

КонецФункции

Функция СобратьМногострочнуюСтроку(Знач ТекущийУзел, выхСледующийУзел = Неопределено)
	
	Результат = "";
	ТекстУзла = ТекущийУзел.ТекстовоеСодержимое;
	Если Лев(ТекстУзла, 1) = """" Тогда 
		Пока Истина Цикл
			Результат = Результат + ТекущийУзел.ТекстовоеСодержимое;
			ТекущийУзел = ТекущийУзел.СледующийСоседний;
			Если Ложь
				Или Прав(Результат, 1) = """" И СтрЧислоВхождений(Результат, """") % 2 = 0
				Или ТекущийУзел = Неопределено
			Тогда
				Прервать;
			КонецЕсли; 
		КонецЦикла; 
		Результат = Вычислить(Результат);
	КонецЕсли; 
	выхСледующийУзел = ТекущийУзел;
	Возврат Результат;

КонецФункции

Процедура ДобавитьОписаниеКнопкиВПолныйСписокКнопок(Знач ОписаниеКнопки, Знач ПолныеКнопки, СоответствиеКнопокКорня, ВиртуальнаяГруппа = Неопределено, Знач ТолькоВЕще = Неопределено)
	
	#Если Сервер И Не Сервер Тогда
		ОписаниеКнопки = Новый Структура;
	#КонецЕсли
	Если Ложь
		Или ЗначениеЗаполнено(ОписаниеКнопки.Заголовок)
		Или ЗначениеЗаполнено(ОписаниеКнопки.Подсказка)
	Тогда
		Если ТолькоВЕще = Неопределено Тогда
			Если СоответствиеКнопокКорня <> Неопределено Тогда
				ТолькоВЕще = СоответствиеКнопокКорня[ОписаниеКнопки.Заголовок] = Неопределено;
			Иначе
				ТолькоВЕще = Ложь;
			КонецЕсли; 
		КонецЕсли; 
		ОписаниеКнопки.Вставить("ВиртуальнаяГруппа", ВиртуальнаяГруппа);
		ОписаниеКнопки.Вставить("ТолькоВЕще", ТолькоВЕще);
		ПолныеКнопки.Добавить(ОписаниеКнопки);
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьЗависимуюКоманднуюПанель(Знач СтрокаДЗ, Знач ЭлементФормы, Знач НаборыКнопок, ИмяСвойства = "КоманднаяПанель")
	
	ПодчиненныйЭлемент = ЭлементФормы[ИмяСвойства];
	Если ПодчиненныйЭлемент.ПодчиненныеЭлементы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	СтрокаДочерняяСтрока = СтрокаДЗ.Строки.Добавить();
	СтрокаДочерняяСтрока.ЭлементФормы = ПодчиненныйЭлемент;
	СтрокаДочерняяСтрока.Текст = ирОбщий.ПредставлениеИзИдентификатораЛкс(ИмяСвойства);
	Если НаборыКнопок.Свойство(ПодчиненныйЭлемент.Имя) Тогда
		Кнопки = НаборыКнопок[ПодчиненныйЭлемент.Имя];
	Иначе
		Кнопки = Новый Массив;
	КонецЕсли; 
	СтрокаДочерняяСтрока.Имя = ПодчиненныйЭлемент.Имя;
	СтрокаДочерняяСтрока.ТолькоВЕще = ИмяСвойства <> "КоманднаяПанель";
	ЗаполнитьСтрокуЭлементаУправляемойФормы(СтрокаДочерняяСтрока);
	ДобавитьГруппуКнопокФормы(СтрокаДочерняяСтрока, ПодчиненныйЭлемент, Кнопки);

КонецПроцедуры

Процедура ДобавитьГруппуФормы(Знач СтрокаДЗ, Знач ДокументDOM, Знач НаборыКолонок, Знач НаборыКнопок, Знач РазыменовательПИ, Знач ЗаголовокиЭлементов)
	
	Для Каждого ПодчиненныйЭлемент Из СтрокаДЗ.ЭлементФормы.ПодчиненныеЭлементы Цикл
		СтрокаДочерняяСтрока = СтрокаДЗ.Строки.Добавить();
		СтрокаДочерняяСтрока.ЭлементФормы = ПодчиненныйЭлемент;
		ЭффективныеСвойства = ЗаголовокиЭлементов[ПодчиненныйЭлемент.Имя];
		Если ЭффективныеСвойства <> Неопределено Тогда
			СтрокаДочерняяСтрока.Текст = ЭффективныеСвойства.Заголовок;
			СтрокаДочерняяСтрока.Подсказка = ЭффективныеСвойства.Подсказка;
		КонецЕсли; 
		ЗаполнитьСтрокуЭлементаУправляемойФормы(СтрокаДочерняяСтрока);
		Если Ложь
			Или ТипЗнч(ПодчиненныйЭлемент) = Тип("ТаблицаФормы") 
			Или ТипЗнч(ПодчиненныйЭлемент) = Тип("ПолеФормы")
			Или ТипЗнч(ПодчиненныйЭлемент) = Тип("ДекорацияФормы")
		Тогда
			ДобавитьЗависимуюКоманднуюПанель(СтрокаДочерняяСтрока, ПодчиненныйЭлемент, НаборыКнопок, "КонтекстноеМеню");
		КонецЕсли; 
		Если ТипЗнч(ПодчиненныйЭлемент) = Тип("ТаблицаФормы") Тогда
			СтрокаДочерняяСтрока.Имя = ПодчиненныйЭлемент.Имя;
			Если ПодчиненныйЭлемент.ПоложениеКоманднойПанели <> ПоложениеКоманднойПанелиЭлементаФормы.Нет Тогда 
				ДобавитьЗависимуюКоманднуюПанель(СтрокаДочерняяСтрока, ПодчиненныйЭлемент, НаборыКнопок);
			КонецЕсли; 
			СтарыйТекущийЭлемент = ПодчиненныйЭлемент.ТекущийЭлемент;
			ДобавитьГруппуКолонокФормы(СтрокаДочерняяСтрока, ПодчиненныйЭлемент, НаборыКолонок[ПодчиненныйЭлемент.Имя]);
			ПодчиненныйЭлемент.ТекущийЭлемент = СтарыйТекущийЭлемент;
		ИначеЕсли ТипЗнч(ПодчиненныйЭлемент) = Тип("ГруппаФормы") Тогда
			Если Ложь
				Или ПодчиненныйЭлемент.Вид = ВидГруппыФормы.КоманднаяПанель 
				Или ПодчиненныйЭлемент.Вид = ВидГруппыФормы.КонтекстноеМеню
			Тогда
				ДобавитьГруппуКнопокФормы(СтрокаДочерняяСтрока, ПодчиненныйЭлемент, НаборыКнопок[ПодчиненныйЭлемент.Имя]);
			Иначе
				ДобавитьГруппуФормы(СтрокаДочерняяСтрока, ДокументDOM, НаборыКолонок, НаборыКнопок, РазыменовательПИ, ЗаголовокиЭлементов);
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьГруппуКолонокФормы(СтрокаДЗ, ГруппаТаблицыФормы, СвойстваКолонок, КоличествоВидимыхДобавлено = 0, Группировка = Неопределено)
	
	#Если Сервер И Не Сервер Тогда
		СвойстваКолонок = Новый Структура;
	#КонецЕсли
	ТаблицаФормы = ирОбщий.РодительЭлементаУправляемойФормыЛкс(ГруппаТаблицыФормы, Тип("ТаблицаФормы"));
	ПодчиненныеЭлементы = Новый ТаблицаЗначений;
	ПодчиненныеЭлементы.Колонки.Добавить("Приоритет");
	ПодчиненныеЭлементы.Колонки.Добавить("Элемент");
	Для Каждого ПодчиненныйЭлемент Из ГруппаТаблицыФормы.ПодчиненныеЭлементы Цикл
		СтрокаЭлемента = ПодчиненныеЭлементы.Добавить();
		ЗаполнитьПриоритетКолонкиТаблицыФормы(ПодчиненныйЭлемент, СтрокаЭлемента);
	КонецЦикла;
	Для Позиция = 0 По ПодчиненныеЭлементы.Количество() - 1 Цикл
		ИмяПозиции = "ИзменениеПозиции" + Позиция;
		Если СвойстваКолонок.Свойство(ИмяПозиции) Тогда 
			НастройкиКолонки = СвойстваКолонок[ИмяПозиции];
			ИмяКолонки = НастройкиКолонки.Имя;
			Колонка = Форма.Элементы.Найти(ИмяКолонки);
			Если Колонка <> Неопределено Тогда
				СтрокаЭлемента = ПодчиненныеЭлементы.Найти(Колонка, "Элемент");
				ПодчиненныеЭлементы.Сдвинуть(СтрокаЭлемента, Позиция - ПодчиненныеЭлементы.Индекс(СтрокаЭлемента));
			Иначе
				НастройкиКолонки.Вставить("КартинкаШапки");
				НастройкиКолонки.Вставить("Видимость", Истина);
				НастройкиКолонки.Вставить("Доступность", Истина);
				НастройкиКолонки.Вставить("ОтображатьВШапке", Истина);
				НастройкиКолонки.Вставить("ФиксацияВТаблице", );
				НастройкиКолонки.Вставить("РодительскоеПоле", ирОбщий.СтрРазделитьЛкс(НастройкиКолонки.ПутьКДанным)[1]);
				СтрокаЭлемента = ПодчиненныеЭлементы.Вставить(Позиция);
				ЗаполнитьПриоритетКолонкиТаблицыФормы(НастройкиКолонки, СтрокаЭлемента);
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
	ПодчиненныеЭлементы.Сортировать("Приоритет");
	ПодчиненныеЭлементы = ПодчиненныеЭлементы.ВыгрузитьКолонку("Элемент");
	Для Каждого ПодчиненныйЭлемент Из ПодчиненныеЭлементы Цикл
		СтрокаДочерняяСтрока = СтрокаДЗ.Строки.Добавить();
		СтрокаДочерняяСтрока.ЭлементФормы = ПодчиненныйЭлемент;
		СтрокаДочерняяСтрока.Картинка = ПодчиненныйЭлемент.КартинкаШапки;
		Если ТипЗнч(ПодчиненныйЭлемент) = Тип("Структура") Тогда
			СтрокаДочерняяСтрока.Тип = Тип("ПолеФормы");
		КонецЕсли; 
		ЭффективнаяВидимость = ПодчиненныйЭлемент.Видимость;
		Если ПодчиненныйЭлемент.Доступность И ПодчиненныйЭлемент.ОтображатьВШапке И ТипЗнч(ПодчиненныйЭлемент) <> Тип("Структура") Тогда
			СтарыйТекущийЭлемент = ТаблицаФормы.ТекущийЭлемент;
			Если СтарыйТекущийЭлемент <> Неопределено И СтарыйТекущийЭлемент.Родитель = ПодчиненныйЭлемент Тогда
				// http://www.hostedredmine.com/issues/881782
			Иначе
				ТаблицаФормы.ТекущийЭлемент = ПодчиненныйЭлемент;
				Если ТаблицаФормы.ТекущийЭлемент <> ПодчиненныйЭлемент Тогда
					// Антибаг платформы 8.3.15 После присвоения Неопределено свойству ТекущийЭлемент таблицы формы можно присвоить колонку с отключенной пользовательской видимостью https://partners.v8.1c.ru/forum/topic/1912733
					ТаблицаФормы.ТекущийЭлемент = СтарыйТекущийЭлемент; 
					ЭффективнаяВидимость = Ложь;
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 
		ЗаполнитьСтрокуЭлементаУправляемойФормы(СтрокаДочерняяСтрока,, ЭффективнаяВидимость);
		Если ЭффективнаяВидимость Тогда
			Если ПодчиненныйЭлемент.ОтображатьВШапке Тогда
				КоличествоВидимыхДобавлено = КоличествоВидимыхДобавлено + 1;
				ЗаполнитьСтрокуДереваДляКолонкиТаблицы(КоличествоВидимыхДобавлено, СвойстваКолонок, СтрокаДочерняяСтрока);
			КонецЕсли; 
			Если ТипЗнч(ПодчиненныйЭлемент) = Тип("ГруппаФормы") Тогда
				ДобавитьГруппуКолонокФормы(СтрокаДочерняяСтрока, ПодчиненныйЭлемент, СвойстваКолонок, КоличествоВидимыхДобавлено, ПодчиненныйЭлемент.Группировка);
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПриоритетКолонкиТаблицыФормы(Знач ПодчиненныйЭлемент, Знач СтрокаЭлемента)
	
	Если ПодчиненныйЭлемент.ФиксацияВТаблице = ФиксацияВТаблице.Лево Тогда
		СтрокаЭлемента.Приоритет = 1;
	ИначеЕсли ПодчиненныйЭлемент.ФиксацияВТаблице = ФиксацияВТаблице.Право Тогда
		СтрокаЭлемента.Приоритет = 3;
	Иначе
		СтрокаЭлемента.Приоритет = 2;
	КонецЕсли;
	СтрокаЭлемента.Элемент = ПодчиненныйЭлемент;

КонецПроцедуры

Процедура ЗаполнитьСтрокуДереваДляКолонкиТаблицы(КоличествоВидимыхДобавлено, Знач СвойстваКолонок, Знач СтрокаДочерняяСтрока)
	
	ВнутреннееИмяКолонки = "Колонка" + XMLСтрока(КоличествоВидимыхДобавлено);
	Если СвойстваКолонок.Свойство(ВнутреннееИмяКолонки) Тогда
		ЭффективныеСвойстваКолонки = СвойстваКолонок[ВнутреннееИмяКолонки];
		СтрокаДочерняяСтрока.Текст = ЭффективныеСвойстваКолонки.Заголовок;
		СтрокаДочерняяСтрока.Подсказка = ЭффективныеСвойстваКолонки.Подсказка;
		Если ТипЗнч(СтрокаДочерняяСтрока.ЭлементФормы) = Тип("Структура") Тогда
			СтрокаДочерняяСтрока.Текст = СтрокаДочерняяСтрока.Текст + " (" + СтрокаДочерняяСтрока.ЭлементФормы.РодительскоеПоле + ")";
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьГруппуКнопокФормы(СтрокаДЗ, ГруппаТаблицыФормы, СвойстваКнопок, НомерКнопки = 0, КоличествоВидимыхВсего = 0, КоличествоВидимыхДобавлено = 0)
	
	#Если Сервер И Не Сервер Тогда
		СвойстваКнопок = Новый Массив;
	#КонецЕсли
	ПеренестиКнопкиВКонец = Новый Соответствие;
	Для Каждого ПодчиненныйЭлемент Из ГруппаТаблицыФормы.ПодчиненныеЭлементы Цикл
		Если Не ПодчиненныйЭлемент.Видимость Тогда
			Продолжить;
		КонецЕсли; 
		Если ТипЗнч(ПодчиненныйЭлемент) = Тип("ГруппаФормы") Тогда
			Если ПодчиненныйЭлемент.ПодчиненныеЭлементы.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли; 
			КоличествоВидимыхПодчиненных = 0;
			Если ПодчиненныйЭлемент.Вид = ВидГруппыФормы.Подменю И СвойстваКнопок.Количество() > НомерКнопки Тогда
				НомерКнопки = НомерКнопки + 1;
				ЭффективныеСвойстваКнопки = СвойстваКнопок[НомерКнопки - 1];
				ГруппаКомандКонфигурации = НайтиГруппуКомандКонфигурации(ПодчиненныйЭлемент.Имя);
				ПервоеСловоЗаголовка = ирОбщий.ПервыйФрагментЛкс(ЭффективныеСвойстваКнопки.Заголовок, " ");
				ДлинаПервогоСлова = СтрДлина(ПервоеСловоЗаголовка);
				Если ДлинаПервогоСлова > 12 Тогда
					ПервоеСловоЗаголовка = Лев(ПервоеСловоЗаголовка, ДлинаПервогоСлова - 5);
				ИначеЕсли ДлинаПервогоСлова > 10 Тогда
					ПервоеСловоЗаголовка = Лев(ПервоеСловоЗаголовка, ДлинаПервогоСлова - 4);
				ИначеЕсли ДлинаПервогоСлова > 7 Тогда
					ПервоеСловоЗаголовка = Лев(ПервоеСловоЗаголовка, ДлинаПервогоСлова - 3);
				Иначе
					ПервоеСловоЗаголовка = Лев(ПервоеСловоЗаголовка, ДлинаПервогоСлова - 2);
				КонецЕсли; 
				Если Ложь
					Или Не ЗначениеЗаполнено(ЭффективныеСвойстваКнопки.ИндексПодменю) 
					Или (Истина
						И Найти(Нрег(ПодчиненныйЭлемент.Имя), НРег(ПервоеСловоЗаголовка)) = 0
						И (Ложь
							Или ГруппаКомандКонфигурации = Неопределено
							Или ГруппаКомандКонфигурации.Представление() <> ЭффективныеСвойстваКнопки.Заголовок))
				Тогда
					НомерКнопки = НомерКнопки - 1;
					Продолжить;
				КонецЕсли;
				КоличествоВидимыхПодчиненных = ЭффективныеСвойстваКнопки.КоличествоПодчиненных;
			КонецЕсли; 
			СтрокаДочерняяСтрока = СтрокаДЗ.Строки.Добавить();
			СтрокаДочерняяСтрока.ЭлементФормы = ПодчиненныйЭлемент;
			ЗаполнитьСтрокуЭлементаУправляемойФормы(СтрокаДочерняяСтрока);
			Если ПодчиненныйЭлемент.Вид = ВидГруппыФормы.Подменю Тогда
				ДобавитьГруппуКнопокФормы(СтрокаДочерняяСтрока, ПодчиненныйЭлемент, СвойстваКнопок, НомерКнопки, КоличествоВидимыхПодчиненных);
			Иначе
				ДобавитьГруппуКнопокФормы(СтрокаДочерняяСтрока, ПодчиненныйЭлемент, СвойстваКнопок, НомерКнопки, КоличествоВидимыхВсего, КоличествоВидимыхДобавлено);
			КонецЕсли; 
		Иначе
			Если ирОбщий.СтрКончаетсяНаЛкс(ПодчиненныйЭлемент.Имя, "ИзменитьФорму", Истина) > 0 Тогда
				ПеренестиКнопкиВКонец.Вставить("Изменить форму...", ПодчиненныйЭлемент);
			ИначеЕсли ирОбщий.СтрКончаетсяНаЛкс(ПодчиненныйЭлемент.Имя, "Справка", Истина) > 0 Тогда
				ПеренестиКнопкиВКонец.Вставить("Справка", ПодчиненныйЭлемент);
			Иначе
				Если СвойстваКнопок.Количество() > НомерКнопки И ЗначениеЗаполнено(СвойстваКнопок[НомерКнопки].ИндексПодменю) Тогда
					Продолжить;
				КонецЕсли; 
				ДобавитьКнопкуВДерево(СтрокаДЗ, ПодчиненныйЭлемент, СвойстваКнопок, НомерКнопки);
			КонецЕсли;
			КоличествоВидимыхДобавлено = КоличествоВидимыхДобавлено + 1;
		КонецЕсли; 
		Если КоличествоВидимыхВсего > 0 И КоличествоВидимыхВсего = КоличествоВидимыхДобавлено Тогда
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	Для Каждого КлючИЗначение Из ПеренестиКнопкиВКонец Цикл
		ДобавитьКнопкуВДерево(СтрокаДЗ, КлючИЗначение.Значение, СвойстваКнопок, НомерКнопки, КлючИЗначение.Ключ);
	КонецЦикла;
	
КонецПроцедуры

Функция НайтиГруппуКомандКонфигурации(ИмяКнопки)
	
	Для Каждого ГруппаКоманд Из Метаданные.ГруппыКоманд Цикл
		Если Найти(ИмяКнопки, ГруппаКоманд.Имя) > 0 Тогда
			Возврат ГруппаКоманд;
		КонецЕсли; 
	КонецЦикла;
	Возврат Неопределено;
	
КонецФункции

Процедура ДобавитьКнопкуВДерево(СтрокаДЗ, ПодчиненныйЭлемент, СвойстваКнопок, НомерКнопки, Заголовок = "", Подсказка = "")
	
	НомерКнопки = НомерКнопки + 1;
	Пока СвойстваКнопок.Количество() >= НомерКнопки Цикл 
		ЭффективныеСвойстваКнопки = СвойстваКнопок[НомерКнопки - 1];
		Если ЭффективныеСвойстваКнопки.ВиртуальнаяГруппа = Неопределено Тогда
			Прервать;
		КонецЕсли;
		СтрокаДочерняяСтрока = СтрокаДЗ.Строки.Добавить();
		СтрокаДочерняяСтрока.ЭлементФормы = ЭффективныеСвойстваКнопки.ВиртуальнаяГруппа;
		СтрокаДочерняяСтрока.ТолькоВЕще = ЭффективныеСвойстваКнопки.ТолькоВЕще;
		СтрокаДочерняяСтрока.Картинка = ЭффективныеСвойстваКнопки.Картинка;
		СтрокаДочерняяСтрока.Тип = Тип("ГруппаФормы");
		ЗаполнитьСтрокуЭлементаУправляемойФормы(СтрокаДочерняяСтрока);
		ДобавитьГруппуКнопокФормы(СтрокаДочерняяСтрока, ЭффективныеСвойстваКнопки.ВиртуальнаяГруппа, СвойстваКнопок, НомерКнопки);
		НомерКнопки = НомерКнопки + 1;
	КонецЦикла; 
	Если СвойстваКнопок.Количество() >= НомерКнопки Тогда
		СтрокаДочерняяСтрока = СтрокаДЗ.Строки.Добавить();
		СтрокаДочерняяСтрока.ЭлементФормы = ПодчиненныйЭлемент;
		СтрокаДочерняяСтрока.Текст = ЭффективныеСвойстваКнопки.Заголовок;
		СтрокаДочерняяСтрока.Подсказка = ЭффективныеСвойстваКнопки.Подсказка;
		СтрокаДочерняяСтрока.ТолькоВЕще = ЭффективныеСвойстваКнопки.ТолькоВЕще;
		СтрокаДочерняяСтрока.Картинка = ЭффективныеСвойстваКнопки.Картинка;
	Иначе
		СтрокаДочерняяСтрока = СтрокаДЗ.Строки.Добавить();
		СтрокаДочерняяСтрока.ЭлементФормы = ПодчиненныйЭлемент;
		СтрокаДочерняяСтрока.Текст = Заголовок;
		СтрокаДочерняяСтрока.Подсказка = Подсказка;
	КонецЕсли;
	СтрокаДочерняяСтрока.Тип = Тип("КнопкаФормы");
	ЗаполнитьСтрокуЭлементаУправляемойФормы(СтрокаДочерняяСтрока);

КонецПроцедуры

Процедура ЗаполнитьСтрокуЭлементаУправляемойФормы(СтрокаДерева, Роль = "", ЭффективнаяВидимость = Неопределено)
	
	ЭлементФормы = СтрокаДерева.ЭлементФормы;
	Если ЗначениеЗаполнено(СтрокаДерева.Текст) Тогда
		ЗаголовокЭлемента = СтрокаДерева.Текст;
	КонецЕсли; 
	Если Не ЗначениеЗаполнено(СтрокаДерева.Тип) Тогда
		СтрокаДерева.Тип = ТипЗнч(ЭлементФормы);
	КонецЕсли; 
	СтрокаДерева.ПредставлениеТипа = "" + СтрокаДерева.Тип;
	ИсключаемыеСвойства = Новый Массив;
	Если ЗначениеЗаполнено(СтрокаДерева.Подсказка) И ТипЗнч(ЭлементФормы) <> Тип("КнопкаФормы") Тогда
		ИсключаемыеСвойства.Добавить("Подсказка");
	КонецЕсли; 
	Если ЗначениеЗаполнено(СтрокаДерева.Картинка) И ТипЗнч(ЭлементФормы) <> Тип("ПолеФормы")  Тогда
		ИсключаемыеСвойства.Добавить("Картинка");
	КонецЕсли; 
	ЗаполнитьЗначенияСвойств(СтрокаДерева, ЭлементФормы,, ирОбщий.СтрСоединитьЛкс(ИсключаемыеСвойства));
	РезультирующиеПризнаки = РезультирующийПризнакСтрокиДерева(СтрокаДерева);
	ЗаполнитьЗначенияСвойств(СтрокаДерева, РезультирующиеПризнаки); 
	Если ЭффективнаяВидимость <> Неопределено Тогда
		СтрокаДерева.Видимость = СтрокаДерева.Видимость И ЭффективнаяВидимость;
	КонецЕсли; 
	//Свертка = ирОбщий.БезопасноПолучитьЗначениеСвойстваЛкс(ЭлементФормы, "Свернута");
	//Если СтрокаДерева.Видимость И Свертка <> Неопределено И Свертка <> РежимСверткиЭлементаУправления.Нет Тогда
	//	СтрокаДерева.Видимость = Ложь;
	//КонецЕсли; 
	Если Не ЗначениеЗаполнено(СтрокаДерева.Имя) Тогда
		СтрокаДерева.Имя = "" + ТипЗнч(ЭлементФормы);
	КонецЕсли; 
	Если Истина
		И ТипЗнч(ЭлементФормы) = Тип("ГруппаФормы") 
		И ЭлементФормы.Вид = ВидГруппыФормы.Страницы 
		И ЭлементФормы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет 
	Тогда
		СтрокаДерева.СтараяТекущаяСтраница = ЭлементФормы.ТекущаяСтраница;
	КонецЕсли; 
	ИмяСвойстваЗаголовка = "Заголовок";
	Если Не ЗначениеЗаполнено(ЗаголовокЭлемента) Тогда
		ЗаголовокЭлемента = ирОбщий.БезопасноПолучитьЗначениеСвойстваЛкс(ЭлементФормы, ИмяСвойстваЗаголовка);
		Если Не ЗначениеЗаполнено(ЗаголовокЭлемента) Тогда
			ЗаголовокЭлемента = СтрокаДерева.Имя;
			РодительСИменем = СтрокаДерева.Родитель;
			Пока Истина
				И РодительСИменем <> Неопределено
				И Не ЗначениеЗаполнено(РодительСИменем.Имя) 
			Цикл
				РодительСИменем = РодительСИменем.Родитель;
			КонецЦикла; 
			Если Истина
				И РодительСИменем <> Неопределено 
				И Найти(ЗаголовокЭлемента, РодительСИменем.Имя) = 1 
			Тогда
				ЗаголовокЭлемента = Сред(ЗаголовокЭлемента, СтрДлина(РодительСИменем.Имя) + 1);
			КонецЕсли; 
			ЗаголовокЭлемента = ирОбщий.ПредставлениеИзИдентификатораЛкс(ЗаголовокЭлемента);
		КонецЕсли; 
	КонецЕсли; 
	Если ЗначениеЗаполнено(СтрокаДерева.ПредставлениеСочетаниеКлавиш) Тогда
		ЗаголовокЭлемента = ЗаголовокЭлемента + " (" + СтрокаДерева.ПредставлениеСочетаниеКлавиш + ")";
	КонецЕсли; 
	СтрокаДерева.Текст = ЗаголовокЭлемента;
	Если ЗначениеЗаполнено(Роль) Тогда
		СтрокаДерева.Текст = "[" + Роль + "] " + СтрокаДерева.Текст;
	КонецЕсли; 
	Если Не ЗначениеЗаполнено(СтрокаДерева.Текст) Тогда
		СтрокаДерева.Текст = "<" + СтрокаДерева.Имя + ">";
	КонецЕсли; 
	Если СтрокаДерева.Подсказка = СтрокаДерева.Текст Тогда
		СтрокаДерева.Подсказка = "";
	КонецЕсли;
	Попытка
		ТекстРасширеннойПодсказки = ЭлементФормы.РасширеннаяПодсказка.Заголовок;
	Исключение
		ТекстРасширеннойПодсказки = "";
	КонецПопытки;
	Если ЗначениеЗаполнено(ТекстРасширеннойПодсказки) Тогда
		СтрокаДерева.Подсказка = ТекстРасширеннойПодсказки;
	КонецЕсли; 
	Если СтрокаДерева.СочетаниеКлавиш <> Неопределено Тогда
		ПредставлениеСочетаниеКлавиш = ирОбщий.ПредставлениеСочетанияКлавишЛкс(СтрокаДерева.СочетаниеКлавиш);
		Если Не ирОбщий.СтрокиРавныЛкс(ПредставлениеСочетаниеКлавиш, "Нет") Тогда
			СтрокаДерева.ПредставлениеСочетаниеКлавиш = ПредставлениеСочетаниеКлавиш;
		КонецЕсли; 
	КонецЕсли; 
	Если мУсловноеОформлениеФормы <> Неопределено Тогда
		#Если Сервер И Не Сервер Тогда
			мУсловноеОформлениеФормы = Новый НастройкиКомпоновкиДанных;
		#КонецЕсли
		Если ТипЗнч(ЭлементФормы) = Тип("УправляемаяФорма") Тогда
			СтрокаДерева.ЕстьУсловноеОформление = мУсловноеОформлениеФормы.УсловноеОформление.Элементы.Количество();
		Иначе
			СсылающиесяЭлементыОформления = Новый Соответствие;
			ирОбщий.НайтиЭлементУсловногоОформленияПоПолюЛкс(мУсловноеОформлениеФормы.УсловноеОформление, ЭлементФормы.Имя,, СсылающиесяЭлементыОформления);
			СтрокаДерева.ЕстьУсловноеОформление = СсылающиесяЭлементыОформления.Количество();
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры

Процедура СортироватьСтроки(СтрокаДерева)
	Если Ложь
		Или СтрокаДерева = Дерево
		Или СтрокаДерева.Тип = Тип("СтраницаПанели")
	Тогда
		СтрокаДерева.Строки.Сортировать("Тип,Текст");
	КонецЕсли; 
	Для Каждого ДочерняяСтрока Из СтрокаДерева.Строки Цикл
		СортироватьСтроки(ДочерняяСтрока);
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьСтрокуЭлементаОбычнойФормы(СтрокаДерева, Роль = "")
	
	ЭлементФормы = СтрокаДерева.ЭлементФормы;
	СтрокаДерева.Тип = ТипЗнч(ЭлементФормы);
	СтрокаДерева.ПредставлениеТипа = "" + СтрокаДерева.Тип;
	ИсключаемыеСвойства = Новый Массив;
	Если ЗначениеЗаполнено(СтрокаДерева.Картинка) Тогда
		ИсключаемыеСвойства.Добавить("Картинка");
	КонецЕсли; 
	ЗаполнитьЗначенияСвойств(СтрокаДерева, ЭлементФормы,, ирОбщий.СтрСоединитьЛкс(ИсключаемыеСвойства));
	Если ТипЗнч(ЭлементФормы) = Тип("ПолеHTMLДокумента") Тогда
		СтрокаДерева.Доступность = Истина;
	КонецЕсли; 
	РезультирующиеПризнаки = РезультирующийПризнакСтрокиДерева(СтрокаДерева);
	ЗаполнитьЗначенияСвойств(СтрокаДерева, РезультирующиеПризнаки); 
	Свертка = ирОбщий.БезопасноПолучитьЗначениеСвойстваЛкс(ЭлементФормы, "Свертка");
	Если СтрокаДерева.Видимость И Свертка <> Неопределено И Свертка <> РежимСверткиЭлементаУправления.Нет Тогда
		СтрокаДерева.Видимость = Ложь;
	КонецЕсли; 
	Если Не ЗначениеЗаполнено(СтрокаДерева.Имя) Тогда
		СтрокаДерева.Имя = "" + ТипЗнч(ЭлементФормы);
	КонецЕсли; 
	Если ТипЗнч(ЭлементФормы) = Тип("Панель") Тогда
		ИмяКоллекции = "Страницы";
		ИмяСвойстваЗаголовка = "Заголовок";
		Если ЭлементФормы.ОтображениеЗакладок = ОтображениеЗакладок.НеИспользовать Тогда
			СтрокаДерева.СтараяТекущаяСтраница = ЭлементФормы.ТекущаяСтраница;
		КонецЕсли; 
	ИначеЕсли ТипЗнч(ЭлементФормы) = Тип("ТабличноеПоле") Тогда
		ИмяКоллекции = "Колонки";
		ИмяСвойстваЗаголовка = "ТекстШапки";
	ИначеЕсли ТипЗнч(ЭлементФормы) = Тип("КоманднаяПанель") Тогда
		ИмяКоллекции = "Кнопки";
		ИмяСвойстваЗаголовка = "Текст";
	ИначеЕсли ТипЗнч(ЭлементФормы) = Тип("КолонкаТабличногоПоля") Тогда
		ИмяСвойстваЗаголовка = "ТекстШапки";
		СтрокаДерева.Подсказка = ЭлементФормы.ПодсказкаВШапке;
	ИначеЕсли ТипЗнч(ЭлементФормы) = Тип("КнопкаКоманднойПанели") Тогда
		ИмяСвойстваЗаголовка = "Текст";
		СтрокаДерева.Видимость = СтрокаДерева.Родитель.Видимость;
	Иначе
		ИмяСвойстваЗаголовка = "Заголовок";
	КонецЕсли; 
	Если ЗначениеЗаполнено(ИмяКоллекции) Тогда
		ЗаголовокЭлемента = "";
		СчетчикНепустых = 0;
		Индекс = 0;
		Пока Индекс < ЭлементФормы[ИмяКоллекции].Количество() Цикл
			ЭлементКоллекции = ЭлементФормы[ИмяКоллекции][Индекс];
			ЗаголовокВложенного = ЭлементКоллекции[ИмяСвойстваЗаголовка];
			Индекс = Индекс + 1;
			Если Ложь
				Или Не ЗначениеЗаполнено(ЗаголовокВложенного) 
				Или (Истина
					И ИмяКоллекции = "Кнопки"
					И ЭлементКоллекции.Имя = "СтруктураКоманднойПанели")
			Тогда
				Продолжить;
			КонецЕсли; 
			Если ЗаголовокЭлемента <> "" Тогда
				ЗаголовокЭлемента = ЗаголовокЭлемента + ", ";
			КонецЕсли; 
			ЗаголовокЭлемента = ЗаголовокЭлемента + ЗаголовокВложенного;
			СчетчикНепустых = СчетчикНепустых + 1;
			Если СчетчикНепустых = 5 Тогда
				Прервать;
			КонецЕсли; 
		КонецЦикла;
	Иначе
		ЗаголовокЭлемента = ирОбщий.БезопасноПолучитьЗначениеСвойстваЛкс(ЭлементФормы, ИмяСвойстваЗаголовка);
	КонецЕсли; 
	Если ЗначениеЗаполнено(СтрокаДерева.ПредставлениеСочетаниеКлавиш) Тогда
		ЗаголовокЭлемента = ЗаголовокЭлемента + " (" + СтрокаДерева.ПредставлениеСочетаниеКлавиш + ")";
	КонецЕсли; 
	СтрокаДерева.Текст = ЗаголовокЭлемента;
	Если ЗначениеЗаполнено(Роль) Тогда
		СтрокаДерева.Текст = "[" + Роль + "] " + СтрокаДерева.Текст;
	КонецЕсли; 
	Если Не ЗначениеЗаполнено(СтрокаДерева.Текст) Тогда
		СтрокаДерева.Текст = "<" + СтрокаДерева.Имя + ">";
	КонецЕсли; 
	Если СтрокаДерева.Подсказка = СтрокаДерева.Текст Тогда
		СтрокаДерева.Подсказка = "";
	КонецЕсли; 
	Если Истина
		И СтрокаДерева.СочетаниеКлавиш <> Неопределено 
		И Не ЗначениеЗаполнено(СтрокаДерева.ПредставлениеСочетаниеКлавиш) 
	Тогда
		ПредставлениеСочетаниеКлавиш = ирОбщий.ПредставлениеСочетанияКлавишЛкс(СтрокаДерева.СочетаниеКлавиш);
		Если Не ирОбщий.СтрокиРавныЛкс(ПредставлениеСочетаниеКлавиш, "Нет") Тогда
			СтрокаДерева.ПредставлениеСочетаниеКлавиш = ПредставлениеСочетаниеКлавиш;
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры

Процедура ОбойтиУзелДереваЭлементовОбычнойФормы(КорневаяСтрокаФормы, Форма, Узел, СтрокаДЗ, КорневойЭлемент = Неопределено)
	
	Для каждого УзелЭлементаФормы Из Узел.ДочерниеУзлы Цикл
		Если УзелЭлементаФормы.ИмяУзла = "data" Тогда 
			Если ТипЗнч(СтрокаДЗ.ЭлементФормы) = Тип("Панель") Тогда
				Для каждого Страница Из СтрокаДЗ.ЭлементФормы.Страницы Цикл
					Если КорневойЭлемент = Неопределено Тогда
						//Если Не ПоказыватьНевидимые И Не Страница.Видимость Тогда
						//	Продолжить;
						//КонецЕсли; 
						СтрокаСтраница = СтрокаДЗ.Строки.Добавить();
						СтрокаСтраница.ЭлементФормы = Страница;
						ЗаполнитьСтрокуЭлементаОбычнойФормы(СтрокаСтраница);
					КонецЕсли; 
					Если Страница = КорневойЭлемент Тогда
						Прервать;
					КонецЕсли; 
				КонецЦикла;
			Иначе
				// У формы обработки ирЗагрузкаТабличныхДанных сюда попало поле табличного документа ТабличныйДокумент
			КонецЕсли; 
		Иначе
			СвойстваЭлФормы = УзелЭлементаФормы.ДочерниеУзлы;
			Если СвойстваЭлФормы.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли; 
			Если ТипЗнч(УзелЭлементаФормы.ПоследнийДочерний) = Тип("ТекстDOM") Тогда
				УзелЭлементаФормы.УдалитьДочерний(УзелЭлементаФормы.ПоследнийДочерний);
			КонецЕсли; 
			ЭтоПанель = СвойстваЭлФормы[СвойстваЭлФормы.Количество() - 1].ТекстовоеСодержимое <> "0";
			Если ирКэш.НомерВерсииПлатформыЛкс() <= 803008 Тогда
				СвойстваЭлФормыСИменем = СвойстваЭлФормы[СвойстваЭлФормы.Количество() - 3].ДочерниеУзлы;
				СвойстваЭлФормыСИндексом = СвойстваЭлФормы[СвойстваЭлФормы.Количество() - 5].ДочерниеУзлы;
				ИндексСтраницы = Число(СвойстваЭлФормыСИндексом[СвойстваЭлФормыСИндексом.Количество() - 10].ТекстовоеСодержимое);
				ЭлементФормы = Форма.ЭлементыФормы[СтрЗаменить(СвойстваЭлФормыСИменем[2].ТекстовоеСодержимое, """", "")];
			Иначе
				СвойстваЭлФормыСИменем = СвойстваЭлФормы[СвойстваЭлФормы.Количество() - 2].ДочерниеУзлы;
				СвойстваЭлФормыСИндексом = СвойстваЭлФормы[СвойстваЭлФормы.Количество() - 3].ДочерниеУзлы;
				ИндексСтраницы = Число(СвойстваЭлФормыСИндексом[СвойстваЭлФормыСИндексом.Количество() - 5].ТекстовоеСодержимое);
				ЭлементФормы = Форма.ЭлементыФормы[СтрЗаменить(СвойстваЭлФормыСИменем[1].ТекстовоеСодержимое, """", "")];
			КонецЕсли; 
			Если Ложь
				Или ТипЗнч(ЭлементФормы) = Тип("Разделитель")
				Или ТипЗнч(ЭлементФормы) = Тип("РамкаГруппы")
				Или ирОбщий.БезопасноПолучитьЗначениеСвойстваЛкс(ЭлементФормы, "Видимость") = Ложь 
			Тогда
				Продолжить;
			КонецЕсли; 
			Если КорневойЭлемент = Неопределено Тогда
				СтрокаРодителя = СтрокаДЗ.Строки[ИндексСтраницы];
			Иначе
				СтрокаРодителя = КорневаяСтрокаФормы;
			КонецЕсли; 
			НовСтрокаДЗ = СтрокаРодителя.Строки.Добавить();
			НовСтрокаДЗ.ЭлементФормы = ЭлементФормы;
			ЗаполнитьСтрокуЭлементаОбычнойФормы(НовСтрокаДЗ);
			Если ЭтоПанель Тогда
				ОбойтиУзелДереваЭлементовОбычнойФормы(КорневаяСтрокаФормы, Форма, СвойстваЭлФормы[СвойстваЭлФормы.Количество() - 1], НовСтрокаДЗ);
			КонецЕсли;
			Если НовСтрокаДЗ <> СтрокаДЗ Тогда
				КонтекстноеМеню = ирОбщий.БезопасноПолучитьЗначениеСвойстваЛкс(ЭлементФормы, "КонтекстноеМеню");
				Если КонтекстноеМеню <> Неопределено И КонтекстноеМеню.Кнопки.Количество() > 0 Тогда
					СтрокаКонтекстногоМеню = НовСтрокаДЗ.Строки.Добавить();
					СтрокаКонтекстногоМеню.ЭлементФормы = КонтекстноеМеню;
					ЗаполнитьСтрокуЭлементаОбычнойФормы(СтрокаКонтекстногоМеню);
					СтрокаКонтекстногоМеню.Текст = "[Контекстное меню] " + СтрокаКонтекстногоМеню.Текст;
					ЗаполнитьСтрокиИзКнопок(КонтекстноеМеню.Кнопки, СтрокаКонтекстногоМеню.Строки);
				КонецЕсли; 
				Если ТипЗнч(ЭлементФормы) = Тип("КоманднаяПанель") Тогда
					ЗаполнитьСтрокиИзКнопок(ЭлементФормы.Кнопки, НовСтрокаДЗ.Строки);
				ИначеЕсли ТипЗнч(ЭлементФормы) = Тип("ТабличноеПоле") Тогда
					Если ТипЗнч(ЭлементФормы.Значение) <> Тип("ОтборКомпоновкиДанных") Тогда
						Для Каждого КолонкаТабличногоПоля Из ЭлементФормы.Колонки Цикл
							Если Не КолонкаТабличногоПоля.Видимость И Не КолонкаТабличногоПоля.ИзменятьВидимость Тогда
								Продолжить;
							КонецЕсли; 
							СтрокаКолонки = НовСтрокаДЗ.Строки.Добавить();
							СтрокаКолонки.ЭлементФормы = КолонкаТабличногоПоля;
							ЗаполнитьСтрокуЭлементаОбычнойФормы(СтрокаКолонки);
						КонецЦикла;
					КонецЕсли; 
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокиИзКнопок(Кнопки, СтрокиДерева)
	
	Для Каждого Кнопка Из Кнопки Цикл
		Если Ложь
			Или Кнопка.ТипКнопки = ТипКнопкиКоманднойПанели.Разделитель 
			//Или Кнопка.Имя = "СтруктураКоманднойПанели"
		Тогда
			Продолжить;
		КонецЕсли; 
		СтрокаДерева = СтрокиДерева.Добавить();
		СтрокаДерева.ЭлементФормы = Кнопка;
		ЗаполнитьЗначенияСвойств(СтрокаДерева, Кнопка); 
		Если СтрокаДерева.Подсказка = СтрокаДерева.Текст Тогда
			СтрокаДерева.Подсказка = "";
		КонецЕсли; 
		ПредставлениеСочетаниеКлавиш = ирОбщий.ПредставлениеСочетанияКлавишЛкс(СтрокаДерева.СочетаниеКлавиш);
		Если "" + Кнопка.Действие = "Переместить вверх" Тогда
			ПредставлениеСочетаниеКлавиш = ПредставлениеСочетаниеКлавиш + "Up";
		ИначеЕсли "" + Кнопка.Действие = "Переместить вниз" Тогда
			ПредставлениеСочетаниеКлавиш = ПредставлениеСочетаниеКлавиш + "Down";
		ИначеЕсли "" + Кнопка.Действие = "Удалить" Тогда
			ПредставлениеСочетаниеКлавиш = ПредставлениеСочетаниеКлавиш + "Del";
		КонецЕсли; 
		Если Не ирОбщий.СтрокиРавныЛкс(ПредставлениеСочетаниеКлавиш, "Нет") Тогда
			СтрокаДерева.ПредставлениеСочетаниеКлавиш = ПредставлениеСочетаниеКлавиш;
		КонецЕсли; 
		// Тупик. Этот идентификатор оказался не от картинки
		//Если СтрокаДерева.Картинка.Вид = ВидКартинки.Пустая Тогда
		//	СтрокаВнутр = ЗначениеВСтрокуВнутр(Кнопка);
		//	// {"#",365b8b83-672d-4c9f-b2d7-5d8331db6c7c,
		//	//{8,"Действие",0,0,
		//	//{1,1,
		//	//{"ru","&Добавить"}
		//	//},0,6f8d70fc-66dc-47aa-84cd-dae08452935d,2511,99,0,0,1,0,1,0,0}
		//	//}
		//	СтрокаВнутр = ирОбщий.ПоследнийФрагментЛкс(СтрокаВнутр, "},");
		//	Фрагменты = ирОбщий.СтрРазделитьЛкс(СтрокаВнутр, ",");
		//	ИДКартинки = Фрагменты[1];
		//	Картинка = КартинкаИзБиблиотекиПоИдентификатору(ИДКартинки);
		//	Если Картинка <> Неопределено Тогда
		//		СтрокаДерева.Картинка = Картинка;
		//	КонецЕсли; 
		//КонецЕсли; 
		ЗаполнитьСтрокуЭлементаОбычнойФормы(СтрокаДерева);
		Если Кнопка.ТипКнопки = ТипКнопкиКоманднойПанели.Подменю Тогда
			ЗаполнитьСтрокиИзКнопок(Кнопка.Кнопки, СтрокаДерева.Строки);
		КонецЕсли; 
	КонецЦикла;

КонецПроцедуры

Процедура ДеревоПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки) Экспорт
	
	ирОбщий.ТабличноеПолеПриВыводеСтрокиЛкс(ЭтаФорма, Элемент, ОформлениеСтроки, ДанныеСтроки);
	Если ДанныеСтроки.Картинка <> Неопределено Тогда
		Картинка = ДанныеСтроки.Картинка;
	КонецЕсли; 
	Если Ложь
		Или Картинка = Неопределено
		Или Картинка.Вид = ВидКартинки.Пустая
	Тогда 
		СтруктураТипа = ирКэш.Получить().СтруктураТипаИзКонкретногоТипа(ДанныеСтроки.Тип);
		ИмяОбщегоТипа = СтруктураТипа.ИмяОбщегоТипа;
		Если ИмяОбщегоТипа = "ТаблицаФормы" Тогда
			ИмяОбщегоТипа = "ТабличноеПоле";
		ИначеЕсли ИмяОбщегоТипа = "КнопкаФормы" Тогда
			ИмяОбщегоТипа = "Кнопка";
		ИначеЕсли Ложь
			Или ИмяОбщегоТипа = "УправляемаяФорма" 
			Или ИмяОбщегоТипа = "ФормаКлиентскогоПриложения" 
		Тогда
			ИмяОбщегоТипа = "Форма";
		ИначеЕсли ИмяОбщегоТипа = "ГруппаФормы" Тогда 
			Если Ложь
				Или ДанныеСтроки.ЭлементФормы.Вид = ВидГруппыФормы.КоманднаяПанель
				Или ДанныеСтроки.ЭлементФормы.Вид = ВидГруппыФормы.КонтекстноеМеню 
			Тогда
				ИмяОбщегоТипа = "КоманднаяПанель";
			ИначеЕсли ДанныеСтроки.ЭлементФормы.Вид = ВидГруппыФормы.Страницы Тогда
				ИмяОбщегоТипа = "Панель";
			ИначеЕсли ДанныеСтроки.ЭлементФормы.Вид = ВидГруппыФормы.Страница Тогда
				ИмяОбщегоТипа = "СтраницаПанели";
			КонецЕсли; 
		КонецЕсли; 
		Картинка = ирОбщий.КартинкаКорневогоТипаМДЛкс(ИмяОбщегоТипа);
	КонецЕсли; 
	ОформлениеСтроки.Ячейки.Текст.УстановитьКартинку(Картинка);
	Если ДанныеСтроки.ТолькоВЕще Тогда
		ОформлениеСтроки.ЦветТекста = WebЦвета.Коричневый;
	КонецЕсли; 
	Если Не ДанныеСтроки.Видимость Тогда
		ОформлениеСтроки.ЦветТекста = Новый Цвет(100, 100, 100);
	ИначеЕсли Не ДанныеСтроки.Доступность Тогда
		ОформлениеСтроки.ЦветТекста = Новый Цвет(50, 50, 50);
	КонецЕсли;
	Если ТипЗнч(ДанныеСтроки) = Тип("СтрокаДереваЗначений") Тогда
		ирОбщий.ОформитьСтрокуВТабличномПолеДереваСПоискомЛкс(Элемент, ОформлениеСтроки, ДанныеСтроки, мСтруктураПоискаВДереве);
	КонецЕсли; 
	
КонецПроцедуры

Функция РезультирующийПризнакСтрокиДерева(Знач СтрокаДерева)

	Результат = Новый Структура("Видимость, Доступность, ТолькоВЕще");
	ЗаполнитьЗначенияСвойств(Результат, СтрокаДерева); 
	Пока Истина
		И СтрокаДерева <> Неопределено 
		И (Ложь
			Или Результат.Видимость
			Или Результат.Доступность
			Или Не Результат.ТолькоВЕще)
	Цикл
		Если Не СтрокаДерева.Видимость Тогда
			Результат.Видимость = Ложь;
		КонецЕсли; 
		Если Не СтрокаДерева.Доступность Тогда
			Результат.Доступность = Ложь;
		КонецЕсли; 
		Если СтрокаДерева.ТолькоВЕще Тогда
			Результат.ТолькоВЕще = Истина;
		КонецЕсли; 
		СтрокаДерева = СтрокаДерева.Родитель;
	КонецЦикла; 
	Возврат Результат;

КонецФункции
 
Процедура ПриЗакрытии()
	
	ирОбщий.Форма_ПриЗакрытииЛкс(ЭтаФорма);
	ВосстановитьСвойстваЭлементовФормы(Истина);
	мТекущаяСтрока = Неопределено;
	
КонецПроцедуры

Процедура ДеревоПриАктивизацииСтроки(Элемент)
	
	ирОбщий.ТабличноеПолеПриАктивизацииСтрокиЛкс(ЭтаФорма, Элемент);
	ВосстановитьСвойстваЭлементовФормы();
	ЭтаФорма.ПутьВДереве = ПутьКСтроке(Элемент.ТекущаяСтрока);
	мТекущаяСтрока = ЭлементыФормы.Дерево.ТекущаяСтрока;
	ЭтаФорма.ОписаниеТекущейСтроки = мТекущаяСтрока.Подсказка;
	Если ЗначениеЗаполнено(ОписаниеТекущейСтроки) Тогда
		ЭтаФорма.ОписаниеТекущейСтроки = ОписаниеТекущейСтроки + Символы.ПС;
	КонецЕсли; 
	ЭтаФорма.ОписаниеТекущейСтроки = ОписаниеТекущейСтроки + мТекущаяСтрока.Текст;
	
	Если мУсловноеОформлениеФормы <> Неопределено Тогда
		ЭлементФормы = мТекущаяСтрока.ЭлементФормы;
		Если ТипЗнч(ЭлементФормы) = Тип("УправляемаяФорма") Тогда
			Компоновщик.ЗагрузитьНастройки(мУсловноеОформлениеФормы);
		Иначе
			СсылающиесяЭлементыОформления = Новый Соответствие;
			ирОбщий.НайтиЭлементУсловногоОформленияПоПолюЛкс(мУсловноеОформлениеФормы.УсловноеОформление, ЭлементФормы.Имя,, СсылающиесяЭлементыОформления);
			ЭлементДляКопирования = ирОбщий.СоответствиеВМассивЛкс(СсылающиесяЭлементыОформления, Ложь, Истина);
			ирОбщий.СкопироватьЭлементыКомпоновкиЛкс(Компоновщик.Настройки.УсловноеОформление, мУсловноеОформлениеФормы.УсловноеОформление,,, ЭлементДляКопирования);
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Функция ПутьКСтроке(СтрокаДерева)
	
	Если СтрокаДерева = Неопределено Тогда
		Возврат "";
	КонецЕсли; 
	Исключения = Новый Массив;
	Исключения.Добавить(Новый Структура("Тип, Текст, СвойстваДляСравнения", Тип("Панель"), "Панель", "Тип"));
	Исключения.Добавить(Новый Структура("Тип, Текст, СвойстваДляСравнения", Тип("ТабличноеПоле"), "Табличное поле", "Тип"));
	Исключения.Добавить(Новый Структура("Тип, Текст, СвойстваДляСравнения", Тип("КоманднаяПанель"), "Командная панель", "Тип"));
	ПутьКСтроке = ирОбщий.Дерево_ПутьСтрокойЛкс(СтрокаДерева, "Текст", Истина, " \ ",, Исключения);
	Возврат ПутьКСтроке;

КонецФункции

Процедура ВосстановитьСвойстваЭлементовФормы(ВосстанавливатьТекущиеСтраницы = Ложь)
	
	Если мТекущаяСтрока <> Неопределено Тогда
		ЭлементФормы = ЭлементФормыСтрокиДерева(мТекущаяСтрока);
		//Попытка
		//	ЭлементФормы.Видимость = мТекущаяСтрока.Видимость;
		//Исключение
		//КонецПопытки; 
	КонецЕсли; 
	ВсеСтрокиДерева = ирОбщий.ВсеСтрокиДереваЗначенийЛкс(Дерево);
	Для Каждого СтрокаДерева Из ВсеСтрокиДерева Цикл
		ВосстановитьСтарыеСвойстваЭлементаФормы(СтрокаДерева, Истина); 
		Если ВосстанавливатьТекущиеСтраницы И СтрокаДерева.СтараяТекущаяСтраница <> Неопределено Тогда
			СтрокаДерева.ЭлементФормы.ТекущаяСтраница = СтрокаДерева.СтараяТекущаяСтраница;
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

Процедура СтрокаПоискаПриИзменении(Элемент)
	
	ирОбщий.ПолеВводаСИсториейВыбора_ПриИзмененииЛкс(Элемент, ЭтаФорма);
	НайтиСтрокиДерева();
	
КонецПроцедуры

Процедура СтрокаПоискаНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	ирОбщий.ПолеВводаСИсториейВыбора_НачалоВыбораИзСпискаЛкс(Элемент, ЭтаФорма);
	
КонецПроцедуры

Процедура ВпередНажатие(Элемент)
	
	ирОбщий.СледующееВхождениеСтрокиПоискаВТабличномПолеДереваЛкс(ЭлементыФормы.Дерево, мСтруктураПоискаВДереве);
	
КонецПроцедуры

Процедура НазадНажатие(Элемент)
	
	ирОбщий.ПредыдущееВхождениеСтрокиПоискаВТабличномПолеДереваЛкс(ЭлементыФормы.Дерево, мСтруктураПоискаВДереве);

КонецПроцедуры

Процедура ДеревоВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Колонка = ЭлементыФормы.Дерево.Колонки.Текст Тогда
		Если РежимПодсистемы Тогда
			СтрокаОбъекта = ВыбраннаяСтрока;
			Пока СтрокаОбъекта.Родитель <> Неопределено Цикл
				СтрокаОбъекта = СтрокаОбъекта.Родитель;
			КонецЦикла;
			СтрокаОсновнойФормы = СтрокаОбъекта.Строки.Найти(Истина, "Основной");
			Если СтрокаОсновнойФормы <> Неопределено Тогда 
				Форма = СтрокаОсновнойФормы.ЭлементФормы;
				Если Не Форма.Открыта() Тогда
					Форма.Открыть();
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли; 
		ПоказатьСвязаннуюФорму();
	ИначеЕсли Колонка = ЭлементыФормы.Дерево.Колонки.Подсказка Тогда
		ОткрытьТекстПодсказки(ВыбраннаяСтрока.Подсказка);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОткрытьТекстПодсказки(Знач Текст)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ирОбщий.ОткрытьТекстЛкс(Текст,,, Истина, ЭлементыФормы.Дерево.ТекущаяСтрока);
	КонецЕсли;

КонецПроцедуры

Процедура ПоказатьСвязаннуюФорму()
	
	Если Форма <> Неопределено Тогда
		ирОбщий.Форма_АктивироватьОткрытьЛкс(Форма);
	КонецЕсли; 
	ЭтаФорма.Активизировать();

КонецПроцедуры

Процедура КоманднаяПанельДереваКонсольКода(Кнопка)
	
	Если ЭлементыФормы.Дерево.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ирОбщий.ПредложитьЗакрытьМодальнуюФормуЛкс(ЭтаФорма);
	ТекущаяСтрока = ЭлементыФормы.Дерево.ТекущаяСтрока;
	ПрограммныйКод = "Форма.";
	Если ТипЗнч(Форма) = Тип("Форма") Тогда
		Если ТекущаяСтрока.Тип <> Тип("Форма") Тогда
			ПутьКЭлементу = "";
			Пока Истина Цикл 
				Если ТекущаяСтрока.Тип = Тип("Форма") Тогда 
					ПутьКЭлементу = "Форма.ЭлементыФормы." + ПутьКЭлементу;
					Прервать;
				ИначеЕсли Ложь
					Или ТекущаяСтрока.Тип = Тип("Панель")
					Или ТекущаяСтрока.Тип = Тип("СтраницаПанели")
				Тогда 
					//
				ИначеЕсли ТекущаяСтрока.Тип = Тип("КнопкаКоманднойПанели") Тогда 
					ПутьКЭлементу = ".Кнопки." + ТекущаяСтрока.Имя + ПутьКЭлементу;
				ИначеЕсли ТекущаяСтрока.Тип = Тип("КолонкаТабличногоПоля") Тогда 
					ПутьКЭлементу = ".Колонки." + ТекущаяСтрока.Имя + ПутьКЭлементу;
				Иначе
					ПутьКЭлементу = ТекущаяСтрока.Имя + ПутьКЭлементу;
				КонецЕсли; 
				ТекущаяСтрока = ТекущаяСтрока.Родитель;
			КонецЦикла; 
			ПрограммныйКод = ПутьКЭлементу;
		КонецЕсли; 
	Иначе
		Если ТекущаяСтрока.Тип <> Тип("УправляемаяФорма") Тогда
			ПрограммныйКод = "Форма.Элементы." + ТекущаяСтрока.Имя;
		КонецЕсли; 
	КонецЕсли; 
	ирОбщий.ОперироватьСтруктуройЛкс(ПрограммныйКод, , Новый Структура("Форма", Форма));

КонецПроцедуры

Процедура КоманднаяПанельДереваИсследоватьЭлементФормы(Кнопка)
	
	Если ЭлементыФормы.Дерево.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ирОбщий.ПредложитьЗакрытьМодальнуюФормуЛкс(ЭтаФорма);
	ТекущаяСтрока = ЭлементыФормы.Дерево.ТекущаяСтрока;
	ирОбщий.ИсследоватьЛкс(ЭлементФормыСтрокиДерева(ТекущаяСтрока, Истина));
	
КонецПроцедуры

Процедура КоманднаяПанельДереваСсылкаНаМодуль(Кнопка)
	
	ирОбщий.ПоказатьСсылкуНаМодульКонфигурацииЛкс(Дерево.Строки[0].Имя + ".Форма");
	
КонецПроцедуры

Процедура АктивнаяПодсветкаПриИзменении(Элемент)
	
	ИндикацияТекущегоЭлемента();
	
КонецПроцедуры

Процедура КоманднаяПанельДереваХранилищеНастроекФормы(Кнопка)
	
	ирОбщий.ПредложитьЗакрытьМодальнуюФормуЛкс(ЭтаФорма);
	ФормаРедактора = ирОбщий.ПолучитьФормуЛкс("Обработка.ирРедакторХранилищНастроек.Форма.Форма",);
	Если ТипЗнч(Форма) = Тип("Форма") Тогда
		ФормаРедактора.ЭлементыФормы.ОписаниеНастроек.ОтборСтрок.КлючНастроек.Значение = ирОбщий.КлючХраненияНастроекФормыЛкс(Форма);
	Иначе
		ФормаРедактора.ЭлементыФормы.ОписаниеНастроек.ОтборСтрок.ИмяОбъекта.Значение = Дерево.Строки[0].Имя;
	КонецЕсли; 
	ФормаРедактора.Открыть();
	
КонецПроцедуры

Процедура КлсКомандаНажатие(Кнопка)
	
	ирОбщий.УниверсальнаяКомандаФормыЛкс(ЭтаФорма, Кнопка);
	
КонецПроцедуры

Процедура ПодсказкаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьТекстПодсказки(ОписаниеТекущейСтроки);

КонецПроцедуры

Процедура СтрокаПоискаАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	
	//ирОбщий.ПромежуточноеОбновлениеСтроковогоЗначенияПоляВводаЛкс(Элемент, Текст); // Иначе иногда это событие начинало циклически вызываться
	НайтиСтрокиДерева(Текст);
	
КонецПроцедуры

Процедура НайтиСтрокиДерева(Знач ИскомаяСтрока = Неопределено)
	
	Если ИскомаяСтрока = Неопределено Тогда
		ИскомаяСтрока = СтрокаПоиска;
	КонецЕсли; 
	ирОбщий.ПрименитьСтрокуПоискаКТабличномуПолюДереваЛкс(ЭлементыФормы.Дерево, ИскомаяСтрока, "Текст, Подсказка", мСтруктураПоискаВДереве,, ЭлементыФормы.Найденные);
	ПоказатьСвернутьНайденные(); // разворачиваем до заполнения путей, иначе первый раз поле в своей жизни табличное поле не отображает строки
	Для Каждого НайденнаяСтрока Из Найденные Цикл
		НайденнаяСтрока.Путь = ПутьКСтроке(НайденнаяСтрока.СтрокаДерева.Родитель);
	КонецЦикла; 
	ЭлементыФормы.НадписьНайдено.Заголовок = "Найдено: " + Найденные.Количество();

КонецПроцедуры

Процедура ВнешнееСобытие(Источник, Событие, Данные) Экспорт
	
	ирОбщий.Форма_ВнешнееСобытиеЛкс(ЭтаФорма, Источник, Событие, Данные);

КонецПроцедуры

Процедура ТабличноеПолеПриПолученииДанных(Элемент, ОформленияСтрок) Экспорт 
	
	ирОбщий.ТабличноеПолеПриПолученииДанныхЛкс(ЭтаФорма, Элемент, ОформленияСтрок);

КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник) Экспорт 
	
	ирОбщий.Форма_ОбработкаОповещенияЛкс(ЭтаФорма, ИмяСобытия, Параметр, Источник); 
	
КонецПроцедуры

Процедура ПоказатьСвернутьНайденные()
	
	ирОбщий.ИзменитьСвернутостьЛкс(ЭтаФорма, Найденные.Количество() > 0, ЭлементыФормы.Найденные, ЭлементыФормы.РазделительГоризонтальныйПодНайденными, ЭтаФорма.Панель, "верх");
	
КонецПроцедуры

Процедура СвернутьПанели()
	ПоказатьСвернутьНайденные();
КонецПроцедуры
	
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Не Отказ Тогда
		ПоказатьСвернутьНайденные();
	КонецЕсли; 

КонецПроцедуры

Процедура НайденныеПриАктивизацииСтроки(Элемент)
	
	ирОбщий.ТабличноеПолеПриАктивизацииСтрокиЛкс(ЭтаФорма, Элемент);
	ЭлементыФормы.Дерево.ТекущаяСтрока = ЭлементыФормы.Найденные.ТекущаяСтрока.СтрокаДерева; 
	
КонецПроцедуры

Процедура КоманднаяПанельДереваКРодителю(Кнопка)
	
	ТекущаяСтрока = ЭлементыФормы.Дерево.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	Если ТекущаяСтрока.Родитель <> Неопределено Тогда
		ЭлементыФормы.Дерево.ТекущаяСтрока = ТекущаяСтрока.Родитель;
	КонецЕсли; 
	
КонецПроцедуры

Процедура УсловноеОформлениеПриАктивизацииСтроки(Элемент)
	
	ирОбщий.ТабличноеПолеПриАктивизацииСтрокиЛкс(ЭтаФорма, Элемент);
	
КонецПроцедуры

Процедура УсловноеОформлениеПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ирОбщий.УсловноеОформлениеПриВыводеСтрокиЛкс(ЭтаФорма, Элемент, ОформлениеСтроки, ДанныеСтроки);
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	ирОбщий.Форма_ОбновлениеОтображенияЛкс(ЭтаФорма);
	
КонецПроцедуры

ирОбщий.ИнициироватьФормуЛкс(ЭтаФорма, "Обработка.ирПлатформа.Форма.СтруктураФормы");
Дерево.Колонки.Добавить("Картинка");
Дерево.Колонки.Добавить("Тип");
Дерево.Колонки.Добавить("СочетаниеКлавиш");
Дерево.Колонки.Добавить("СтарыеСвойства");
Дерево.Колонки.Добавить("СтарыеСвойстваТекущейСтроки");
Дерево.Колонки.Добавить("Основной", Новый ОписаниеТипов("Булево"));
Дерево.Колонки.Добавить("ЭлементФормы");
Дерево.Колонки.Добавить("СтараяТекущаяСтраница");
Найденные.Колонки.Добавить("СтрокаДерева");
Найденные.Колонки.Добавить("Картинка");
Найденные.Колонки.Добавить("Тип");
ЭтаФорма.АктивнаяПодсветка = Истина;
Если КлючУникальности = "ВсеИнструменты" Тогда
	ЭтаФорма.КлючСохраненияПоложенияОкна = КлючУникальности;
	ЭтаФорма.СоединяемоеОкно = Истина;
	ЭтаФорма.СостояниеОкна = ВариантСостоянияОкна.Прикрепленное;
	ЭтаФорма.ПоложениеПрикрепленногоОкна = ВариантПрикрепленияОкна.Лево;
Иначе
	ЭтаФорма.КлючСохраненияПоложенияОкна = "";
	ЭтаФорма.СостояниеОкна = ВариантСостоянияОкна.Обычное;
	ЭтаФорма.СоединяемоеОкно = Ложь;
КонецЕсли;
