
Процедура ОбновитьДеревоПредставления() Экспорт

	ЭлементыФормы.СериализацияШирокая.УстановитьТекст(XMLНастройки);
	ЭтаФорма.ДлинаТекста = СтрДлина(XMLНастройки);
	ЗначениеДеревом.Строки.Очистить();
	ЭлементыФормы.ЗначениеДеревом.Колонки.Очистить();
	Попытка
		XMLДокумент = Новый ЧтениеXML;
		XMLДокумент.УстановитьСтроку(XMLНастройки);
		XMLДокумент.Прочитать();	
		ДеревоXMLДокумента = Новый ДеревоЗначений;
		ОписаниеСписокАтрибутов = Новый ОписаниеТипов("СписокЗначений");
		ДеревоXMLДокумента.Колонки.Очистить();
		ДеревоXMLДокумента.Колонки.Добавить("ТипУзла");
		ДеревоXMLДокумента.Колонки.Добавить("ИмяXMLЭлементаДанных");
		ДеревоXMLДокумента.Колонки.Добавить("ЗначениеXMLЭлементаДанных");
		ДеревоXMLДокумента.Колонки.Добавить("СписокАтрибутов", ОписаниеСписокАтрибутов);
		ЗагрузитьРекурсивно(XMLДокумент, ДеревоXMLДокумента.Строки);
		ЗначениеДеревом = ДеревоXMLДокумента;
		ЭлементыФормы.ЗначениеДеревом.СоздатьКолонки();
		ЭлементыФормы.ЗначениеДеревом.Колонки.ТипУзла.Видимость = Ложь;
		ЭлементыФормы.ЗначениеДеревом.Колонки.ИмяXMLЭлементаДанных.ОтображатьИерархию = Истина;
		ЭлементыФормы.ПанельОсн.Страницы.СтраницаДерево.Видимость = Истина;
		Для каждого СтрокаКорня Из ЗначениеДеревом.Строки Цикл
			ЭлементыФормы.ЗначениеДеревом.Развернуть(СтрокаКорня, Истина);
		КонецЦикла; 
	Исключение
		ЭлементыФормы.ПанельОсн.Страницы.СтраницаДерево.Видимость = Ложь;
	КонецПопытки; 
	
КонецПроцедуры

Процедура ЗагрузитьРекурсивно(XMLДокумент, СтрокаДерева) 
	
	Пока XMLДокумент.Прочитать() Цикл
		Если XMLДокумент.ТипУзла = ТипУзлаXML.ИнструкцияОбработки Тогда
			НоваяСрокаДерева = СтрокаДерева.Добавить();
			НоваяСрокаДерева.ТипУзла = XMLДокумент.ТипУзла;
			НоваяСрокаДерева.ИмяXMLЭлементаДанных = XMLДокумент.Имя;
			НоваяСрокаДерева.ЗначениеXMLЭлементаДанных = XMLДокумент.Значение;
		ИначеЕсли XMLДокумент.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			НоваяСрокаДерева = СтрокаДерева.Добавить();
			НоваяСрокаДерева.ТипУзла = XMLДокумент.ТипУзла;
			НоваяСрокаДерева.ИмяXMLЭлементаДанных = XMLДокумент.Имя;
			НоваяСрокаДерева.ЗначениеXMLЭлементаДанных = XMLДокумент.Значение;
			Пока XMLДокумент.ПрочитатьАтрибут() Цикл
				НоваяСрокаДерева.СписокАтрибутов.Добавить(XMLДокумент.Имя, XMLДокумент.Значение);
			КонецЦикла;
			ЗагрузитьРекурсивно(XMLДокумент, НоваяСрокаДерева.Строки);
		ИначеЕсли XMLДокумент.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			Возврат; Прервать;
		ИначеЕсли XMLДокумент.ТипУзла = ТипУзлаXML.Ничего Тогда
			Возврат; Прервать;
		ИначеЕсли XMLДокумент.ТипУзла = ТипУзлаXML.Текст Тогда
			Если СтрокаДерева.Количество() = 0 И СтрокаДерева.Родитель <> Неопределено Тогда
				СтрокаДерева.Родитель.ЗначениеXMLЭлементаДанных = XMLДокумент.Значение;
			Иначе
				НоваяСрокаДерева = СтрокаДерева.Добавить();
				НоваяСрокаДерева.ТипУзла = XMLДокумент.ТипУзла;
				НоваяСрокаДерева.ИмяXMLЭлементаДанных = XMLДокумент.Имя;
				НоваяСрокаДерева.ЗначениеXMLЭлементаДанных = XMLДокумент.Значение;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ирОбщий.Форма_ПриОткрытииЛкс(ЭтаФорма);
	ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.ЗначениеНастройки;
	Если Не ЗначениеЗаполнено(XMLНастройки) Тогда
		XMLНастройки = ЗначениеНастройки;
	КонецЕсли; 
	ОбновитьДеревоПредставления();
	
КонецПроцедуры

Процедура ПриПовторномОткрытии(СтандартнаяОбработка)
	
	ОбновитьДеревоПредставления();
	
КонецПроцедуры

Процедура ОсновныеДействияФормыИсследовать(Кнопка)
	
	ирОбщий.ИсследоватьЛкс(ЗначениеНастройки);
	
КонецПроцедуры

Процедура ОсновныеДействияФормыСравнить(Кнопка)
	
	ирОбщий.СравнитьСодержимоеЭлементаУправленияЛкс(ЭтаФорма, ЭлементыФормы.СериализацияШирокая);

КонецПроцедуры

Процедура КлсКомандаНажатие(Кнопка) Экспорт 
	
	ирОбщий.УниверсальнаяКомандаФормыЛкс(ЭтаФорма, Кнопка);
	
КонецПроцедуры

Процедура ОбработчикОжиданияСПараметрамиЛкс() Экспорт 
	
	ирОбщий.ОбработчикОжиданияСПараметрамиЛкс();

КонецПроцедуры

Процедура ПриЗакрытии()
	ирОбщий.Форма_ПриЗакрытииЛкс(ЭтаФорма);
КонецПроцедуры

Процедура ТабличноеПолеПриПолученииДанных(Элемент, ОформленияСтрок) Экспорт 
	
	ирОбщий.ТабличноеПолеПриПолученииДанныхЛкс(ЭтаФорма, Элемент, ОформленияСтрок);

КонецПроцедуры

Процедура ВнешнееСобытие(Источник, Событие, Данные) Экспорт 
	
	ирОбщий.Форма_ВнешнееСобытиеЛкс(ЭтаФорма, Источник, Событие, Данные);
	
КонецПроцедуры

ирОбщий.ИнициироватьФормуЛкс(ЭтаФорма, "Обработка.ирРедакторХранилищНастроек.Форма.СериализацияЗначения");
